!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the Microphysics scheme

module mphys_alg_mod

  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  ! xios output
  use io_config_mod,        only: write_diag, use_xios_io
  use constants_mod,        only: r_def, i_def

  use geometric_constants_mod, only: get_height
  use physical_op_constants_mod, only: get_dz_at_wtheta
  use fs_continuity_mod,       only: W3, Wtheta

  implicit none

  private
  public mphys_alg

contains
  !>@brief Run the UM Microphysics scheme
  !>@details The UM Microphysics scheme calculates:
  !>             1) Precipitation rates output to the surface
  !>                and other physics schemes.
  !>             2) Increments to the large scale prognostics
  !>                due to cloud microphysical processes
  !>                (e.g. latent heating and cooling).
  !>         See UMDP26 for full scheme details
  !>@param[in]     mr_n           Start of timestep mixing ratios
  !>@param[in]     theta          Potential temperature (theta) in wth space
  !>@param[in]     dry_rho_in_w3  Dry density in the w3 space
  !>@param[in]     derived_fields Group of derived fields
  !>@param[in,out] microphysics_fields    Fields for mphys scheme
  !>@param[in]     cloud_fields           Fields for cloud scheme
  !>@param[in]     aerosol_fields         Fields for aerosol scheme
  !>@param[in]     turbulence_fields      Fields for turbulence scheme
  !>@param[in]     f_arr                  Fractional standard dev params
  !>@param[in,out] dmr_mphys      Mixing ratio increment due to microphysics
  !>@param[in,out] theta_inc      Potential temperature increment in wth space
  !>@param[in,out] dcfl           Increment to liquid cloud fraction
  !>@param[in,out] dcff           Increment to ice cloud fraction
  !>@param[in,out] dbcf           Increment to bulk cloud fraction
  !>@param[in]     dt             The model timestep length
  subroutine mphys_alg( mr_n, theta, dry_rho_in_w3, derived_fields,            &
                        microphysics_fields, cloud_fields, aerosol_fields,     &
                        turbulence_fields, f_arr, dmr_mphys, theta_inc, dcfl,  &
                        dcff, dbcf, dt )

    use pc2_conv_coupling_kernel_mod, only: pc2_conv_coupling_kernel_type
    use mphys_kernel_mod, only: mphys_kernel_type
    use mphys_turb_gen_kernel_mod, only: mphys_turb_gen_kernel_type
    use mr_indices_mod,   only: imr_v, imr_cl, imr_r, imr_ci, imr_s, imr_g, nummr
    use cloud_config_mod, only: mphys_erosion
    use microphysics_config_mod, only: turb_gen_mixph

    use io_config_mod,             only: subroutine_timers
    use timer_mod,                 only: timer
    use log_mod,                   only: log_event, LOG_LEVEL_INFO

    implicit none

    type( field_type ), intent( in )    :: mr_n ( nummr ), theta, dry_rho_in_w3

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(inout) :: microphysics_fields
    type( field_collection_type ), intent(in)    :: cloud_fields
    type( field_collection_type ), intent(in)    :: aerosol_fields
    type( field_collection_type ), intent(in)    :: turbulence_fields
    type( field_type ),            intent(in)    :: f_arr
    real( kind=r_def ),            intent(in)    :: dt

    type( field_type ), intent( inout ) :: dmr_mphys ( nummr )
    type( field_type ), intent( inout ) :: theta_inc
    type( field_type ), intent( inout ) :: dcfl, dcff, dbcf

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth        => null()
    type( field_type ), pointer :: u1_in_w3            => null()
    type( field_type ), pointer :: u2_in_w3            => null()
    type( field_type ), pointer :: w_physics           => null()
    type( field_type ), pointer :: wetrho_in_w3        => null()
    type( field_type ), pointer :: rho_in_wth          => null()

    type( field_type ), pointer :: cf_in               => null()
    type( field_type ), pointer :: cfl_in              => null()
    type( field_type ), pointer :: cff_in              => null()

    type( field_type ), pointer :: dmv_mphys           => null()
    type( field_type ), pointer :: ls_rain             => null()
    type( field_type ), pointer :: ls_snow             => null()
    type( field_type ), pointer :: lsca_2d             => null()

    type( field_type ), pointer :: cloud_drop_no_conc  => null()

    type( field_type ), pointer :: wvar   => null()

    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: dz_in_wth => null()

    integer(kind=i_def) :: mesh_id

    if ( subroutine_timers ) call timer("mphys_alg")

    call log_event( 'slow_physics: Running Microphysics', LOG_LEVEL_INFO )

    mesh_id = theta%get_mesh_id()

    call theta%copy_field_properties(dcfl)
    call theta%copy_field_properties(dcff)
    call theta%copy_field_properties(dbcf)
    call invoke( setval_c(dcfl,      0.0_r_def), &
                 setval_c(dcff,      0.0_r_def), &
                 setval_c(dbcf,      0.0_r_def), &
                 setval_c(theta_inc, 0.0_r_def) )

    ! Unpack fields
    exner_in_wth       => derived_fields%get_field('exner_in_wth')
    u1_in_w3           => derived_fields%get_field('u1_in_w3')
    u2_in_w3           => derived_fields%get_field('u2_in_w3')
    w_physics          => derived_fields%get_field('w_physics')
    wetrho_in_w3       => derived_fields%get_field('wetrho_in_w3')
    rho_in_wth         => derived_fields%get_field('rho_in_wth')

    cf_in              => cloud_fields%get_field('bulk_fraction')
    cfl_in             => cloud_fields%get_field('liquid_fraction')
    cff_in             => cloud_fields%get_field('ice_fraction')

    ls_rain            => microphysics_fields%get_field('ls_rain')
    ls_snow            => microphysics_fields%get_field('ls_snow')
    lsca_2d            => microphysics_fields%get_field('lsca_2d')

    cloud_drop_no_conc => aerosol_fields%get_field('cloud_drop_no_conc')

    wvar               => turbulence_fields%get_field('wvar')

    height_wth => get_height(Wtheta, mesh_id)
    height_w3 => get_height(W3, mesh_id)

    if (mphys_erosion) then
      call invoke( pc2_conv_coupling_kernel_type(theta, mr_n(imr_v),           &
                                                 mr_n(imr_cl), mr_n(imr_ci),   &
                                                 cfl_in, cff_in, cf_in,        &
                                                 exner_in_wth, theta_inc,      &
                                                 dmr_mphys(imr_v),             &
                                                 dmr_mphys(imr_cl),            &
                                                 dmr_mphys(imr_ci),            &
                                                 dcfl, dcff, dbcf, dt) )
    end if

    call invoke( mphys_kernel_type ( mr_n(imr_v), mr_n(imr_cl), mr_n(imr_ci),  &
                                     mr_n(imr_r), mr_n(imr_g),                 &
                                     cf_in, cfl_in, cff_in,                    &
                                     u1_in_w3, u2_in_w3, w_physics,            &
                                     theta, exner_in_wth,                      &
                                     wetrho_in_w3, dry_rho_in_w3,              &
				     height_w3, height_wth,                    &
                                     cloud_drop_no_conc,                       &
                                     dmr_mphys(imr_v),  dmr_mphys(imr_cl),     &
                                     dmr_mphys(imr_ci), dmr_mphys(imr_r),      &
                                     dmr_mphys(imr_g), ls_rain, ls_snow,       &
                                     lsca_2d,                                  &
                                     theta_inc, dcfl, dcff, dbcf, f_arr ) )

    if (turb_gen_mixph) then
      dz_in_wth => get_dz_at_wtheta()
      call invoke( mphys_turb_gen_kernel_type(theta, mr_n(imr_v), mr_n(imr_cl),&
                                              mr_n(imr_ci), mr_n(imr_s),       &
                                              cfl_in, cff_in, cf_in,           &
                                              exner_in_wth, rho_in_wth, wvar,  &
                                              dz_in_wth,                       &
                                              theta_inc, dmr_mphys(imr_v),     &
                                              dmr_mphys(imr_cl),               &
                                              dmr_mphys(imr_ci),               &
                                              dmr_mphys(imr_s),                &
                                              dcfl, dcff, dbcf) )
    end if

    ! Save mv increment for stochastic physics
    dmv_mphys => microphysics_fields%get_field('dmv_mphys')
    call invoke(setval_X(dmv_mphys, dmr_mphys(imr_v)))

    if ( subroutine_timers ) call timer("mphys_alg")

    ! Output microphysics diagnostics
    if (write_diag .and. use_xios_io) then
      if ( subroutine_timers ) call timer("mphys_xios")
      call ls_rain%write_field('ls_rain')
      call ls_snow%write_field('ls_snow')
      call lsca_2d%write_field('lsca_2d')
      if ( subroutine_timers ) call timer("mphys_xios")
    end if

  end subroutine mphys_alg

end module mphys_alg_mod
