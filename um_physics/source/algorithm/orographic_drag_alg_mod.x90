!-------------------------------------------------------------------------------
! (c) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the orographic drag parametrization scheme

module orographic_drag_alg_mod
  use field_mod, only: field_type
  use field_collection_mod, only: field_collection_type
  use psykal_lite_phys_mod, only: invoke_orographic_drag_kernel

  ! XIOS output
  use io_config_mod, only: write_diag, &
                           use_xios_io

  use map_fd_to_prognostics_alg_mod, only: set_wind

  implicit none

  private

  public :: orographic_drag_alg_step

contains

  !> @details Run the orographic drag parametrization scheme
  !> @param[in,out] du_tot_orographic_drag Increment to total (3D) wind field
  !> @param[in,out] dtheta_orographic_drag Increment to theta field
  !> @param[in]     derived fields         Group of derived fields
  !> @param[in]     orography_fields       Fields for orographic drag scheme
  !> @param[in]     theta                  Potential temperature in wtheta
  !> @param[in]     height_w3              Height on w3
  !> @param[in]     height_wth             Height on wtheta
  subroutine orographic_drag_alg_step(                              &
                    du_tot_orographic_drag, dtheta_orographic_drag, &
                    derived_fields, orography_fields, theta,        &
                    height_w3, height_wth)

    ! orographic kernels
    use orographic_drag_kernel_mod, only: orographic_drag_kernel_type

    implicit none

    type( field_type ), intent( inout ) :: du_tot_orographic_drag
    type( field_type ), intent( inout ) :: dtheta_orographic_drag
    type( field_collection_type ), intent(in) :: derived_fields
    type( field_collection_type ), intent(in) :: orography_fields
    type( field_type ), intent(in) :: theta
    type( field_type ), intent(in) :: height_w3
    type( field_type ), intent(in) :: height_wth

    ! Local fields
    ! Total orographic drag increments to ...
    type( field_type ) :: du_orographic_drag    ! ... zonal wind
    type( field_type ) :: dv_orographic_drag    ! ... meridional wind
    type( field_type ) :: dw_orographic_drag    ! ... vertical wind (set to zero)
    type( field_type ) :: dtemp_orographic_drag ! ... temperature

    ! Orographic blocking drag increments to ...
    type( field_type ) :: du_blk    ! ... zonal wind
    type( field_type ) :: dv_blk    ! ... meridional wind
    type( field_type ) :: dtemp_blk ! ... temperature

    ! Orographic gravity wave drag increments to ...
    type( field_type ) :: du_orog_gwd    ! ... zonal wind
    type( field_type ) :: dv_orog_gwd    ! ... meridional wind
    type( field_type ) :: dtemp_orog_gwd ! ... temperature

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: u1_in_w3     => null()
    type( field_type ), pointer :: u2_in_w3     => null()
    type( field_type ), pointer :: exner_in_wth => null()
    type( field_type ), pointer :: wetrho_in_w3 => null()

    type( field_type ), pointer :: sd_orog      => null()
    type( field_type ), pointer :: grad_xx_orog => null()
    type( field_type ), pointer :: grad_xy_orog => null()
    type( field_type ), pointer :: grad_yy_orog => null()

    ! Unpack fields
    u1_in_w3 => derived_fields%get_field('u1_in_w3')
    u2_in_w3 => derived_fields%get_field('u2_in_w3')
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    wetrho_in_w3 => derived_fields%get_field('wetrho_in_w3')

    ! Unpack orography fields
    sd_orog      => orography_fields%get_field('sd_orog')
    grad_xx_orog => orography_fields%get_field('grad_xx_orog')
    grad_xy_orog => orography_fields%get_field('grad_xy_orog')
    grad_yy_orog => orography_fields%get_field('grad_yy_orog')

    ! Local increments
    call u1_in_w3%copy_field_properties(du_orographic_drag)
    call u2_in_w3%copy_field_properties(dv_orographic_drag)
    call theta%copy_field_properties(dw_orographic_drag)
    call u1_in_w3%copy_field_properties(du_blk)
    call u2_in_w3%copy_field_properties(dv_blk)
    call u1_in_w3%copy_field_properties(du_orog_gwd)
    call u2_in_w3%copy_field_properties(dv_orog_gwd)
    call theta%copy_field_properties(dtemp_orographic_drag)
    call theta%copy_field_properties(dtemp_blk)
    call theta%copy_field_properties(dtemp_orog_gwd)

    ! Initalize field increments from orographic drag
    call invoke(setval_c(dw_orographic_drag, 0.0_r_def), &
                setval_c(du_blk,    0.0_r_def),          &
                setval_c(dv_blk,    0.0_r_def),          &
                setval_c(dtemp_blk, 0.0_r_def),          &
                setval_c(du_orog_gwd,    0.0_r_def),     &
                setval_c(dv_orog_gwd,    0.0_r_def),     &
                setval_c(dtemp_orog_gwd, 0.0_r_def) )

    ! Call orographic drag kernel
    ! The orographic drag kernel is temporarily being called from
    ! psykal_lite_phys_mod.F90 because functionality to loop over a
    ! subset of points (in this case, points where sd_orog > 0)
    ! does not currently exist in PSyclone.
    ! PSyclone ticket relating to this functionality is #487.
    call invoke_orographic_drag_kernel(                         &
                du_blk, dv_blk, du_orog_gwd, dv_orog_gwd,       &
                dtemp_blk, dtemp_orog_gwd, u1_in_w3, u2_in_w3,  &
                wetrho_in_w3, theta, sd_orog, grad_xx_orog,     &
                grad_xy_orog, grad_yy_orog,height_w3, height_wth)

    ! Sum increments
    call invoke(X_plus_Y (du_orographic_drag, du_blk, du_orog_gwd),          &
                X_plus_Y (dv_orographic_drag, dv_blk, dv_orog_gwd),          &
                X_plus_Y (dtemp_orographic_drag, dtemp_blk, dtemp_orog_gwd), &

    ! Convert temperature to potential temperature
                X_divideby_Y (dtheta_orographic_drag, dtemp_orographic_drag, &
                              exner_in_wth) )

    ! Mapping back of wind increments
    call set_wind(du_tot_orographic_drag, du_orographic_drag, &
                  dv_orographic_drag, dw_orographic_drag)

    ! Write diagnostics
    if (write_diag .and. use_xios_io) then
      call du_orographic_drag%write_field('du_orographic_drag')
      call dv_orographic_drag%write_field('dv_orographic_drag')
      call du_blk%write_field('du_orog_blk')
      call dv_blk%write_field('dv_orog_blk')
      call du_orog_gwd%write_field('du_orog_gwd')
      call dv_orog_gwd%write_field('dv_orog_gwd')
      call dtheta_orographic_drag%write_field('dtheta_orographic_drag')
      call dtemp_orographic_drag%write_field('dtemp_orographic_drag')
      call dtemp_blk%write_field('dtemp_orog_blk')
      call dtemp_orog_gwd%write_field('dtemp_orog_gwd')
    end if

  end subroutine orographic_drag_alg_step

end module orographic_drag_alg_mod
