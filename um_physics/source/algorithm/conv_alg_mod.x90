!-------------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the UM convection scheme

module conv_alg_mod

  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  use mr_indices_mod,       only: nummr, imr_v

  use io_config_mod,        only: subroutine_timers
  use timer_mod,            only: timer


  ! xios output
  use io_config_mod,      only: write_diag, use_xios_io

  implicit none

  private

  public conv_alg_step

contains

  !> @brief Run the Lambert-Lewis convection scheme
  !> @details The Lambert-Lewis convection scheme is a simple
  !>           convection parametrization that mixes theta and moisture
  !>           as documented in UMDP41
  !> @param[in,out] physics_incs   Group of physics increments
  !> @param[in,out] derived_fields Group of derived fields
  !> @param[in,out] twod_fields    Group of twod fields
  !> @param[in]     exner          Exner pressure field in density (w3) space
  !> @param[in]     mr             Mixing ratios
  subroutine conv_alg_step(physics_incs, derived_fields, twod_fields, exner, mr)

    use conv_kernel_mod, only: conv_kernel_type

    implicit none

    type( field_collection_type ), intent(inout) :: physics_incs
    type( field_collection_type ), intent(inout) :: derived_fields
    type( field_collection_type ), intent(inout) :: twod_fields
    type( field_type ), intent( in )             :: exner, mr(nummr)

    ! Temporary fields unpacked from collections
    type( field_type ), pointer :: exner_in_wth  => null()
    type( field_type ), pointer :: theta_star => null()
    type( field_type ), pointer :: dt_conv => null()
    type( field_type ), pointer :: dmv_conv => null()
    type( field_type ), pointer :: conv_rain => null()

    if ( subroutine_timers ) call timer("conv_alg_step")

    ! Unpack fields
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    theta_star => derived_fields%get_field('theta_star')

    dt_conv => physics_incs%get_field('dt_conv')
    dmv_conv => physics_incs%get_field('dmv_conv')

    conv_rain => twod_fields%get_field('conv_rain')

    call invoke(conv_kernel_type(dt_conv, dmv_conv, theta_star, mr(imr_v), &
                                 exner_in_wth, exner, conv_rain) )

    if (write_diag .and. use_xios_io) then
      call dt_conv%write_field('dt_conv')
      call dmv_conv%write_field('dmv_conv')
    end if

    if ( subroutine_timers ) call timer("conv_alg_step")

  end subroutine conv_alg_step

end module conv_alg_mod
