!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the PC2 cloud scheme

module pc2_pressure_forcing_alg_mod

   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type

   implicit none

   private

   public pc2_pressure_forcing_alg_step

contains
  !>@brief Run pc2_pressure_forcing process
  !>@details PC2 pressure forcing
  !>         1) Exner function after slow and exner function now
  !>            get passed down to calculate a pressure difference within the kernel.
  !>         2) Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[inout]  mr                   Current mixing ratios
  !>@param[in]     theta                Current potential temperature (theta) in wth space
  !>@param[in]     theta_after_slow     as above but value after slow physics.
  !>@param[in]     exner_wth            Current exner function in wth space (not w3)
  !>@param[in]     exner_after_slow_wth as above but value after slow physics in wth space.
  !>@param[inout]  cloud_fields         Group of cloud fields
  !>@param[out]    dtheta_pc2_inc       Potential temperature response in wth space

  subroutine pc2_pressure_forcing_alg_step( mr,                   &
                                            theta,                &
                                            theta_after_slow,     &
                                            exner_wth,            &
                                            exner_after_slow_wth, &
                                            cloud_fields,         &
                                            dtheta_pc2_inc )

    use pc2_homogeneous_kernel_mod, only: pc2_homogeneous_kernel_type
    use mr_indices_mod,             only: imr_v, imr_cl, imr_ci, nummr
    use io_config_mod,              only: subroutine_timers
    use timer_mod,                  only: timer

    implicit none

    type( field_type ), intent( in )    :: theta
    type( field_type ), intent( inout ) :: mr ( nummr )
    type( field_type ), intent( in )    :: exner_wth
    type( field_type ), intent( in )    :: exner_after_slow_wth
    type( field_type ), intent( in )    :: theta_after_slow

    type( field_collection_type ), intent(inout) :: cloud_fields

    type( field_type ), intent( inout ) :: dtheta_pc2_inc
    type( field_type )                  :: dqv_pc2_inc
    type( field_type )                  :: dqcl_pc2_inc
    type( field_type )                  :: dcfl_pc2_inc
    type( field_type )                  :: dbcf_pc2_inc

    ! Define multiple fields that get filled with zeros
    ! as they are associated with zero forcing when this algorithm
    ! calls pc2_homogeneous_kernel_type
    type( field_type ) :: dtheta_forcing, dqv_forcing, dqcl_forcing

    ! For PC2
    type( field_type ), pointer :: liquid_fraction      => null()
    type( field_type ), pointer :: ice_fraction         => null()
    type( field_type ), pointer :: bulk_fraction        => null()

    if ( subroutine_timers ) call timer("pc2_pressure_forcing_alg_step")

    ! Unpack fields
    liquid_fraction => cloud_fields%get_field('liquid_fraction')
    ice_fraction    => cloud_fields%get_field('ice_fraction')
    bulk_fraction   => cloud_fields%get_field('bulk_fraction')

    call theta%copy_field_properties(dtheta_forcing)
    call theta%copy_field_properties(dqv_forcing)
    call theta%copy_field_properties(dqcl_forcing)

    call theta%copy_field_properties(dqv_pc2_inc)
    call theta%copy_field_properties(dqcl_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke( name="update_within_pc2_pressure_forcing",          &
                                                                     !
                 setval_c(dtheta_forcing, 0.0_r_def),                &
                 setval_c(dqv_forcing,    0.0_r_def),                &
                 setval_c(dqcl_forcing,   0.0_r_def),                &
                                                                     !
                 pc2_homogeneous_kernel_type( mr(imr_v),             &
                                              mr(imr_cl),            &
                                              liquid_fraction,       &
                                              ice_fraction,          &
                                              bulk_fraction,         &
                                              theta,                 &
                                              exner_wth,             &
                                              exner_after_slow_wth,  &
                                                ! Forcings
                                              dtheta_forcing,        &
                                              dqv_forcing,           &
                                              dqcl_forcing,          &
                                                ! Response increments
                                              dtheta_pc2_inc,        &
                                              dqv_pc2_inc,           &
                                              dqcl_pc2_inc,          &
                                              dcfl_pc2_inc,          &
                                              dbcf_pc2_inc ),        &
                                                                     !
     ! N.B. all fields that need to get updated are being updated apart from
     ! theta (where we pass an increment back up instead).
     ! So we update the water vapour, liquid water content,
     ! liquid cloud fraction and bulk cloud fraction.
     ! Ice water content and ice fraction increments do not get produced by
     ! homogeneous forcing process so no need to increment those fields.
                                                                     !
                        inc_X_plus_Y(mr(imr_v),       dqv_pc2_inc),  &
                        inc_X_plus_Y(mr(imr_cl),      dqcl_pc2_inc), &
                        inc_X_plus_Y(liquid_fraction, dcfl_pc2_inc), &
                        inc_X_plus_Y(bulk_fraction,   dbcf_pc2_inc)  )

    if ( subroutine_timers ) call timer("pc2_pressure_forcing_alg_step")

  end subroutine pc2_pressure_forcing_alg_step

end module pc2_pressure_forcing_alg_mod
