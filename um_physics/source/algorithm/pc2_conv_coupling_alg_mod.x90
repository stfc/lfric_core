!-------------------------------------------------------------------------------
! (c) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the PC2 cloud scheme

module pc2_conv_coupling_alg_mod

   use constants_mod,        only: r_def
   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type
   use io_config_mod,        only: write_diag, use_xios_io
   use mesh_mod,             only: mesh_type
   use mr_indices_mod,       only: nummr, imr_v, imr_cl, imr_ci
   use um_sizes_init_mod,    only: um_sizes_init

   implicit none

   private
   public pc2_conv_coupling_alg

contains
  !>@brief Run pc2_conv_coupling
  !>@details Convection affects cloud condensate and cloud fraction.
  !>         Increments from detrainment get calculated by the convection
  !>         scheme itself.
  !>         Increments from subsidence advection, subsidence warming and
  !>         erosion get calculated downward from here.
  !>         Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[in,out] mr               Mixing ratios
  !>@param[in]     derived_fields   Group of derived fields
  !>@param[in,out] convection_fields Fields for convection scheme
  !>@param[in,out] cloud_fields      Fields for cloud scheme
  !>@param[in]     dt                The model timestep length
  subroutine pc2_conv_coupling_alg( mr, derived_fields,                  &
                                    convection_fields, cloud_fields, dt )

    use constants_mod,                only: i_def
    use io_config_mod,                only: subroutine_timers
    use timer_mod,                    only: timer
    use pc2_conv_coupling_kernel_mod, only: pc2_conv_coupling_kernel_type

    implicit none

    type( field_type ), intent( inout ) :: mr(nummr)

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(inout) :: convection_fields
    type( field_collection_type ), intent(inout) :: cloud_fields
    real( kind=r_def ),            intent(in)    :: dt

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_wth       => null()
    type( field_type ), pointer :: theta_star      => null()

    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: frozen_fraction => null()
    type( field_type ), pointer :: bulk_fraction   => null()

    type( field_type ), pointer :: dt_conv         => null()
    type( field_type ), pointer :: dmv_conv        => null()
    type( field_type ), pointer :: dmcl_conv       => null()
    type( field_type ), pointer :: dmci_conv       => null()
    type( field_type ), pointer :: dcfl_conv       => null()
    type( field_type ), pointer :: dcff_conv       => null()
    type( field_type ), pointer :: dbcf_conv       => null()

    type(mesh_type), pointer :: mesh => null()
    integer(i_def) :: ncells

    if ( subroutine_timers ) call timer ("pc2_conv_coupling_alg")

    exner_wth       => derived_fields%get_field('exner_in_wth')
    theta_star      => derived_fields%get_field('theta_star')

    liquid_fraction => cloud_fields%get_field('liquid_fraction')
    frozen_fraction => cloud_fields%get_field('frozen_fraction')
    bulk_fraction   => cloud_fields%get_field('bulk_fraction')

    dt_conv         => convection_fields%get_field('dt_conv')
    dmv_conv        => convection_fields%get_field('dmv_conv')
    dmcl_conv       => convection_fields%get_field('dmcl_conv')
    dmci_conv       => convection_fields%get_field('dmci_conv')
    dcfl_conv       => convection_fields%get_field('dcfl_conv')
    dcff_conv       => convection_fields%get_field('dcff_conv')
    dbcf_conv       => convection_fields%get_field('dbcf_conv')

    ! Re-initialise UM to work on whole domain (i-first)
    mesh => exner_wth%get_mesh()
    ncells = mesh%get_last_edge_cell()
    call um_sizes_init(ncells)

    call invoke ( name="update_from_pc2_conv_coupling",            &
                                                                   !
                  pc2_conv_coupling_kernel_type ( theta_star,      &
                                                  mr(imr_v),       &
                                                  mr(imr_cl),      &
                                                  liquid_fraction, &
                                                  frozen_fraction, &
                                                  bulk_fraction,   &
                                                  exner_wth,       &
                                                  dt_conv,         &
                                                  dmv_conv,        &
                                                  dmcl_conv,       &
                                                  dcfl_conv,       &
                                                  dcff_conv,       &
                                                  dbcf_conv, dt),  &
                                                                   !
                  ! All variables are being updated apart from theta
                  inc_X_plus_Y(mr(imr_v),       dmv_conv),         &
                  inc_X_plus_Y(mr(imr_cl),      dmcl_conv),        &
                  inc_X_plus_Y(mr(imr_ci),      dmci_conv),        &
                  inc_X_plus_Y(liquid_fraction, dcfl_conv),        &
                  inc_X_plus_Y(frozen_fraction, dcff_conv),        &
                  inc_X_plus_Y(bulk_fraction,   dbcf_conv)         )

    ! Re-initialise UM back to columns
    call um_sizes_init(1_i_def)

    if ( subroutine_timers ) call timer ("pc2_conv_coupling_alg")

    if ( write_diag .and. use_xios_io ) then
      if ( subroutine_timers ) call timer("pc2_conv_coupling_xios")
      call dt_conv%write_field('cloud__dtheta_pc2_conv_coupling')
      call dmv_conv%write_field('cloud__dmv_pc2_conv_coupling')
      call dmcl_conv%write_field('cloud__dmcl_pc2_conv_coupling')
      call dmci_conv%write_field('cloud__dmci_pc2_conv_coupling')
      call dcfl_conv%write_field('cloud__dcfl_pc2_conv_coupling')
      call dcff_conv%write_field('cloud__dcff_pc2_conv_coupling')
      call dbcf_conv%write_field('cloud__dbcf_pc2_conv_coupling')
      if ( subroutine_timers ) call timer("pc2_conv_coupling_xios")
    end if

  end subroutine pc2_conv_coupling_alg

end module pc2_conv_coupling_alg_mod
