!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the PC2 cloud scheme

module pc2_rad_response_alg_mod

   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type

   implicit none

   private

   public pc2_rad_response_alg_step

contains
  !>@brief Run pc2_rad_response process
  !>@details PC2 homogeneous forcing and turbulence
  !>         1) Forcing to theta (not T), qv, qcl and pressure are considered.
  !>         2) Turbulence part is redundant as subsequent kernel
  !>            calls pc2_rad_response with zeros for mixing rates.
  !>         3) Fields do not get updated. Increments are passed back out.
  !>         See UMDP30 for full scheme details.
  !>@param[in]     mr_n           Start of timestep mixing ratios
  !>@param[in]     theta          Potential temperature (theta) in wth space
  !>@param[in]     derived_fields Group of derived fields
  !>@param[in]     cloud_fields   Group of cloud fields
  !>@param[in]     dtheta_forcing Potential temperature forcing (not T) in wth space
  !>@param[out]    dtheta_pc2_inc Potential temperature response in wth space
  !>@param[out]    dqv_pc2_inc    Vapour                response in wth space
  !>@param[out]    dqcl_pc2_inc   Liquid water content  response in wth space
  !>@param[out]    dcfl_pc2_inc   Liquid cloud fraction response in wth space
  !>@param[out]    dbcf_pc2_inc   Bulk cloud fraction   response in wth space

  subroutine pc2_rad_response_alg_step( mr_n,           &
                                        theta,          &
                                        derived_fields, &
                                        cloud_fields,   &
                                        dtheta_forcing, &
                                        dtheta_pc2_inc, &
                                        dqv_pc2_inc,    &
                                        dqcl_pc2_inc,   &
                                        dcfl_pc2_inc,   &
                                        dbcf_pc2_inc    )

    use pc2_homogeneous_kernel_mod, only: pc2_homogeneous_kernel_type
    use mr_indices_mod,             only: imr_v, imr_cl, imr_ci, nummr
    use io_config_mod,              only: subroutine_timers
    use timer_mod,                  only: timer

    implicit none

    type( field_type ), intent( in ) :: mr_n ( nummr )
    type( field_type ), intent( in ) :: theta
    type( field_type ), intent( in ) :: dtheta_forcing
    type( field_type )               :: dqv_forcing
    type( field_type )               :: dqcl_forcing

    type( field_collection_type ), intent(in) :: derived_fields, cloud_fields

    ! Increments out.
    type( field_type ), intent( out ) :: dtheta_pc2_inc
    type( field_type ), intent( out ) :: dqv_pc2_inc
    type( field_type ), intent( out ) :: dqcl_pc2_inc
    type( field_type ), intent( out ) :: dcfl_pc2_inc
    type( field_type ), intent( out ) :: dbcf_pc2_inc

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_wth  => null()
    ! Dummy, as will be set to same as above
    type( field_type ), pointer :: exner_copy_wth  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction      => null()
    type( field_type ), pointer :: ice_fraction         => null()
    type( field_type ), pointer :: bulk_fraction        => null()

    if ( subroutine_timers ) call timer("pc2_rad_response_alg_step")

    ! Unpack fields
    liquid_fraction => cloud_fields%get_field('liquid_fraction')
    ice_fraction    => cloud_fields%get_field('ice_fraction')
    bulk_fraction   => cloud_fields%get_field('bulk_fraction')

    exner_wth       => derived_fields%get_field('exner_in_wth')
    exner_copy_wth  => derived_fields%get_field('exner_in_wth')

    call theta%copy_field_properties(dqv_forcing)
    call theta%copy_field_properties(dqcl_forcing)
    call theta%copy_field_properties(dtheta_pc2_inc)
    call theta%copy_field_properties(dqv_pc2_inc)
    call theta%copy_field_properties(dqcl_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke( setval_c(dqv_forcing,    0.0_r_def),                 &
                 setval_c(dqcl_forcing,   0.0_r_def),                 &
                                                                      !
                 pc2_homogeneous_kernel_type ( mr_n(imr_v),           &
                                               mr_n(imr_cl),          &
                                               liquid_fraction,       &
                                               ice_fraction,          &
                                               bulk_fraction,         &
                                               theta,                 &
                                               exner_wth,             &
                                               exner_copy_wth,        &
                                                 ! Forcings
                                               dtheta_forcing,        &
                                               dqv_forcing,           &
                                               dqcl_forcing,          &
                                                 ! Response increments
                                               dtheta_pc2_inc,        &
                                               dqv_pc2_inc,           &
                                               dqcl_pc2_inc,          &
                                               dcfl_pc2_inc,          &
                                               dbcf_pc2_inc         ) )

    ! N.B. We are not updating the variables that came in, just
    !      providing increments that need to be added on later.

    if ( subroutine_timers ) call timer("pc2_rad_response_alg_step")

  end subroutine pc2_rad_response_alg_step

end module pc2_rad_response_alg_mod
