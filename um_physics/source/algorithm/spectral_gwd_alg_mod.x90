!-------------------------------------------------------------------------------
! (c) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the spectral gravity wave drag scheme

module spectral_gwd_alg_mod
   use constants_mod, only: i_def
   use field_mod, only: field_type
   use field_collection_mod, only: field_collection_type
   use io_config_mod,        only: subroutine_timers
   use timer_mod,            only: timer
   use log_mod,              only: log_event, LOG_LEVEL_INFO
   use fs_continuity_mod,    only: W3, Wtheta

  ! XIOS output
  use io_config_mod, only: write_diag, &
                           use_xios_io

  use map_fd_to_prognostics_alg_mod, only: set_wind

  implicit none

  private
  public :: spectral_gwd_alg

contains

  !> @brief Run the spectral gravity wave drag scheme
  !> @param[in,out] du_tot_spectral_gwd  Increment to total (3D) wind field
  !> @param[in,out] dtheta_spectral_gwd  Increment to theta field
  !> @param[in]     derived fields       Group of derived fields
  !> @param[in]     microphysics_fields  Fields for mphys scheme
  !> @param[in]     convection_fields    Fields for convection scheme
  !> @param[in]     theta                Potential temperature in wtheta
   subroutine spectral_gwd_alg(du_tot_spectral_gwd, dtheta_spectral_gwd, &
                               derived_fields, microphysics_fields,      &
                               convection_fields, theta)

    ! spectral_gwd kernels
    use spectral_gwd_kernel_mod, only: spectral_gwd_kernel_type

    use geometric_constants_mod, only: get_latitude, get_height

   implicit none

    type( field_type ), intent( inout ) :: du_tot_spectral_gwd
    type( field_type ), intent( inout ) :: dtheta_spectral_gwd
    type( field_collection_type ), intent(in) :: derived_fields
    type( field_collection_type ), intent(in) :: microphysics_fields
    type( field_collection_type ), intent(in) :: convection_fields
    type( field_type ), intent(in) :: theta

    ! Local fields
    ! Increments from spectral gravity wave drag to ...
    type( field_type ) :: du_spectral_gwd    ! ... zonal wind
    type( field_type ) :: dv_spectral_gwd    ! ... meridional wind
    type( field_type ) :: dw_spectral_gwd    ! ... vertical wind (set to zero)
    type( field_type ) :: dtemp_spectral_gwd ! ... temperature

    type( field_type ) :: totalppn ! total surface precipitation

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: u1_in_w3 => null()
    type( field_type ), pointer :: u2_in_w3 => null()
    type( field_type ), pointer :: u3_in_w3 => null()
    type( field_type ), pointer :: exner_in_wth => null()
    type( field_type ), pointer :: wetrho_in_w3 => null()

    type( field_type ), pointer :: ls_rain => null()
    type( field_type ), pointer :: ls_snow => null()
    type( field_type ), pointer :: conv_rain => null()
    type( field_type ), pointer :: conv_snow => null()

    type( field_type ), pointer :: latitude_w3 => null()
    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()

    if ( subroutine_timers ) call timer("spectral_gwd_alg")

    call log_event( 'slow_physics: Running spectral gravity wave drag', LOG_LEVEL_INFO )

    ! Unpack fields
    u1_in_w3 => derived_fields%get_field('u1_in_w3')
    u2_in_w3 => derived_fields%get_field('u2_in_w3')
    u3_in_w3 => derived_fields%get_field('u3_in_w3')
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    wetrho_in_w3 => derived_fields%get_field('wetrho_in_w3')

    ls_rain => microphysics_fields%get_field('ls_rain')
    ls_snow => microphysics_fields%get_field('ls_snow')
    conv_rain => convection_fields%get_field('conv_rain')
    conv_snow => convection_fields%get_field('conv_snow')

    latitude_w3 => get_latitude()
    height_wth => get_height(Wtheta)
    height_w3 => get_height(W3)

    call u1_in_w3%copy_field_properties(du_spectral_gwd)
    call u2_in_w3%copy_field_properties(dv_spectral_gwd)
    call theta%copy_field_properties(dw_spectral_gwd)
    call dtheta_spectral_gwd%copy_field_properties(dtemp_spectral_gwd)
    call ls_rain%copy_field_properties(totalppn)

    call invoke(                                                            &
                ! Set vertical wind inc to zero for mapping back of winds
                setval_c(dw_spectral_gwd, 0.0_r_def),                       &

                ! Add precipitation
                X_plus_Y(totalppn, ls_rain, ls_snow),                       &
                inc_X_plus_Y(totalppn, conv_rain),                          &
                inc_X_plus_Y(totalppn, conv_snow),                          &

                ! Call spectral gravity wave drag kernel
                spectral_gwd_kernel_type (du_spectral_gwd, dv_spectral_gwd, &
                            dtemp_spectral_gwd, u1_in_w3, u2_in_w3,         &
                            wetrho_in_w3, exner_in_wth, theta,              &
                            height_w3, height_wth, totalppn, latitude_w3),  &

                ! Convert temperature to potential temperature
                X_divideby_Y ( dtheta_spectral_gwd, dtemp_spectral_gwd,     &
                               exner_in_wth) )

    ! Mapping back of wind increments
    call set_wind(du_tot_spectral_gwd, du_spectral_gwd, dv_spectral_gwd, &
                  dw_spectral_gwd)

    if (write_diag .and. use_xios_io) then
      call du_spectral_gwd%write_field('du_spectral_gwd')
      call dv_spectral_gwd%write_field('dv_spectral_gwd')
      call dtheta_spectral_gwd%write_field('dtheta_spectral_gwd')
      call dtemp_spectral_gwd%write_field('dtemp_spectral_gwd')
   end if

   if ( subroutine_timers ) call timer("spectral_gwd_alg")

  end subroutine spectral_gwd_alg

end module spectral_gwd_alg_mod
