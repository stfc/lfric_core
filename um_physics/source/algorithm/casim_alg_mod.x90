!-------------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the CASIM Microphysics scheme

module casim_alg_mod

  use field_mod,            only: field_type
  use field_collection_mod, only: field_collection_type
  ! xios output
  use io_config_mod,        only: write_diag, use_xios_io
  use constants_mod,        only: r_def, i_def

  use geometric_constants_mod, only: get_height
  use fs_continuity_mod,       only: W3, Wtheta

  implicit none

  private
  public casim_alg

contains
  !>@brief Run the CASIM Microphysics scheme
  !>@details The CASIM Microphysics scheme calculates:
  !>             1) Precipitation rates output to the surface
  !>                and other physics schemes.
  !>             2) Increments to the large scale prognostics
  !>                due to cloud microphysical processes
  !>                (e.g. latent heating and cooling).
  !>         See UMDP50 for full scheme details
  !>@param[in]     mr_n           Start of timestep mixing ratios
  !>@param[in]     theta          Potential temperature (theta) in wth space
  !>@param[in]     dry_rho_in_w3  Dry density in the w3 space
  !>@param[in]     derived_fields Group of derived fields
  !>@param[in,out] microphysics_fields    Fields for mphys scheme
  !>@param[in]     cloud_fields           Fields for cloud scheme
  !>@param[in]     turbulence_fields      Fields for turbulence scheme
  !>@param[in,out] dmr_mphys      Mixing ratio increment due to microphysics
  !>@param[in,out] theta_inc      Potential temperature increment in wth space
  !>@param[in,out] dcfl           Increment to liquid cloud fraction
  !>@param[in,out] dcff           Increment to ice cloud fraction
  !>@param[in,out] dbcf           Increment to bulk cloud fraction
  subroutine casim_alg( mr_n, theta, dry_rho_in_w3, derived_fields,            &
                        microphysics_fields, cloud_fields,                     &
                        turbulence_fields, dmr_mphys, theta_inc, dcfl,         &
                        dcff, dbcf )

    use casim_kernel_mod, only: casim_kernel_type
    use mr_indices_mod,   only: imr_v, imr_cl, imr_r, imr_ci, imr_s, imr_g, nummr

    use io_config_mod,             only: subroutine_timers
    use timer_mod,                 only: timer
    use log_mod,                   only: log_event, LOG_LEVEL_INFO

    implicit none

    type( field_type ), intent( in )    :: mr_n ( nummr ), theta, dry_rho_in_w3

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(in)    :: microphysics_fields
    type( field_collection_type ), intent(in)    :: cloud_fields
    type( field_collection_type ), intent(in)    :: turbulence_fields

    type( field_type ), intent( inout ) :: dmr_mphys ( nummr )
    type( field_type ), intent( inout ) :: theta_inc
    type( field_type ), intent( inout ) :: dcfl, dcff, dbcf

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth        => null()
    type( field_type ), pointer :: w_in_wth           => null()
    type( field_type ), pointer :: wetrho_in_w3        => null()
    type( field_type ), pointer :: rho_in_wth          => null()

    type( field_type ), pointer :: cf_in               => null()
    type( field_type ), pointer :: cfl_in              => null()
    type( field_type ), pointer :: cff_in              => null()

    type( field_type ), pointer :: dmv_mphys           => null()
    type( field_type ), pointer :: ls_rain             => null()
    type( field_type ), pointer :: ls_snow             => null()

    type( field_type ), pointer :: nl_mphys            => null()
    type( field_type ), pointer :: nr_mphys            => null()
    type( field_type ), pointer :: ni_mphys            => null()
    type( field_type ), pointer :: ns_mphys            => null()
    type( field_type ), pointer :: ng_mphys            => null()

    type( field_type ), pointer :: wvar   => null()

    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()

    integer(kind=i_def) :: mesh_id

    if ( subroutine_timers ) call timer("mphys_alg")

    call log_event( 'slow_physics: Running Microphysics', LOG_LEVEL_INFO )

    mesh_id = theta%get_mesh_id()

    call theta%copy_field_properties(dcfl)
    call theta%copy_field_properties(dcff)
    call theta%copy_field_properties(dbcf)
    call invoke( setval_c(dcfl,      0.0_r_def), &
                 setval_c(dcff,      0.0_r_def), &
                 setval_c(dbcf,      0.0_r_def), &
                 setval_c(theta_inc, 0.0_r_def) )

    ! Unpack fields
    call derived_fields%get_field('exner_in_wth', exner_in_wth)
    call derived_fields%get_field('w_in_wth', w_in_wth)
    call derived_fields%get_field('wetrho_in_w3', wetrho_in_w3)
    call derived_fields%get_field('rho_in_wth', rho_in_wth)

    call cloud_fields%get_field('liquid_fraction', cfl_in)
    call cloud_fields%get_field('frozen_fraction', cff_in)

    call microphysics_fields%get_field('ls_rain', ls_rain)
    call microphysics_fields%get_field('ls_snow', ls_snow)

    call microphysics_fields%get_field('nl_mphys',nl_mphys)
    call microphysics_fields%get_field('nr_mphys',nr_mphys)
    call microphysics_fields%get_field('ni_mphys',ni_mphys)
    call microphysics_fields%get_field('ns_mphys',ns_mphys)
    call microphysics_fields%get_field('ng_mphys',ng_mphys)

    call turbulence_fields%get_field('wvar', wvar)

    height_w3 => get_height(W3, mesh_id)
    height_wth => get_height(Wtheta, mesh_id)

    call invoke( casim_kernel_type ( mr_n(imr_v), mr_n(imr_cl), mr_n(imr_ci),  &
                                     mr_n(imr_r), mr_n(imr_g), mr_n(imr_s),    &
                                     cfl_in, cff_in,                           &
                                     nl_mphys, nr_mphys,                       &
                                     ni_mphys, ns_mphys, ng_mphys,             &
                                     w_in_wth,                                 &
                                     theta, exner_in_wth,                      &
                                     wetrho_in_w3, dry_rho_in_w3,              &
                                     height_w3, height_wth,                    &
                                     dmr_mphys(imr_v),  dmr_mphys(imr_cl),     &
                                     dmr_mphys(imr_ci), dmr_mphys(imr_r),      &
                                     dmr_mphys(imr_g), dmr_mphys(imr_s),       &
                                     ls_rain, ls_snow,                         &
                                     theta_inc, dcfl, dcff ) )

    ! Save mv increment for stochastic physics
    call microphysics_fields%get_field('dmv_mphys', dmv_mphys)
    call invoke(setval_X(dmv_mphys, dmr_mphys(imr_v)))

    if ( subroutine_timers ) call timer("mphys_alg")

    ! Output microphysics diagnostics
    if (write_diag .and. use_xios_io) then
      if ( subroutine_timers ) call timer("mphys_xios")
      call ls_rain%write_field('microphysics__ls_rain')
      call ls_snow%write_field('microphysics__ls_snow')

      ! CASIM diagnostics
      call nl_mphys%write_field('casim__nl_mphys')
      call nr_mphys%write_field('casim__nr_mphys')
      call ni_mphys%write_field('casim__ni_mphys')
      call ns_mphys%write_field('casim__ns_mphys')
      call ng_mphys%write_field('casim__ng_mphys')

      if ( subroutine_timers ) call timer("mphys_xios")
    end if

  end subroutine casim_alg

end module casim_alg_mod
