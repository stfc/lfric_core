!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!> @brief Interface to the PC2 cloud scheme

module pc2_initiation_alg_mod

   use field_mod,            only: field_type
   use field_collection_mod, only: field_collection_type

   implicit none

   private

   public pc2_initiation_alg_step

contains
  !>@brief Run pc2_initiation
  !>@details This algorithm calls the PC2 initiation kernel which assesses the
  !>         current model state fields. If certain conditions are met, the
  !>         kernel will create cloud from clear sky or break up an overcast
  !>         scene.
  !>   
  !>         Moisture and Cloud fields are updated accordingly and
  !>         PC2 increments to potential temperature returned.
  !>
  !>         See UMDP30 for full scheme details.
  !>@param[inout]  mr               Current Mixing ratios
  !>@param[in]     theta            Current Potential temperature (theta) in wth space
  !>@param[in]     exner_w3         Current value of enver in w3 space
  !>@param[in]     exner_wth        Current value of exner in wtheta space
  !>@param[in]     mr_n             Start of timestep mixing ratios
  !>@param[in]     theta_n          Start of timestep theta in wth space
  !>@param[in]     derived_fields   Group of derived fields
  !>@param[in]     turbulence_fields Fields for turbulence scheme
  !>@param[in,out] cloud_fields      Fields for cloud scheme
  !>@param[inout]  dtheta_pc2_inc   Potential temperature response in wth space

  subroutine pc2_initiation_alg_step( mr,                &
                                      theta,             &
                                      exner_w3,          &
                                      exner_wth,         &
                                      mr_n,              &
                                      theta_n,           &
                                      derived_fields,    &
                                      turbulence_fields, &
                                      cloud_fields,      &
                                      dtheta_pc2_inc     )

    use pc2_initiation_kernel_mod, only: pc2_initiation_kernel_type
    use mr_indices_mod,            only: imr_v, imr_cl, imr_ci, nummr
    use io_config_mod,             only: subroutine_timers
    use timer_mod,                 only: timer

    implicit none

    type( field_type ), intent( in )    :: theta
    type( field_type ), intent( in )    :: exner_w3
    type( field_type ), intent( inout ) :: mr   ( nummr )
    type( field_type ), intent( in )    :: mr_n ( nummr ), theta_n
    type( field_type ), intent( in )    :: exner_wth

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(in)    :: turbulence_fields
    type( field_collection_type ), intent(inout) :: cloud_fields

    type( field_type ), intent(inout) :: dtheta_pc2_inc
    type( field_type )                :: dqv_pc2_inc
    type( field_type )                :: dqcl_pc2_inc
    type( field_type )                :: dqcf_pc2_inc
    type( field_type )                :: dcfl_pc2_inc
    type( field_type )                :: dcff_pc2_inc
    type( field_type )                :: dbcf_pc2_inc

    ! Temporary updated theta after initiation, used for call to PC2 checks
    type( field_type ) :: tmp_theta

    ! Temporary unpacked fields from collections
    type( field_type ), pointer :: exner_n_wth  => null()

    ! For PC2
    type( field_type ), pointer :: liquid_fraction => null()
    type( field_type ), pointer :: ice_fraction    => null()
    type( field_type ), pointer :: bulk_fraction   => null()
    type( field_type ), pointer :: area_fraction   => null()
    type( field_type ), pointer :: rh_crit         => null()

    type( field_type ), pointer :: zlcl_mixed      => null()
    type( field_type ), pointer :: r_cumulus       => null()

    if ( subroutine_timers ) call timer("pc2_initiation_alg_step")

    ! Unpack fields
    exner_n_wth     => derived_fields%get_field('exner_in_wth')

    bulk_fraction   => cloud_fields%get_field('bulk_fraction')
    liquid_fraction => cloud_fields%get_field('liquid_fraction')
    ice_fraction    => cloud_fields%get_field('ice_fraction')
    area_fraction   => cloud_fields%get_field('area_fraction')
    rh_crit         => cloud_fields%get_field('rh_crit')

    zlcl_mixed      => turbulence_fields%get_field('zlcl_mixed')
    r_cumulus       => turbulence_fields%get_field('cumulus')

    call theta%copy_field_properties(dqv_pc2_inc)
    call theta%copy_field_properties(dqcl_pc2_inc)
    call theta%copy_field_properties(dqcf_pc2_inc)
    call theta%copy_field_properties(dcfl_pc2_inc)
    call theta%copy_field_properties(dcff_pc2_inc)
    call theta%copy_field_properties(dbcf_pc2_inc)

    call invoke( name="update_from_pc2_initiation",              &
                                                                 !
                 pc2_initiation_kernel_type ( mr(imr_v),         &
                                              mr(imr_cl),        &
                                              mr(imr_ci),        &
                                              liquid_fraction,   &
                                              ice_fraction,      &
                                              bulk_fraction,     &
                                              theta,             &
                                              exner_wth,      &
                                              exner_w3,          &
                      ! Next 4 are start of timestep qv, qcl, theta
                      ! and exner to find RHt at start of timestep
                                              mr_n(imr_v),       &
                                              mr_n(imr_cl),      &
                                              theta_n,           &
                                              exner_n_wth,       &
                                              zlcl_mixed,        &
                                              r_cumulus,         &
                                                ! Responses
                                              dtheta_pc2_inc,    &
                                              dqv_pc2_inc,       &
                                              dqcl_pc2_inc,      &
                                              dqcf_pc2_inc,      &
                                              dcfl_pc2_inc,      &
                                              dcff_pc2_inc,      &
                                              dbcf_pc2_inc,      &
                                              rh_crit       ) ,  &
                                                                 !
                ! Update all field (apart from theta)
                ! with increments from initiation
                                                                 !
                inc_X_plus_Y(mr(imr_v),       dqv_pc2_inc),      &
                inc_X_plus_Y(mr(imr_cl),      dqcl_pc2_inc),     &
                inc_X_plus_Y(mr(imr_ci),      dqcf_pc2_inc),     &
                inc_X_plus_Y(liquid_fraction, dcfl_pc2_inc),     &
                inc_X_plus_Y(ice_fraction,    dcff_pc2_inc),     &
                inc_X_plus_Y(bulk_fraction,   dbcf_pc2_inc),     &
                ! area_fraction = bulk_fraction
                setval_X    (area_fraction,   bulk_fraction)     )

    if ( subroutine_timers ) call timer("pc2_initiation_alg_step")

  end subroutine pc2_initiation_alg_step

end module pc2_initiation_alg_mod
