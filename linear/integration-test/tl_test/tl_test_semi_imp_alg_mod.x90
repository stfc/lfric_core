!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!>@brief   The tangent linear test for the semi-implicit timestepping
!!         (Taylor remainder convergence).
!>@details Test whether a section of tangent linear code is tangent linear
!!         to the corresponding section of nonlinear code, for the semi
!!         implicit algorithm, using the Taylor remainder convergence test.
module tl_test_semi_imp_alg_mod

  use assign_field_random_kernel_mod, only: assign_field_random_kernel_type
  use constants_mod,                  only: i_def, r_def, str_def
  use field_mod,                      only: field_type
  use field_bundle_mod,               only: clone_bundle
  use field_collection_mod,           only: field_collection_type
  use function_space_collection_mod,  only: function_space_collection
  use finite_element_config_mod,      only: element_order
  use formulation_config_mod,         only: moisture_formulation,    &
                                            moisture_formulation_dry
  use fs_continuity_mod,              only: W2, W3, Wtheta
  use gungho_model_data_mod,          only: model_data_type
  use model_clock_mod,                only: model_clock_type
  use moist_dyn_mod,                  only: num_moist_factors
  use mr_indices_mod,                 only: nummr
  use timestepping_config_mod,        only: dt
  use log_mod,                        only: log_event,         &
                                            log_scratch_space, &
                                            LOG_LEVEL_INFO
  use mesh_mod,                       only: mesh_type
  use tl_si_timestep_alg_mod,         only: tl_semi_implicit_alg_init, &
                                            tl_semi_implicit_alg_step, &
                                            tl_semi_implicit_alg_final
  use semi_implicit_timestep_alg_mod, only: semi_implicit_alg_init, &
                                            semi_implicit_alg_step, &
                                            semi_implicit_alg_final
  use moist_dyn_factors_alg_mod,      only: moist_dyn_factors_alg
  use tl_moist_dyn_factors_alg_mod,                                  &
                                      only: tl_moist_dyn_factors_alg
  use tl_test_convergence_rate_check, only: convergence_rate_check

  implicit none

  private
  public test_semi_imp_alg

  contains

  !> @brief Test the tangent linear semi-implicit algorithm
  !> @param[in] model_data    The working data set for a model run
  !> @param[in] mesh          The current 3d mesh
  !> @param[in] twod_mesh     The current 2d mesh
  !> @param[in] model_clock   Time within the model.
  !>
  subroutine test_semi_imp_alg( model_data, &
                                mesh,       &
                                twod_mesh,  &
                                model_clock )

    implicit none

    type(mesh_type), pointer, intent(in) :: mesh
    type(mesh_type), pointer, intent(in) :: twod_mesh

    type(model_data_type), target, intent(in) :: model_data
    class(model_clock_type),       intent(in) :: model_clock

    character(str_def) :: label = "semi_imp_alg"

    type( field_collection_type ), pointer :: prognostic_fields => null()
    type( field_collection_type ), pointer :: ls_fields => null()
    type( field_collection_type ), pointer :: diagnostic_fields => null()
    type( field_type ),            pointer :: mr(:) => null()
    type( field_type ),            pointer :: ls_mr(:) => null()
    type( field_collection_type ), pointer :: adv_tracer_all_outer => null()
    type( field_collection_type ), pointer :: adv_tracer_last_outer => null()
    type( field_collection_type ), pointer :: con_tracer_all_outer => null()
    type( field_collection_type ), pointer :: con_tracer_last_outer => null()
    type( field_collection_type ), pointer :: derived_fields => null()
    type( field_collection_type ), pointer :: radiation_fields => null()
    type( field_collection_type ), pointer :: microphysics_fields => null()
    type( field_collection_type ), pointer :: electric_fields => null()
    type( field_collection_type ), pointer :: orography_fields => null()
    type( field_collection_type ), pointer :: turbulence_fields => null()
    type( field_collection_type ), pointer :: convection_fields => null()
    type( field_collection_type ), pointer :: cloud_fields => null()
    type( field_collection_type ), pointer :: surface_fields => null()
    type( field_collection_type ), pointer :: soil_fields => null()
    type( field_collection_type ), pointer :: snow_fields => null()
    type( field_collection_type ), pointer :: chemistry_fields => null()
    type( field_collection_type ), pointer :: aerosol_fields => null()
    type( field_collection_type ), pointer :: stph_fields
    type( field_collection_type ), pointer :: lbc_fields => null()


    type(field_type), pointer :: ls_u => null()
    type(field_type), pointer :: ls_rho => null()
    type(field_type), pointer :: ls_theta => null()
    type(field_type), pointer :: ls_exner => null()
    type(field_type), pointer :: ls_moist_dyn(:) => null()

    type(field_type), pointer :: u => null()
    type(field_type), pointer :: rho => null()
    type(field_type), pointer :: theta => null()
    type(field_type), pointer :: exner => null()
    type(field_type), pointer :: moist_dyn(:) => null()

    type(field_type), dimension(num_moist_factors) :: p_moist_dyn
    type(field_type), dimension(num_moist_factors) :: n_moist_dyn

    type(field_type), dimension(nummr) :: p_mr
    type(field_type), dimension(nummr) :: n_mr
    type(field_type), dimension(nummr) :: r_mr
    type(field_type), dimension(nummr) :: diff_mr
    type(field_type) :: p_u, p_rho, p_theta, p_exner
    type(field_type) :: n_u, n_rho, n_theta, n_exner
    type(field_type) :: r_u, r_rho, r_theta, r_exner
    type(field_type) :: diff_u, diff_rho, diff_theta, diff_exner

    real(r_def) :: gamma_u, gamma_rho, gamma_exner, gamma_theta
    real(r_def) :: gamma_mr
    real(r_def) :: norm_u, norm_rho, norm_exner, norm_theta
    real(r_def) :: norm_mr, norm_mr_tmp
    real(r_def) :: norm_diff, norm_diff_prev

    real(r_def), parameter :: tol = 5.e-1_r_def
    real(r_def), parameter :: dtemp_encorr = 0.0_r_def

    integer :: n, i

    call log_event( "TL Test: " // trim(label), &
                     LOG_LEVEL_INFO )


    ls_fields         => model_data%ls_fields
    ls_moist_dyn      => model_data%ls_moist_dyn
    ls_mr             => model_data%ls_mr
    prognostic_fields => model_data%prognostic_fields
    diagnostic_fields => model_data%diagnostic_fields

    mr => model_data%mr
    moist_dyn => model_data%moist_dyn
    adv_tracer_all_outer => model_data%adv_tracer_all_outer
    adv_tracer_last_outer => model_data%adv_tracer_last_outer
    con_tracer_all_outer => model_data%con_tracer_all_outer
    con_tracer_last_outer => model_data%con_tracer_last_outer
    derived_fields => model_data%derived_fields
    radiation_fields => model_data%radiation_fields
    microphysics_fields => model_data%microphysics_fields
    electric_fields => model_data%electric_fields
    orography_fields => model_data%orography_fields
    turbulence_fields => model_data%turbulence_fields
    convection_fields => model_data%convection_fields
    cloud_fields => model_data%cloud_fields
    surface_fields => model_data%surface_fields
    soil_fields => model_data%soil_fields
    snow_fields => model_data%snow_fields
    chemistry_fields => model_data%chemistry_fields
    aerosol_fields => model_data%aerosol_fields
    stph_fields => model_data%stph_fields
    lbc_fields => model_data%lbc_fields

    ! Input
    call ls_fields%get_field('ls_u', ls_u)
    call ls_fields%get_field('ls_rho', ls_rho)
    call ls_fields%get_field('ls_theta', ls_theta)
    call ls_fields%get_field('ls_exner', ls_exner)

    call prognostic_fields%get_field('u', u)
    call prognostic_fields%get_field('rho', rho)
    call prognostic_fields%get_field('theta', theta)
    call prognostic_fields%get_field('exner', exner)

    call p_u%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W2) )
    call p_rho%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call p_exner%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call p_theta%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, Wtheta) )

    call n_u%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W2) )
    call n_rho%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call n_exner%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call n_theta%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, Wtheta) )

    call r_u%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W2) )
    call r_rho%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call r_exner%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call r_theta%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, Wtheta) )

    call diff_u%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W2) )
    call diff_rho%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call diff_exner%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, W3) )
    call diff_theta%initialise( vector_space = &
      function_space_collection%get_fs(mesh, element_order, Wtheta) )

    call clone_bundle(moist_dyn, p_moist_dyn, num_moist_factors)
    call clone_bundle(moist_dyn, n_moist_dyn, num_moist_factors)

    call clone_bundle(mr, p_mr, nummr)
    call clone_bundle(mr, r_mr, nummr)
    call clone_bundle(mr, n_mr, nummr)
    call clone_bundle(mr, diff_mr, nummr)

    call invoke( assign_field_random_kernel_type( r_u, 1.0_r_def ) ,     &
                 assign_field_random_kernel_type( r_rho, 1.0_r_def ) ,   &
                 assign_field_random_kernel_type( r_theta, 1.0_r_def ) , &
                 assign_field_random_kernel_type( r_exner, 1.0_r_def ) )

    do i = 1, nummr
      call invoke( assign_field_random_kernel_type( r_mr(i), 1.0_r_def ) )
    end do

    call invoke( setval_X( u, ls_u ),                     &
                 setval_X( theta, ls_theta ),             &
                 setval_X( rho, ls_rho ),                 &
                 setval_X( exner, ls_exner ) )

    do i = 1, num_moist_factors
      call invoke( setval_X( moist_dyn(i), ls_moist_dyn(i) ) )
    end do
    do i = 1, nummr
      call invoke( setval_X( mr(i), ls_mr(i) ) )
    end do

    call semi_implicit_alg_final()

    call semi_implicit_alg_init(mesh, u, rho, theta, exner, mr)

    call semi_implicit_alg_step(u, rho, theta, exner, mr, moist_dyn,         &
                                adv_tracer_all_outer, adv_tracer_last_outer, &
                                con_tracer_all_outer, con_tracer_last_outer, &
                                derived_fields, radiation_fields,            &
                                microphysics_fields, electric_fields,        &
                                orography_fields,                            &
                                turbulence_fields, convection_fields,        &
                                cloud_fields, surface_fields,                &
                                soil_fields, snow_fields,                    &
                                chemistry_fields,                            &
                                aerosol_fields, stph_fields, lbc_fields,     &
                                model_clock, dtemp_encorr, mesh, twod_mesh)

    call semi_implicit_alg_final()

    gamma_u = 1.e4_r_def
    gamma_theta = 1.e2_r_def
    gamma_rho = 8.0e-1_r_def
    gamma_exner = 6.5e-1_r_def

    if ( moisture_formulation == moisture_formulation_dry ) then
      gamma_mr = 0.0_r_def
    else
      gamma_mr = 1.e-2_r_def
    end if

    do n=1,2

      gamma_u = gamma_u / 2.0_r_def
      gamma_theta = gamma_theta / 2.0_r_def
      gamma_rho = gamma_rho / 2.0_r_def
      gamma_exner = gamma_exner / 2.0_r_def
      gamma_mr = gamma_mr / 2.0_r_def

      call invoke( a_times_X( p_u, gamma_u, r_u ),              &
                   a_times_X( p_theta, gamma_theta, r_theta ),  &
                   a_times_X( p_rho, gamma_rho, r_rho ),        &
                   a_times_X( p_exner, gamma_exner, r_exner ),  &
                   setval_X( n_u, ls_u ),                     &
                   setval_X( n_theta, ls_theta ),             &
                   setval_X( n_rho, ls_rho ),                 &
                   setval_X( n_exner, ls_exner ),             &
                   inc_X_plus_Y( n_u, p_u ),                  &
                   inc_X_plus_Y( n_theta, p_theta ),          &
                   inc_X_plus_Y( n_rho, p_rho ),              &
                   inc_X_plus_Y( n_exner, p_exner ) )

      do i = 1, nummr
        call invoke( &
          a_times_X( p_mr(i), gamma_mr, r_mr(i) ), &
          setval_X( n_mr(i), ls_mr(i) ),                       &
          inc_X_plus_Y( n_mr(i), p_mr(i) ) )
      end do

      call moist_dyn_factors_alg(n_moist_dyn, n_mr)

      call tl_moist_dyn_factors_alg(p_moist_dyn, p_mr )

      call semi_implicit_alg_init(mesh, n_u, n_rho, n_theta, n_exner, n_mr)

      call semi_implicit_alg_step(n_u, n_rho, n_theta, n_exner, n_mr,         &
                                  n_moist_dyn,                                &
                                  adv_tracer_all_outer, adv_tracer_last_outer,&
                                  con_tracer_all_outer, con_tracer_last_outer,&
                                  derived_fields, radiation_fields,           &
                                  microphysics_fields, electric_fields,       &
                                  orography_fields,                           &
                                  turbulence_fields, convection_fields,       &
                                  cloud_fields, surface_fields,               &
                                  soil_fields, snow_fields,                   &
                                  chemistry_fields,                           &
                                  aerosol_fields, stph_fields, lbc_fields,    &
                                  model_clock, dtemp_encorr, mesh, twod_mesh)

      call semi_implicit_alg_final()

      call tl_semi_implicit_alg_init(mesh, p_u, p_rho, p_theta, p_exner,     &
                                     p_mr, ls_u, ls_rho, ls_theta, ls_exner, &
                                     ls_mr, ls_moist_dyn)

      call tl_semi_implicit_alg_step(p_u, p_rho, p_theta,                  &
                                     p_exner, p_mr, p_moist_dyn,           &
                                     ls_u, ls_rho, ls_theta,               &
                                     ls_exner, ls_mr, ls_moist_dyn,        &
                                     derived_fields,                       &
                                     model_clock, mesh, twod_mesh)

      call tl_semi_implicit_alg_final()

      call invoke( X_minus_Y( diff_u, n_u, u ),                      &
                   inc_X_minus_Y( diff_u, p_u ),                     &
                   X_innerproduct_X( norm_u, diff_u ) )
      call invoke( X_minus_Y( diff_rho, n_rho, rho ),                &
                   inc_X_minus_Y( diff_rho, p_rho ),                 &
                   X_innerproduct_X( norm_rho, diff_rho ) )
      call invoke( X_minus_Y( diff_exner, n_exner, exner ),          &
                   inc_X_minus_Y( diff_exner, p_exner ),             &
                   X_innerproduct_X( norm_exner, diff_exner ) )
      call invoke( X_minus_Y( diff_theta, n_theta, theta ),          &
                   inc_X_minus_Y( diff_theta, p_theta ),             &
                   X_innerproduct_X( norm_theta, diff_theta ) )

      norm_mr=0.0_r_def
      do i = 1, nummr
        call invoke( &
          X_minus_Y( diff_mr(i), n_mr(i), mr(i) ),   &
          inc_X_minus_Y( diff_mr(i), p_mr(i) ),             &
          X_innerproduct_X( norm_mr_tmp, diff_mr(i) ) )
          norm_mr = norm_mr + norm_mr_tmp
      end do

      write( log_scratch_space, &
            '(A, E32.12, A, E32.12 )' ) &
             ' norm_u = ' ,   norm_u,   &
             ' norm_rho = ' , norm_rho

      call log_event( log_scratch_space, LOG_LEVEL_INFO )

      write( log_scratch_space, &
            '(A, E32.12, A, E32.12 )' ) &
             ' norm_mr = ' , norm_mr,   &
             ' norm_exner = ' , norm_exner

      call log_event( log_scratch_space, LOG_LEVEL_INFO )

      write( log_scratch_space, &
            '(A, E32.12 )' )     &
             ' norm_theta = ' , norm_theta

      call log_event( log_scratch_space, LOG_LEVEL_INFO )

      norm_diff = norm_rho + norm_theta + norm_exner + norm_u + norm_mr
      norm_diff = sqrt(norm_diff)

      if (n == 2) then
        call convergence_rate_check( norm_diff, norm_diff_prev, &
                                     label, tol=tol )
      end if

      norm_diff_prev = norm_diff
    end do

  end subroutine test_semi_imp_alg

end module tl_test_semi_imp_alg_mod
