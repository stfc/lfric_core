!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Reconstruct a W3 field at W2 points for use in the transport scheme.
!> @todo There are number of workarounds in this algorithm for aspects which
!!       aren't yet supported by PSyclone/LFRic:
!!       1. Issue #868 Multidata fields are not natively supported in PSyclone.
!!                     Currently we treat these as normal fields but need to
!!                     additionally pass in ndata_h or ndata_v to the kernels
!!                     as extra integer arguments.
!!       2. Issue #1246 Region stencils require both the global maximum size of
!!                      the stencil and local column size of the stencil.
!!                      PSyclone currently only passes in the local size and so
!!                      until this is resolved we also pass in the global maximum
!!                      size as an integer argument.
module tl_reconstruct_w3_field_alg_mod

  use constants_mod,                  only: r_def, i_def, l_def
  use field_mod,                      only: field_type
  use fs_continuity_mod,              only: W2, W3
  use poly1d_w3_reconstruction_kernel_mod, &
                                      only: poly1d_w3_reconstruction_kernel_type
  use tl_poly1d_vert_w3_reconstruction_kernel_mod, &
                                      only: tl_poly1d_vert_w3_reconstruction_kernel_type
  use poly2d_w3_reconstruction_kernel_mod, &
                                      only: poly2d_w3_reconstruction_kernel_type
  use transport_config_mod,           only: operators,            &
                                            fv_horizontal_order,  &
                                            fv_vertical_order,    &
                                            operators_fv,         &
                                            operators_fem,        &
                                            oned_reconstruction
  use log_mod,                        only: log_event,         &
                                            LOG_LEVEL_ERROR
  use transport_enumerated_types_mod, only: direction_v,    &
                                            direction_h,    &
                                            direction_3d
  use reconstruct_w3_field_alg_mod,   only: get_flux_stencil_extent,     &
                                            get_flux_2d_stencil_size,    &
                                            get_flux_ndata_h,            &
                                            get_flux_ndata_v,            &
                                            get_reversible_flux_ndata_v, &
                                            get_flux_coeffs,             &
                                            get_vert_flux_coeffs,        &
                                            get_reversible_vert_flux_coeffs

  implicit none

  private

  public :: tl_reconstruct_w3_field_alg

contains


  !=============================================================================
  !> @brief Reconstruct a W3 field at W2 points for tangent linear app.
  !> @details Reconstruct a W3 field (field_old) at W2 points (field_new) using
  !!          desired spatial reconstruction for tangent linear app.
  !!          Options for this are either FE or FV reconstructions.
  !> @param[in,out] field_new    ACTIVE  Reconstructed field at W2 points
  !> @param[in]     field_old    ACTIVE  Initial W3 field
  !> @param[in]     ls_field_old PASSIVE Initial W3 field
  !> @param[in]     ls_wind      PASSIVE Advecting wind to determine upwind direction
  !> @param[in]     direction    Splitting direction (h, v, or 3d) to compute
  !!                             reconstruction
  !> @param[in]     reversible   Use the reversible reconstruction
  !> @param[in]     logspace     Carry out interpolation in log space
  subroutine tl_reconstruct_w3_field_alg(field_new, field_old, ls_field_old, &
                                         ls_wind, direction, reversible, logspace)

    implicit none

    type(field_type),              intent(in)    :: field_old
    type(field_type),              intent(in)    :: ls_field_old
    type(field_type),              intent(in)    :: ls_wind
    type(field_type),              intent(inout) :: field_new
    integer(kind=i_def),           intent(in)    :: direction
    logical(kind=l_def),           intent(in)    :: reversible
    logical(kind=l_def), optional, intent(in)    :: logspace

    integer(kind=i_def) :: mesh_id
    logical(kind=l_def) :: logspace_loc
    integer(kind=i_def) :: stencil_extent

    integer(kind=i_def), pointer :: flux_stencil_extent => null()
    integer(kind=i_def), pointer :: flux_2d_stencil_size => null()
    integer(kind=i_def), pointer :: ndata_h => null()
    integer(kind=i_def), pointer :: ndata_v => null()
    type(field_type),    pointer :: flux_coeffs => null()
    type(field_type),    pointer :: vert_flux_coeffs => null()
    integer(kind=i_def)          :: vertical_order

    logspace_loc = .false.
    if ( present(logspace) ) then
      logspace_loc = logspace
    end if

    mesh_id = field_new%get_mesh_id()

    select case(operators)

      case default
        call log_event( "Gungho: Unrecognized option for operator.", LOG_LEVEL_ERROR )

      case(operators_fv)

        ! Set default value to be 0 and then update the mass flux
        ! in cases where the wind is nonzero using an upwind reconstruction
        call invoke( setval_c( field_new, 0.0_r_def) )
        if ( direction==direction_h .or. direction==direction_3d ) then
          ! Get extents and data sizes for this mesh
          flux_stencil_extent => get_flux_stencil_extent(mesh_id)
          flux_2d_stencil_size => get_flux_2d_stencil_size(mesh_id)
          ndata_h => get_flux_ndata_h(mesh_id)
          flux_coeffs => get_flux_coeffs(mesh_id)
          if ( oned_reconstruction ) then
            ! Use 1d flux reconstruction
            call invoke( name="W3_1dh_reconstruction", &
              poly1d_w3_reconstruction_kernel_type( field_new,           &
                                                    ls_wind,             &
                                                    field_old,           &
                                                    flux_stencil_extent, &
                                                    flux_coeffs,         &
                                                    fv_horizontal_order, &
                                                    ndata_h ) )
          else
            ! Use 2d flux reconstruction
            stencil_extent = fv_horizontal_order / 2_i_def
            call invoke( name="W3_2dh_reconstruction",              &
              poly2d_w3_reconstruction_kernel_type( field_new,      &
                                                    ls_wind,        &
                                                    field_old,      &
                                                    stencil_extent, &
                                                    flux_coeffs,    &
                                                    ndata_h,        &
                                                    flux_2d_stencil_size ) )
         end if
       end if
       if ( direction==direction_v .or. direction==direction_3d ) then
          ! Compute vertical components of mass flux
          if ( reversible ) then
            ndata_v => get_reversible_flux_ndata_v(mesh_id)
            vert_flux_coeffs => get_reversible_vert_flux_coeffs(mesh_id)
            vertical_order = fv_vertical_order - 1_i_def
          else
            ndata_v => get_flux_ndata_v(mesh_id)
            vert_flux_coeffs => get_vert_flux_coeffs(mesh_id)
            vertical_order = fv_vertical_order
          end if
          ! The logspace interpolation makes this different to non-linear kernel
          call invoke( name="W3_1dv_reconstruction",                        &
            tl_poly1d_vert_w3_reconstruction_kernel_type( field_new,        &
                                                          field_old,        &
                                                          ls_wind,          &
                                                          ls_field_old,     &
                                                          vert_flux_coeffs, &
                                                          ndata_v,          &
                                                          vertical_order,   &
                                                          logspace_loc) )
        end if
      case(operators_fem)
        call log_event( "No Tangent linear for operators_fem", LOG_LEVEL_ERROR )
    end select

    nullify(flux_stencil_extent,  &
            flux_2d_stencil_size, &
            ndata_h,              &
            ndata_v,              &
            flux_coeffs,          &
            vert_flux_coeffs      &
            )

  end subroutine tl_reconstruct_w3_field_alg

end module tl_reconstruct_w3_field_alg_mod
