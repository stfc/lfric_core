!-----------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Contains T.L. routine for advecting (advection-form) fields using MOL.

module tl_mol_advective_alg_mod

  use constants_mod,                  only: r_def, i_def, l_def
  use field_mod,                      only: field_type
  use runge_kutta_init_mod,           only: get_rk_transport_weights
  use log_mod,                        only: log_event, LOG_LEVEL_ERROR
  use mesh_mod,                       only: mesh_type
  use advective_increment_alg_mod,    only: advective_increment_alg
  use tl_advective_increment_alg_mod, only: tl_advective_increment_alg
  use transport_metadata_mod,         only: transport_metadata_type
  use transport_runtime_alg_mod,      only: transport_runtime_type
  use transport_runtime_collection_mod, &
                                      only: get_transport_runtime
  use tl_transport_runtime_collection_mod, &
                                      only: tl_transport_runtime
  use transport_config_mod,           only: runge_kutta_method

  implicit none

  public  :: tl_mol_advective_alg

contains

  !> @brief Tangent linear advection of a field with the MOL scheme.
  !> @details Performs a whole time step advection for the tangent linear model
  !!          This solves the advection (non-conservative) equation with the
  !!          specified options in the transport_metadata.
  !> @param[in,out] field_np1   ACTIVE  Field at the end of the time step
  !> @param[in]     field       ACTIVE  Field at the start of the transport step
  !> @param[in]     ls_field    PASSIVE Field at the start of the transport step
  !> @param[in]     direction   Direction of advection (vertical, horizontal, or 3d)
  !> @param[in]     transport_metadata
  !!                         Contains the configuration options for transporting these fields
  subroutine tl_mol_advective_alg( field_np1, field, ls_field, &
                                   direction, transport_metadata )

    implicit none

    type(field_type),              intent(inout) :: field_np1
    type(field_type),              intent(in)    :: field
    type(field_type),              intent(in)    :: ls_field
    integer(i_def),                intent(in)    :: direction
    type(transport_metadata_type), intent(in)    :: transport_metadata

    logical(l_def)   :: logspace
    integer(i_def)   :: monotonicity
    type(field_type) :: rhs, field_n

    type(field_type), pointer     :: advecting_wind => null()
    type(field_type), pointer     :: ls_advecting_wind => null()
    type(field_type)              :: ls_field_np1, ls_field_n
    type(field_type), allocatable :: stored_ls_field(:,:)
    type(field_type), allocatable :: rk_field(:)
    integer(i_def)                :: stage, s
    integer(i_def)                :: nstage, substep
    real(r_def),      allocatable :: rk_weights(:,:)
    type(mesh_type),  pointer     :: mesh => null()
    type(transport_runtime_type), pointer :: transport_runtime => null()
    type(field_type)              :: rhs_field

    integer(i_def) :: number_substeps
    real(r_def)    :: dt_substep
    logical(l_def) :: enforce_min_value
    logical(l_def) :: use_reversible_scheme

    ! Get transport_runtime
    mesh => field%get_mesh()
    transport_runtime => get_transport_runtime( mesh )
    advecting_wind    => tl_transport_runtime%get_advecting_wind( mesh%get_id() )
    ls_advecting_wind => transport_runtime%get_advecting_wind( mesh%get_id() )

    number_substeps = transport_runtime%get_number_substeps( mesh%get_id(), &
                                                             direction,     &
                                                             transport_metadata%get_splitting() )

    dt_substep      = transport_runtime%get_dt_substep( mesh%get_id(), &
                                                        direction,     &
                                                        transport_metadata%get_splitting() )
    logspace     = transport_metadata%get_log_space()
    monotonicity = transport_metadata%get_monotone()
    enforce_min_value = transport_metadata%get_enforce_min_value()

    call rhs%initialise(field%get_function_space())
    call field_n%initialise(field%get_function_space())

    call get_rk_transport_weights(nstage, rk_weights, runge_kutta_method)
    allocate( rk_field(nstage) )
    allocate( stored_ls_field(number_substeps, nstage) )

    do stage = 1, nstage
      call rk_field(stage)%initialise(field%get_function_space())
      do substep = 1, number_substeps
        call stored_ls_field(substep, stage)%initialise(field%get_function_space())
      end do
    end do
    call rhs_field%initialise( field%get_function_space() )

    call field%copy_field(field_np1)
    call ls_field%copy_field(ls_field_np1)

    !--------------------------------------------------------------------------!
    !                    Linearisation state
    !
    !       This calculates the values of ls_field. These are stored as an
    !       array for each substep and each RK stage to use in the perturbation
    !--------------------------------------------------------------------------!

    ! Perform the number of rk-stages and substeps required
    do substep = 1, number_substeps

      ! Reset field_n ready for the this substep
      call ls_field_np1%copy_field(ls_field_n)

      use_reversible_scheme = .false.

      do stage = 1, nstage
        ! Store values for use in the perturbation code. This is done before
        ! advecting so as to pick up the correct linearisation state
        call invoke( setval_X(stored_ls_field(substep, stage), ls_field_np1) )

        if ( stage == nstage .and. transport_metadata%get_reversible() ) &
            use_reversible_scheme= .true.

        ! Compute the field for this stage:
        ! rhs_field = sum(s=1,stage): a(stage,s)*field^(s)
        call invoke( setval_X( rk_field(stage), ls_field_np1 ), &
                     setval_c( rhs_field, 0.0_r_def ) )
        do s = 1, stage
          call invoke( inc_X_plus_bY( rhs_field, rk_weights(stage,s), rk_field(s) ) )
        end do

        ! Compute update: rhs = u.grad(rhs_field)
        call advective_increment_alg(rhs, rhs_field,                   &
                                     ls_advecting_wind, dt_substep,       &
                                     direction, use_reversible_scheme, &
                                     logspace=logspace,                &
                                     monotonicity=monotonicity )

        ! Update field: f = f^n - dt_substep*rhs
        call invoke( X_minus_bY(ls_field_np1, ls_field_n, dt_substep, rhs) )
      end do  ! end rk-stages

    end do

    ! Enforce a min value if required
    if ( enforce_min_value ) then
      call log_event('T.L.: enforcing min value in mol advective not ' // &
                     'yet implemented', LOG_LEVEL_ERROR)
    end if

    ! --------------------------------------------------------------------------
    !                    Perturbation
    !
    !   Repeat the calculation, but now for the tangent linear - adding in
    !   the values of ls_field_np1(substep,stage) obtained in the
    !   linearisation stage stage
    !---------------------------------------------------------------------------

    ! Perform the number of rk-stages and substeps required
    do substep = 1, number_substeps

      ! Reset field_n ready for the this substep
      call field_np1%copy_field(field_n)

      use_reversible_scheme = .false.

      do stage = 1, nstage

        if ( stage == nstage .and. transport_metadata%get_reversible() ) &
          use_reversible_scheme= .true.

        ! Compute the field for this stage:
        ! rhs_field = sum(s=1,stage): a(stage,s)*field^(s)
        call invoke( setval_X( rk_field(stage), field_np1 ), &
                     setval_c( rhs_field, 0.0_r_def ) )
        do s = 1, stage
          call invoke( inc_X_plus_bY( rhs_field, rk_weights(stage,s), rk_field(s) ) )
        end do

        ! Compute update: rhs = u.grad(rhs_field)
        call tl_advective_increment_alg(rhs, rhs_field, advecting_wind,   &
                                        stored_ls_field(substep, stage),  &
                                        ls_advecting_wind, dt_substep,    &
                                        direction, use_reversible_scheme, &
                                        logspace=logspace,                &
                                        monotonicity=monotonicity )

        ! Update field: f = f^n - dt_substep*rhs
        call invoke( X_minus_bY(field_np1, field_n, dt_substep, rhs) )
      end do  ! end rk-stages

    end do

    ! Enforce a min value if required
    if ( enforce_min_value ) then
      call log_event('T.L.: enforcing min value in mol advective not ' // &
                     'yet implemented', LOG_LEVEL_ERROR)
    end if

    if ( allocated(rk_field) ) deallocate(rk_field)
    if ( allocated(rk_weights) ) deallocate(rk_weights)
    if ( allocated(stored_ls_field) ) deallocate(stored_ls_field)

  end subroutine tl_mol_advective_alg

end module tl_mol_advective_alg_mod
