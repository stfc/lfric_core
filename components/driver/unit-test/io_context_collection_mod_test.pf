!-------------------------------------------------------------------------------
! (c) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
module io_context_collection_mod_test

  use pFUnit_mod
  use constants_mod,             only: i_def
  use io_context_collection_mod, only: io_context_collection_type
  use empty_io_context_mod,      only: empty_io_context_type

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: io_context_collection_test_type
    private
  contains

    procedure setUp
    procedure tearDown
    procedure test_all
  end type io_context_collection_test_type

contains

  subroutine setUp( this )

    implicit none

    class(io_context_collection_test_type), intent(inout) :: this

  end subroutine setUp


  subroutine tearDown( this )

    implicit none

    class(io_context_collection_test_type), intent(inout) :: this


  end subroutine tearDown

  @test
  subroutine test_all(this)
    implicit none
    class(io_context_collection_test_type), intent(inout) :: this
    type(io_context_collection_type) :: context_collection
    type(empty_io_context_type) :: empty_context1, empty_context2
    type(empty_io_context_type), pointer :: returned_contextp1 => null(), &
                                            returned_contextp2 => null()

    call empty_context1%initialise("empty_context1")
    call empty_context2%initialise("empty_context2")

    call context_collection%initialise("context_collection", 100_i_def)

    ! Check empty contexts aren't already in collection
    @assertFalse(context_collection%context_exists(empty_context1%get_context_name()))
    @assertFalse(context_collection%context_exists(empty_context2%get_context_name()))

    ! Add empty context to collection and check it is in there.
    call context_collection%add_context(empty_context1)
    @assertTrue(context_collection%context_exists(empty_context1%get_context_name()))

    ! Add second context and check it is there and check context1 is still available
    call context_collection%add_context(empty_context2)
    @assertTrue(context_collection%context_exists(empty_context2%get_context_name()))
    @assertTrue(context_collection%context_exists(empty_context1%get_context_name()))

    ! Check random name isn't in collection
    @assertFalse(context_collection%context_exists("not in collection"))

    ! Get context from collection
    call context_collection%get_io_context(empty_context1%get_context_name(), returned_contextp1)
    @assertEqual(returned_contextp1%get_context_name(), empty_context1%get_context_name())
    call context_collection%get_io_context(empty_context2%get_context_name(), returned_contextp2)
    @assertEqual(returned_contextp2%get_context_name(), empty_context2%get_context_name())

    ! Remove empty context and check it is no longer in the collection
    call context_collection%remove_context(empty_context1%get_context_name())
    @assertFalse(context_collection%context_exists(empty_context1%get_context_name()))

    ! Check context2 is still available
    @assertTrue(context_collection%context_exists(empty_context2%get_context_name()))

  end subroutine test_all

end module io_context_collection_mod_test