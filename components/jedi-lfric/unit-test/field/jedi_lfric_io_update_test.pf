!------------------------------------------------------------------------------
! (C) Crown copyright 2023 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!------------------------------------------------------------------------------
!> @brief  Test the update_io_field_collection subroutine
!!
module jedi_lfric_io_update_test

  use constants_mod,                 only : i_def, l_def, str_def
  use field_collection_mod,          only : field_collection_type
  use fs_continuity_mod,             only : W3, Wtheta
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use halo_routing_collection_mod,   only : halo_routing_collection_type, &
                                            halo_routing_collection
  use local_mesh_mod,                only : local_mesh_type
  use mesh_mod,                      only : mesh_type
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use jedi_lfric_field_meta_mod,       only : jedi_lfric_field_meta_type
  use jedi_lfric_io_update_mod,        only : update_io_field_collection
  use extrusion_mod,                 only : TWOD

  use pFUnit_Mod

  implicit none

  private
  public :: test_jedi_lfric_io_update

  @TestCase
  type, extends(TestCase), public :: jedi_lfric_io_update_test_type
    private
    type(local_mesh_type)              :: unit_test_local_mesh
    type(mesh_type),           pointer :: mesh => null(), twod_mesh => null()
  contains
    procedure setUp
    procedure tearDown
    procedure get_local_mesh_ptr
    procedure test_jedi_lfric_io_update
  end type jedi_lfric_io_update_test_type

  integer(i_def), parameter    :: element_order = 0


contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use mesh_mod, only : mesh_type, PLANE_BI_PERIODIC, PLANE_TWOD_BI_PERIODIC

    implicit none

    class(jedi_lfric_io_update_test_type), intent(inout) :: this
    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr => null()
    type(mesh_type) :: unit_test_mesh, unit_test_mesh_2d
    integer(i_def)  :: mesh_id

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    call this%unit_test_local_mesh%initialise()
    unit_test_local_mesh_ptr => this%get_local_mesh_ptr()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    unit_test_mesh = mesh_type( PLANE_BI_PERIODIC, unit_test_local_mesh_ptr )
    mesh_id        = mesh_collection%add_new_mesh( unit_test_mesh )
    this%mesh     => mesh_collection%get_mesh( mesh_id )

    ! Create the 2D mesh
    unit_test_mesh_2d = mesh_type( PLANE_TWOD_BI_PERIODIC, unit_test_local_mesh_ptr )
    mesh_id           = mesh_collection%add_new_mesh( unit_test_mesh_2d )
    this%twod_mesh   => mesh_collection%get_mesh_variant( this%mesh, TWOD )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(jedi_lfric_io_update_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function get_local_mesh_ptr( this ) result(unit_test_local_mesh_ptr)

    implicit none

    class(jedi_lfric_io_update_test_type), intent(inout), target :: this
    type(local_mesh_type), pointer :: unit_test_local_mesh_ptr

    unit_test_local_mesh_ptr => this%unit_test_local_mesh

  end function get_local_mesh_ptr

  !> Test for update_io_field_collection
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_jedi_lfric_io_update( this )

    implicit none

    class(jedi_lfric_io_update_test_type), intent(inout) :: this

    ! Local
    type( field_collection_type )    :: io_collection
    type( jedi_lfric_field_meta_type ) :: field_meta_data_one
    type( jedi_lfric_field_meta_type ) :: field_meta_data_two
    type( jedi_lfric_field_meta_type ) :: field_meta_data_three
    integer( kind=i_def ), parameter :: nvars = 3_i_def
    character( len=str_def )         :: variable_names(nvars)
    integer( kind=i_def )            :: variable_function_spaces(nvars)
    logical( kind=l_def )            :: variable_is_2d(nvars)
    logical( kind=l_def )            :: has_field
    integer( kind=i_def )            :: collection_length
    integer( kind=i_def )            :: i

    ! Define the field information
    variable_names = (/"field1", "field2", "field3"/)
    variable_function_spaces = (/W3 , Wtheta, W3/)
    variable_is_2d = (/.false., .false., .true./)

    ! Setup the io_collection
    call io_collection%initialise(name = 'io_collection', table_len=100)

    ! 1. Setup meta data with the first two fields
    call field_meta_data_two%initialise( variable_names(1:2),           &
                                         variable_function_spaces(1:2), &
                                         variable_is_2d(1:2) )

    ! Update the field collection with the fields defined in field_meta_data_two
    call update_io_field_collection( io_collection, this%mesh, this%twod_mesh, &
                                     field_meta_data_two )

    ! There should be 2 fields in the collection
    collection_length = io_collection%get_length()
    @assertEqual(2_i_def, collection_length)

    ! The first two fields should be present but the third not
    do i=1,2
      has_field = io_collection%field_exists(variable_names(i))
      @assertTrue( has_field )
    end do
    has_field = io_collection%field_exists(variable_names(3))
    @assertFalse( has_field )

    ! 2. Setup meta data with the first field
    call field_meta_data_one%initialise( variable_names(1:1),           &
                                         variable_function_spaces(1:1), &
                                         variable_is_2d(1:1) )

    ! Update the field collection with the fields defined in field_meta_data_one
    call update_io_field_collection( io_collection, this%mesh, this%twod_mesh, &
                                     field_meta_data_one )

    ! There should be 1 field in the collection
    collection_length = io_collection%get_length()
    @assertEqual(collection_length, 1_i_def)

    ! Check to see if the missing field is present
    has_field = io_collection%field_exists(variable_names(1))
    @assertTrue( has_field )
    do i=2,3
      has_field = io_collection%field_exists(variable_names(i))
      @assertFalse( has_field )
    end do

    ! 3. Setup meta data with all three fields
    call field_meta_data_three%initialise( variable_names,           &
                                           variable_function_spaces, &
                                           variable_is_2d )

    ! Update the field collection with the fields defined in field_meta_data_three
    call update_io_field_collection( io_collection, this%mesh, this%twod_mesh, &
                                     field_meta_data_three )

    ! There should be 3 fields in the collection
    collection_length = io_collection%get_length()
    @assertEqual(collection_length, 3_i_def)

    ! Check to see if the missing field is present
    do i=1,3
      has_field = io_collection%field_exists(variable_names(i))
      @assertTrue( has_field )
    end do

  end subroutine test_jedi_lfric_io_update

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

end module jedi_lfric_io_update_test
