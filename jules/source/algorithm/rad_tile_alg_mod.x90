!------------------------------------------------------------------------------
! (c) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!------------------------------------------------------------------------------
! @brief Calculate properties of tiles for radiative transfer

module rad_tile_alg_mod

use field_mod,                     only: field_type
use field_collection_mod,          only: field_collection_type
use function_space_collection_mod, only: function_space_collection
use function_space_mod,            only: function_space_type
use constants_mod,                 only: r_def, i_def
use io_config_mod,                 only: subroutine_timers
use timer_mod,                     only: timer
use jules_control_init_mod,        only: sw_band_tile, lw_band_tile
use physical_op_constants_mod,     only: get_dz_at_wtheta
use fs_continuity_mod,             only: W3, Wtheta
use radiation_config_mod,          only: l_planet_grey_surface, &
                                         planet_emissivity, planet_albedo, &
                                         n_radstep
use section_choice_config_mod,     only: surface, surface_jules

use sw_rad_tile_kernel_mod, only: sw_rad_tile_kernel_type
use lw_rad_tile_kernel_mod, only: lw_rad_tile_kernel_type

implicit none
private

!------------------------------------------------------------------------------
! Contained functions/subroutines
!------------------------------------------------------------------------------
public :: rad_tile_alg

contains

! @param[in,out] tile_sw_direct_albedo  Tile SW direct albedos
! @param[in,out] tile_sw_diffuse_albedo Tile SW diffuse albedos
! @param[in,out] tile_lw_albedo         Tile LW albedos (1 - emissivity)
! @param[in]     derived_fields         Derived fields
! @param[in,out] radition_fields        Fields for radiation scheme
! @param[in]     orography_fields       Fields for orog drag scheme
! @param[in]     surface_fields         Fields for surface scheme
! @param[in]     soil_fields            Fields for soil hydrology scheme
! @param[in]     snow_fields            Fields for snow scheme
! @param[in]     twod_mesh_id           Identifier given to current 2d mesh
! @param[in]     timestep               Current timestep number
subroutine rad_tile_alg(tile_sw_direct_albedo, tile_sw_diffuse_albedo,   &
                        tile_lw_albedo, derived_fields,                  &
                        radiation_fields, orography_fields,              &
                        surface_fields, soil_fields, snow_fields,        &
                        twod_mesh_id, timestep)

  implicit none

  type( field_type ), intent( inout ) :: tile_sw_direct_albedo
  type( field_type ), intent( inout ) :: tile_sw_diffuse_albedo
  type( field_type ), intent( inout ) :: tile_lw_albedo
  type( field_collection_type ), intent(in)    :: derived_fields
  type( field_collection_type ), intent(inout) :: radiation_fields
  type( field_collection_type ), intent(in)    :: orography_fields
  type( field_collection_type ), intent(in)    :: surface_fields
  type( field_collection_type ), intent(in)    :: soil_fields
  type( field_collection_type ), intent(in)    :: snow_fields
  integer( kind=i_def ), intent(in) :: twod_mesh_id, timestep

  ! Unpacked fields from collections
  type( field_type ), pointer :: sd_orog           => null()

  type( field_type ), pointer :: tile_fraction     => null()
  type( field_type ), pointer :: leaf_area_index   => null()
  type( field_type ), pointer :: canopy_height     => null()
  type( field_type ), pointer :: tile_temperature  => null()
  type( field_type ), pointer :: albedo_obs_scaling => null()
  type( field_type ), pointer :: chloro_sea        => null()
  type( field_type ), pointer :: sea_ice_thickness => null()
  type( field_type ), pointer :: z0msea            => null()

  type( field_type ), pointer :: soil_albedo       => null()
  type( field_type ), pointer :: soil_roughness    => null()

  type( field_type ), pointer :: albedo_obs_vis    => null()
  type( field_type ), pointer :: albedo_obs_nir    => null()
  type( field_type ), pointer :: cos_zenith_angle_rts => null()

  type( field_type ), pointer :: tile_snow_mass    => null()
  type( field_type ), pointer :: tile_snow_rgrain  => null()
  type( field_type ), pointer :: snow_depth        => null()
  type( field_type ), pointer :: snowpack_density  => null()
  type( field_type ), pointer :: snow_soot         => null()

  type( field_type ), pointer :: u1_in_w3          => null()
  type( field_type ), pointer :: u2_in_w3          => null()

  type( field_type ), pointer :: dz_wth            => null()

  ! Pointers to vector spaces
  type(function_space_type), pointer :: vector_space => null()

  ! Grey LW surface albedo
  real( kind=r_def ) :: planet_lw_albedo


  if ( subroutine_timers ) call timer('rad_tile_alg')

  vector_space=>function_space_collection%get_fs(twod_mesh_id,0,W3,sw_band_tile)
  call tile_sw_direct_albedo%initialise(vector_space)
  call tile_sw_diffuse_albedo%initialise(vector_space)

  vector_space=>function_space_collection%get_fs(twod_mesh_id,0,W3,lw_band_tile)
  call tile_lw_albedo%initialise(vector_space)

  if (l_planet_grey_surface) then

    planet_lw_albedo = 1.0_r_def - planet_emissivity
    call invoke(setval_c(tile_sw_direct_albedo,  planet_albedo), &
                setval_c(tile_sw_diffuse_albedo, planet_albedo), &
                setval_c(tile_lw_albedo,         planet_lw_albedo))

  else if (surface == surface_jules) then

    tile_fraction     => surface_fields%get_field('tile_fraction')

    ! SW albedos only required on radiation timesteps
    if (mod(timestep-1_i_def, n_radstep) == 0) then

      sd_orog           => orography_fields%get_field('sd_orog')

      leaf_area_index   => surface_fields%get_field('leaf_area_index')
      canopy_height     => surface_fields%get_field('canopy_height')
      tile_temperature  => surface_fields%get_field('tile_temperature')
      albedo_obs_scaling => surface_fields%get_field('albedo_obs_scaling')
      chloro_sea        => surface_fields%get_field('chloro_sea')
      sea_ice_thickness => surface_fields%get_field('sea_ice_thickness')
      z0msea            => surface_fields%get_field('z0msea')

      soil_albedo       => soil_fields%get_field('soil_albedo')
      soil_roughness    => soil_fields%get_field('soil_roughness')

      albedo_obs_vis    => radiation_fields%get_field('albedo_obs_vis')
      albedo_obs_nir    => radiation_fields%get_field('albedo_obs_nir')
      cos_zenith_angle_rts => radiation_fields%get_field('cos_zenith_angle_rts')

      tile_snow_mass    => snow_fields%get_field('tile_snow_mass')
      tile_snow_rgrain  => snow_fields%get_field('tile_snow_rgrain')
      snow_depth        => snow_fields%get_field('snow_depth')
      snowpack_density  => snow_fields%get_field('snowpack_density')
      snow_soot         => snow_fields%get_field('snow_soot')

      u1_in_w3          => derived_fields%get_field('u1_in_w3')
      u2_in_w3          => derived_fields%get_field('u2_in_w3')

      dz_wth            => get_dz_at_wtheta()

      call invoke(sw_rad_tile_kernel_type(tile_sw_direct_albedo, &
           tile_sw_diffuse_albedo, &
           tile_fraction, &
           leaf_area_index, &
           canopy_height, &
           sd_orog, &
           soil_albedo, &
           soil_roughness, &
           albedo_obs_vis, &
           albedo_obs_nir, &
           albedo_obs_scaling, &
           tile_temperature, &
           tile_snow_mass, &
           tile_snow_rgrain, &
           snow_depth, &
           snowpack_density, &
           snow_soot, &
           chloro_sea, &
           sea_ice_thickness, &
           u1_in_w3, &
           u2_in_w3, &
           dz_wth, &
           z0msea, &
           cos_zenith_angle_rts))

    end if

    ! LW albedos always required
    call invoke(lw_rad_tile_kernel_type(tile_lw_albedo, &
                                        tile_fraction))

  end if

  if ( subroutine_timers ) call timer('rad_tile_alg')

end subroutine rad_tile_alg
end module rad_tile_alg_mod
