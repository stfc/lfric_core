!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

module gungho_extrusion_mod_test

  use constants_mod,           only : i_def, r_def
  use extrusion_mod,           only : extrusion_type
  use gungho_extrusion_mod,    only : create_extrusion
  use gungho_feign_config_mod, only : feign_base_mesh_config, &
                                      feign_extrusion_config, &
                                      feign_planet_config
  use yaxt,                    only : xt_initialize, xt_finalize
  use mpi_mod,                 only : store_comm, clear_comm
  use pFUnit_mod

  implicit none

  private
  public test_planar, test_spherical

  @TestCase
  type, public, extends(MPITestCase) :: gungho_extrusion_test_type
    private
  contains
    private
    procedure, public  :: setUp
    procedure, public  :: tearDown
    procedure, public  :: test_planar
    procedure, public  :: test_spherical
    procedure          :: identify_extrusion
  end type gungho_extrusion_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    implicit none

    class(gungho_extrusion_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(gungho_extrusion_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_planar( this )

    use base_mesh_config_mod, only : base_mesh_geometry_planar, &
                                     base_mesh_partitioner_planar
    use extrusion_config_mod, only : extrusion_method_uniform

    implicit none

    class(gungho_extrusion_test_type), intent(inout) :: this

    class(extrusion_type), allocatable :: unit_under_test

    call feign_base_mesh_config(                       &
             filename='/foo.nc',                       &
             prime_mesh_name='unit_test',              &
             geometry=base_mesh_geometry_planar,       &
             partitioner=base_mesh_partitioner_planar, &
             fplane=.false.,                           &
             f_lat_deg=0.0_r_def )
    call feign_extrusion_config( domain_top=13.5_r_def,           &
                                 method=extrusion_method_uniform, &
                                 number_of_layers=7_i_def )
    call feign_planet_config( gravity=9.98_r_def,    &
                              radius=637100.0_r_def, &
                              omega=7.27E-5_r_def,   &
                              rd=300.0_r_def,        &
                              cp=1000.0_r_def,       &
                              p_zero=100000.0_r_def, &
                              scaling_factor=0.5_r_def )

    allocate( unit_under_test, source=create_extrusion() )

    @assertEqual( 'uniform',  trim( this%identify_extrusion( unit_under_test ) ) )
    @assertEqual( 0.0_r_def,  unit_under_test%get_atmosphere_bottom() )
    @assertEqual( 13.5_r_def, unit_under_test%get_atmosphere_top() )
    @assertEqual( 7_i_def,    unit_under_test%get_number_of_layers() )

    deallocate( unit_under_test )

  end subroutine test_planar

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_spherical( this )

    use base_mesh_config_mod, only : base_mesh_geometry_spherical, &
                                     base_mesh_partitioner_cubedsphere
    use extrusion_config_mod, only : extrusion_method_geometric

    implicit none

    class(gungho_extrusion_test_type), intent(inout) :: this

    class(extrusion_type), allocatable :: unit_under_test

    call feign_base_mesh_config(                            &
             filename='/foo.nc',                            &
             prime_mesh_name='prime',                       &
             geometry=base_mesh_geometry_spherical,         &
             partitioner=base_mesh_partitioner_cubedsphere, &
             fplane=.false.,                                &
             f_lat_deg=0.0_r_def )

    call feign_extrusion_config( method=extrusion_method_geometric, &
                                 domain_top=600000.0_r_def,         &
                                 number_of_layers=7_i_def )
    call feign_planet_config( gravity=9.98_r_def,    &
                              radius=637100.0_r_def, &
                              omega=7.27E-5_r_def,   &
                              rd=300.0_r_def,        &
                              cp=1000.0_r_def,       &
                              p_zero=100000.0_r_def, &
                              scaling_factor=2.0_r_def )

    allocate( unit_under_test, source=create_extrusion() )

    @assertEqual( 'geometric',    trim( this%identify_extrusion( unit_under_test ) ) )
    @assertEqual( 318550.0_r_def, unit_under_test%get_atmosphere_bottom() )
    @assertEqual( 600000.0_r_def, unit_under_test%get_atmosphere_top() )
    @assertEqual( 7_i_def,        unit_under_test%get_number_of_layers() )

    deallocate( unit_under_test )

  end subroutine test_spherical

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  function identify_extrusion( this, extrusion ) result(id)

    use extrusion_mod, only : uniform_extrusion_type,   &
                              quadratic_extrusion_type, &
                              geometric_extrusion_type, &
                              dcmip_extrusion_type
    implicit none

    class(gungho_extrusion_test_type), intent(in) :: this
    class(extrusion_type),             intent(in) :: extrusion
    character(9) :: id

    id = 'unknown  '

    select type (extrusion)
      type is (uniform_extrusion_type)
        id = 'uniform'
      type is (quadratic_extrusion_type)
        id = 'quadratic'
      type is (geometric_extrusion_type)
        id = 'geometric'
      type is (dcmip_extrusion_type)
        id = 'dcmip'
    end select

  end function identify_extrusion

end module gungho_extrusion_mod_test
