!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the assembly of the term diag(D_h.M_{2,h,inv}.D_h^T) into a columnwise
!> operator. This term is required in the gravity wave test
!>
module columnwise_op_asm_diag_hmht_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W2H, W3
  use function_space_mod,            only : function_space_type

  use operator_mod,                  only : operator_type,                 &
                                            operator_proxy_type,           &
                                            columnwise_operator_type,      &
                                            columnwise_operator_proxy_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: columnwise_op_asm_diag_hmht_test_type
    private
    type(function_space_type), pointer :: w2h_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(operator_type)                :: div
    type(operator_type)                :: M2h
    type(columnwise_operator_type)     :: columnwise_diag_hmht
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type columnwise_op_asm_diag_hmht_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    integer :: i
    integer :: alpha, beta, gamma_m, gamma_p

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(                          &
             cellshape=finite_element_cellshape_quadrilateral, &
             coordinate_order=0,                               &
             element_order=0,                                  &
             rehabilitate=.true.,                              &
             supg=.false.,                                     &
             vorticity_in_w1=.false. )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%w2h_fs => function_space_collection%get_fs( this%mesh_id, &
                                                     element_order, &
                                                     W2H )

    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W3 )

    this%div = operator_type( this%w3_fs,  this%w2h_fs )
    this%M2h = operator_type( this%w2h_fs, this%w2h_fs )

    alpha = 1
    beta = 1
    gamma_m = 0
    gamma_p = 0
    this%columnwise_diag_hmht = columnwise_operator_type( this%w3_fs, this%w3_fs, &
                                                          alpha, beta, gamma_m, gamma_p )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use columnwise_op_asm_diag_hmht_kernel_mod, only : &
         columnwise_op_asm_diag_hmht_kernel_code

    implicit none

    class(columnwise_op_asm_diag_hmht_test_type), intent(inout) :: this

    real(r_def), parameter                          :: tol = 1.0e-6_r_def
    integer                                         :: i, j, k, cell
    integer                                         :: ndf_w3, ndf_w2h
    integer                                         :: nlayers, ncell_2d
    type(operator_proxy_type)                       :: div_p
    type(operator_proxy_type)                       :: M2h_p
    type(columnwise_operator_proxy_type)            :: columnwise_diag_hmht_p

    real(kind=r_def), allocatable, dimension(:,:,:) :: local_stencil_div
    real(kind=r_def), allocatable, dimension(:,:,:) :: local_stencil_M2h
    real(kind=r_def),              dimension(4)     :: rho
    real(kind=r_def),              dimension(4)     :: sigma
    real(kind=r_def)                                :: answer

    rho(1) = 2.0_r_def
    rho(2) = 3.0_r_def
    rho(3) = 4.0_r_def
    rho(4) = 5.0_r_def
    sigma(1) = 2.0_r_def
    sigma(2) = 7.0_r_def
    sigma(3) = 9.1_r_def
    sigma(4) = 4.8_r_def
    ! Test mass matrix kernel
    div_p = this%div%get_proxy()
    columnwise_diag_hmht_p = this%columnwise_diag_hmht%get_proxy()
    M2h_p = this%M2h%get_proxy()

    nlayers  = columnwise_diag_hmht_p%fs_from%get_nlayers()
    ncell_2d = columnwise_diag_hmht_p%ncell_2d
    ndf_w3  = div_p%fs_to%get_ndf()
    ndf_w2h  = div_p%fs_from%get_ndf()

    allocate(local_stencil_div(ndf_w3, ndf_w2h,div_p%ncell_3d))
    allocate(local_stencil_M2h(ndf_w2h,ndf_w2h,div_p%ncell_3d))

    cell = 1

    do i=1, div_p%ncell_3d
       local_stencil_div(:,:,i) = reshape( (/ rho(1), rho(2), rho(3), rho(4) /),&
                                             (/1, 4/) )

       local_stencil_M2h(:,:,i) = reshape( (/ sigma(1),0.0_r_def,0.0_r_def,0.0_r_def,   &
                                              0.0_r_def,sigma(2),0.0_r_def,0.0_r_def,   &
                                              0.0_r_def,0.0_r_def,sigma(3),0.0_r_def,   &
                                              0.0_r_def,0.0_r_def,0.0_r_def,sigma(4) /),&
                                              (/4, 4/) )
    end do

    call columnwise_op_asm_diag_hmht_kernel_code(cell,                                     &
                                                 nlayers,                                  &
                                                 ncell_2d,                                 &
                                                 div_p%ncell_3d,                           &
                                                 local_stencil_div,                        &
                                                 div_p%ncell_3d,                           &
                                                 local_stencil_M2h,                        &
                                                 columnwise_diag_hmht_p%columnwise_matrix, &
                                                 columnwise_diag_hmht_p%nrow,              &
                                                 columnwise_diag_hmht_p%bandwidth,         &
                                                 columnwise_diag_hmht_p%alpha,             &
                                                 columnwise_diag_hmht_p%beta,              &
                                                 columnwise_diag_hmht_p%gamma_m,           &
                                                 columnwise_diag_hmht_p%gamma_p,           &
                                                 ndf_w3,                                   &
                                                 columnwise_diag_hmht_p%column_banded_dofmap_to, &
                                                 ndf_w2h)

    answer = 0.0_r_def
    do i=1,4
       answer = answer + rho(i)**2/sigma(i)
    end do

    do i = 1, columnwise_diag_hmht_p%nrow
      ! All other rows should be ( 3/2 )
      @assertEqual(answer, columnwise_diag_hmht_p%columnwise_matrix(1,i,1), tol)
    end do

    deallocate(local_stencil_div)
    deallocate(local_stencil_M2h)

  end subroutine test_all

end module columnwise_op_asm_diag_hmht_kernel_mod_test
