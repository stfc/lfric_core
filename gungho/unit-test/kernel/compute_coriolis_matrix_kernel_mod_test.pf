!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the W2 mass matrix computation
!>
module compute_coriolis_matrix_kernel_mod_test

  use constants_mod,                 only : i_def, r_def, PI
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W0, W2
  use function_space_mod,            only : function_space_type
  use operator_mod,                  only : operator_type, &
                                            operator_proxy_type
  use pFUnit_Mod
  use quadrature_xyoz_mod,           only : quadrature_xyoz_type, &
                                            quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: compute_coriolis_matrix_test_type
    private
    type(quadrature_xyoz_type)         :: qr
    type(quadrature_xyoz_proxy_type)   :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(operator_type)                :: mm
    real(r_def ), allocatable          :: diff_basis(:,:,:,:), &
                                          chi_basis(:,:,:,:),  &
                                          basis(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_coriolis_matrix_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: f_lat = PI/4.0_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use gungho_feign_config_mod,   only : feign_planet_config, &
                                          feign_base_mesh_config
    implicit none

    class(compute_coriolis_matrix_test_type), intent(inout) :: this

    type(field_proxy_type)              :: chi_p(3)
    type(operator_proxy_type)           :: mm_p
    type(quadrature_rule_gaussian_type) :: quadrature_rule

    integer(i_def) :: dim_mm, diff_dim, chi_dim
    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf, ndf_chi

    integer(i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.true., f_lat_deg=f_lat )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W2 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    chi_dim  = chi_p(1)%vspace%get_dim_space( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate( this%diff_basis( diff_dim, ndf_chi, nqp_h, nqp_v ), &
              this%chi_basis(  chi_dim,  ndf_chi, nqp_h, nqp_v ) )

    this%mm = operator_type( this%w2_fs, this%w2_fs )
    mm_p    = this%mm%get_proxy()

    ndf    = mm_p%fs_from%get_ndf()
    dim_mm = mm_p%fs_from%get_dim_space( )
    allocate( this%basis( dim_mm, ndf, nqp_h, nqp_v ) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(compute_coriolis_matrix_test_type), intent(inout) :: this

    deallocate( this%basis, this%diff_basis )

    call mesh_collection%clear()
    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_coriolis_matrix_kernel_mod, only : compute_coriolis_matrix_code
    use mesh_mod,                           only : mesh_type

    implicit none

    class(compute_coriolis_matrix_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def

    type(field_proxy_type)   :: chi_p(3)

    real(kind=r_def) :: answer(6,6)

    integer(i_def) :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)

    ! Required for calling mass matrix kernel

    integer(i_def) :: nlayers, ndf
    integer(i_def) :: ndf_chi, undf_chi
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def), pointer   :: map_chi(:)  => null()
    type(operator_proxy_type) :: mm_p

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    type(mesh_type), pointer  :: mesh => null()

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test mass matrix kernel
    mm_p = this%mm%get_proxy()

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_chi      = chi_p(1)%vspace%get_ndf()
    undf_chi     = chi_p(1)%vspace%get_undf()

    nlayers  = mm_p%fs_from%get_nlayers()
    ndf  = mm_p%fs_from%get_ndf()

    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         this%diff_basis,ndf_chi, nqp_h, nqp_v, xp, zp )
    call chi_p(1)%vspace%compute_basis_function(                         &
         this%chi_basis,ndf_chi, nqp_h, nqp_v, xp, zp )

    call mm_p%fs_from%compute_basis_function(                            &
         this%basis, ndf, nqp_h, nqp_v, xp, zp )

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)

    mm_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_coriolis_matrix_code( cell,                             &
                                      nlayers,                           &
                                      mm_p%ncell_3d,                     &
                                      mm_p%local_stencil,                &
                                      chi_p(1)%data,                     &
                                      chi_p(2)%data,                     &
                                      chi_p(3)%data,                     &
                                      ndf,                               &
                                      this%basis,                        &
                                      ndf_chi,                           &
                                      undf_chi,                          &
                                      map_chi,                           &
                                      this%chi_basis,                    &
                                      this%diff_basis,                   &
                                      nqp_h,                             &
                                      nqp_v,                             &
                                      wh,                                &
                                      wv                                 &
                                      )
    call coriolis_matrix_w2(answer)
    ! Normalise by 1/average edge length
    answer(:,:) = answer(:,:) * ( dx + dy + dz ) / 3.0_r_def
    mm_p%local_stencil(:, :, 1 ) = mm_p%local_stencil(:, :, 1 ) &
                                * ( dx + dy + dz ) / 3.0_r_def

    @assertEqual(answer, mm_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine coriolis_matrix_w2( mm )
    implicit none

    real(kind=r_def), dimension(6,6), intent(inout) :: mm
    mm = reshape( [ &
  0.000000000000000E+000_r_def,  2.498890612217043E-010_r_def,  0.000000000000000E+000_r_def,  2.498890612217044E-010_r_def,  3.645715452095383E-008_r_def,  3.645715452095383E-008_r_def, &
 -2.498890612217043E-010_r_def,  0.000000000000000E+000_r_def, -2.498890612217046E-010_r_def,  0.000000000000000E+000_r_def,  0.000000000000000E-000_r_def, -0.000000000000000E-000_r_def, &
  0.000000000000000E+000_r_def,  2.498890612217046E-010_r_def,  0.000000000000000E+000_r_def,  2.498890612217045E-010_r_def,  3.645715452095383E-008_r_def,  3.645715452095383E-008_r_def, &
 -2.498890612217044E-010_r_def,  0.000000000000000E+000_r_def, -2.498890612217045E-010_r_def,  0.000000000000000E+000_r_def,  0.000000000000000E-000_r_def,  0.000000000000000E-000_r_def, &
 -3.645715452095383E-008_r_def,  0.000000000000000E-000_r_def, -3.645715452095383E-008_r_def,  0.000000000000000E-000_r_def,  0.000000000000000E-000_r_def,  0.000000000000000E-000_r_def, &
 -3.645715452095382E-008_r_def,  0.000000000000000E-000_r_def, -3.645715452095383E-008_r_def,  0.000000000000000E-000_r_def,  0.000000000000000E-000_r_def,  0.000000000000000E-000_r_def  &
    ], shape(mm) )
    return
  end subroutine coriolis_matrix_w2

end module compute_coriolis_matrix_kernel_mod_test
