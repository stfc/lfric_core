!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the wind initialization by sampling
!>
module sample_initial_u_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W2, WCHI
  use quadrature_xyoz_mod,           only : quadrature_xyoz_type
  use pFUnit_Mod

  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: sample_initial_u_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: sample_initial_u_test_type
    private
    type(quadrature_xyoz_type)         :: qr
    type(function_space_type), pointer :: chi_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: wind
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_initial_u_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: u0             = 0.0_r_def
  real(r_def), parameter :: v0             = 0.0_r_def
  real(r_def), parameter :: sbr_angle_lat  = 0.0_r_def
  real(r_def), parameter :: sbr_angle_lon  = 0.0_r_def
  real(r_def), parameter :: wind_time_period = 10.0_r_def
  real(r_def), parameter :: nl_constant = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use gungho_feign_config_mod,   only : feign_initial_wind_config
    use initial_wind_config_mod,   only : initial_wind_profile_none
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(sample_initial_u_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: wind_proxy

    integer(i_def) :: mesh_id

    integer(i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_initial_wind_config(              &
             nl_constant=nl_constant,            &
             profile=initial_wind_profile_none,  &
             sbr_angle_lat=sbr_angle_lat,        &
             sbr_angle_lon=sbr_angle_lon,        &
             smp_init_wind=.false.,              &
             u0=u0, v0=v0,                       &
             wind_time_period=wind_time_period )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%chi_fs => function_space_collection%get_fs( mesh_id, element_order+1, WCHI )
    this%w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )

    this%wind   = field_type( vector_space = this%w2_fs )
    wind_proxy  = this%wind%get_proxy()

    ! Make a coordinate field with a function space
    do i = 1,3
      this%chi(i) = field_type( vector_space = this%chi_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(sample_initial_u_test_type), intent(inout) :: this

    call mesh_collection%clear()
    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use sample_initial_u_kernel_mod, only : sample_initial_u_code

   implicit none

    class(sample_initial_u_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    type(field_proxy_type)   :: chi_p(3)

    real(kind=r_def) :: answer

    integer(i_def) :: i, j, k, cell, err, df

    integer(i_def)                 :: nlayers
    integer(i_def)                 :: ndf_chi, ndf_w2
    integer(i_def)                 :: undf_chi, undf_w2
    integer(i_def), pointer        :: map_w2(:) => null()
    integer(i_def), pointer        :: map_chi(:) => null()

    type(field_proxy_type)        :: wind_proxy

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates, hard-wired for P1DG case
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,2
          chi_p(1)%data(map_chi(1:7:2) + k) = real(i-1, r_def)*dx
          chi_p(1)%data(map_chi(2:8:2) + k) = real(i, r_def)*dx
      
          chi_p(2)%data(map_chi(1:2) + k) = real(j-1, r_def)*dy
          chi_p(2)%data(map_chi(5:6) + k) = real(j-1, r_def)*dy
          chi_p(2)%data(map_chi(3:4) + k) = real(j, r_def)*dy
          chi_p(2)%data(map_chi(7:8) + k) = real(j, r_def)*dy
      
          chi_p(3)%data(map_chi(1:4) + k) = real(k, r_def)*dz
          chi_p(3)%data(map_chi(5:8) + k) = real(k+1, r_def)*dz
        end do
        cell = cell + 1
      end do
    end do
    ! Test kernel
    wind_proxy  = this%wind%get_proxy()

    nlayers = wind_proxy%vspace%get_nlayers()

    ndf_w2  = wind_proxy%vspace%get_ndf( )
    undf_w2 = wind_proxy%vspace%get_undf()

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()

    cell = 3

    map_w2  => wind_proxy%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    wind_proxy%data(:) = 0.0_r_def

    call sample_initial_u_code( nlayers,                                   &
                                wind_proxy%data,                           &
                                chi_p(1)%data,                             &
                                chi_p(2)%data,                             &
                                chi_p(3)%data,                             &
                                ndf_w2, undf_w2,                           &
                                map_w2, ndf_chi,                           &
                                undf_chi, map_chi                          &
                                )

    ! Test
    answer = 0.0_r_def
    do k = 0,2
      @assertEqual(answer, wind_proxy%data(map_w2(1)+k), tol)
      @assertEqual(answer, wind_proxy%data(map_w2(2)+k), tol)
    end do

  end subroutine test_all

end module sample_initial_u_kernel_mod_test
