!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the vorticity advection kernel computation
!>
module w2_vorticity_advection_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W1, W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type, &
                                               quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: w2_vorticity_advection_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: w2_vorticity_advection_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: r_u, xi
    real(r_def), allocatable           :: basis_w2(:,:,:,:), &
                                          basis_w1(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type w2_vorticity_advection_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_profile_mod,     only : reference_profile

    implicit none

    class(w2_vorticity_advection_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy, xi_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_w0, ndf_w2
    integer(i_def) :: undf_w0, undf_w2
    integer(i_def) :: diff_dim_w0, dim_w0, dim_w2
    integer(i_def) :: mesh_id

    integer(i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)

    this%r_u  = field_type( vector_space = this%w2_fs )
    r_u_proxy = this%r_u%get_proxy()

    ndf_w2  = r_u_proxy%vspace%get_ndf( )
    dim_w2  = r_u_proxy%vspace%get_dim_space( )
    undf_w2 = r_u_proxy%vspace%get_undf()
    allocate(this%basis_w2(dim_w2,ndf_w2,nqp_h,nqp_v))

    this%xi  = field_type( vector_space = this%w2_fs )

    ! Make a coordinate field with a function space
    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    diff_dim_w0 = chi_p(1)%vspace%get_dim_space_diff( )
    dim_w0      = chi_p(1)%vspace%get_dim_space()
    undf_w0     = chi_p(1)%vspace%get_undf()
    allocate(this%diff_basis_w0(diff_dim_w0,ndf_w0,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(w2_vorticity_advection_test_type), intent(inout) :: this

    deallocate( this%basis_w2,     &
                this%diff_basis_w0 )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use w2_vorticity_advection_kernel_mod, only : w2_vorticity_advection_code

    implicit none

    class(w2_vorticity_advection_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-9_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    type(field_type)          :: mass_flux
    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: r_u_proxy, &
                                 f_proxy, xi_proxy


    integer :: i, j, k, cell, err

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_w0, ndf_w2
    integer                 :: undf_w0, undf_w2

    integer, pointer        :: map_w0(:) => null()
    integer, pointer        :: map_w2(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel
    mass_flux = field_type( vector_space = this%w2_fs )

    r_u_proxy  = this%r_u%get_proxy()
    f_proxy    = mass_flux%get_proxy()
    xi_proxy   = this%xi%get_proxy()

    nlayers = r_u_proxy%vspace%get_nlayers()

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_w2      = r_u_proxy%vspace%get_ndf( )
    undf_w2     = r_u_proxy%vspace%get_undf()

    ndf_w0      = chi_p(1)%vspace%get_ndf( )
    undf_w0     = chi_p(1)%vspace%get_undf()

    call r_u_proxy%vspace%compute_basis_function( &
                                 this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp )

    call chi_p(1)%vspace%compute_diff_basis_function( &
                            this%diff_basis_w0, ndf_w0, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w2 => r_u_proxy%vspace%get_cell_dofmap( cell )
    map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    r_u_proxy%data(:) = 0.0_r_def
    f_proxy%data(:)   = 2.0_r_def
    xi_proxy%data(:)  = 0.0_r_def

    call w2_vorticity_advection_code(                          &
                                   nlayers,                    &
                                   r_u_proxy%data,             &
                                   f_proxy%data,               &
                                   xi_proxy%data,              &
                                   chi_p(1)%data,              &
                                   chi_p(2)%data,              &
                                   chi_p(3)%data,              &
                                   ndf_w2, undf_w2,            &
                                   map_w2, this%basis_w2,      &
                                   ndf_w0, undf_w0,            &
                                   map_w0, this%diff_basis_w0, &
                                   nqp_h, nqp_v, wh, wv        &
                                   )

    do k = 0,2
      @assertEqual(answer, r_u_proxy%data(map_w2(5)+k), tol)
      @assertEqual(answer, r_u_proxy%data(map_w2(6)+k), tol)
    end do

  end subroutine test_all

end module w2_vorticity_advection_kernel_mod_test
