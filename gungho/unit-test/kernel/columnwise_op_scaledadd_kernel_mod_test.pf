!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the scaled addition of two columnwise assembled operators
!> For this, add the operators and check that the product operator
!> has the same entries as the corresponding dense matrix.
module columnwise_op_scaledadd_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : Wtheta, W3
  use function_space_mod,            only : function_space_type

  use operator_mod,                  only : columnwise_operator_type, &
                                            columnwise_operator_proxy_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: columnwise_op_scaledadd_test_type
    private
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(columnwise_operator_type)     :: columnwise_op_mass, &
                                          columnwise_op_DT_D, &
                                          columnwise_op_mass_DT_D
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type columnwise_op_scaledadd_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(columnwise_op_scaledadd_test_type), intent(inout) :: this

    type(columnwise_operator_type)     :: columnwise_op_D, &
                                          columnwise_op_DT

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(                          &
             cellshape=finite_element_cellshape_quadrilateral, &
             coordinate_order=0,                               &
             element_order=0,                                  &
             rehabilitate=.true.,                              &
             supg=.false.,                                     &
             vorticity_in_w1=.false. )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wtheta_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    0, &
                                                    Wtheta )

    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    0, &
                                                    W3 )

    ! mass matrix M
    this%columnwise_op_mass = columnwise_operator_type (this%w3_fs, &
                                                        this%w3_fs )

    ! divergence and gradient D and D^T
    columnwise_op_D    = columnwise_operator_type( this%wtheta_fs, &
                                                   this%w3_fs )
    columnwise_op_DT   = columnwise_operator_type( this%w3_fs, &
                                                   this%wtheta_fs )
    ! D^T.D
    this%columnwise_op_DT_D =  &
         columnwise_op_DT%columnwise_operator_product( &
         columnwise_op_D )

    ! M + D^T.D
    this%columnwise_op_mass_DT_D = &
         this%columnwise_op_mass%columnwise_operator_sum( &
         this%columnwise_op_DT_D )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(columnwise_op_scaledadd_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use columnwise_op_scaledadd_kernel_mod, only : &
         columnwise_op_scaledadd_kernel_code

    implicit none

    class(columnwise_op_scaledadd_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    integer :: i, j, cell

    ! scaling factor
    real(kind=r_def) :: alpha, beta

    type(columnwise_operator_proxy_type) :: columnwise_op_mass_p,      &
                                            columnwise_op_DT_D_p,      &
                                            columnwise_op_mass_DT_D_p

    ! dense representations of the operators mass, D^T.D and
    ! mass + beta*D^T.D
    real(kind=r_def), allocatable, dimension(:,:) :: densematrix_mass,     &
                                                     densematrix_DT_D,     &
                                                     densematrix_mass_DT_D

    integer(kind=i_def) :: j_minus, j_plus
    real(kind=r_def) :: matrixentry ! sparse matrix entry to compare in test

    ! Tesbt mass matrix kernel
    columnwise_op_mass_p = this%columnwise_op_mass%get_proxy()
    columnwise_op_DT_D_p = this%columnwise_op_DT_D%get_proxy()
    columnwise_op_mass_DT_D_p = this%columnwise_op_mass_DT_D%get_proxy()

    cell = 1
    alpha = 2.4_r_def
    beta = 3.7_r_def

    ! construct banded and dense matrix representation,
    allocate(densematrix_mass( columnwise_op_mass_p%nrow,      &
                               columnwise_op_mass_p%ncol) )
    allocate(densematrix_DT_D( columnwise_op_mass_p%nrow,      &
                                    columnwise_op_mass_p%ncol) )
    allocate(densematrix_mass_DT_D( columnwise_op_mass_p%nrow, &
                                    columnwise_op_mass_p%ncol) )
    densematrix_mass(:,:) = 0.0_r_def
    densematrix_DT_D(:,:) = 0.0_r_def
    densematrix_mass_DT_D(:,:) = 0.0_r_def

    columnwise_op_mass_p%columnwise_matrix(:,:,:) = 0.0_r_def
    columnwise_op_DT_D_p%columnwise_matrix(:,:,:) = 0.0_r_def

    ! Populate matrix entries
    do i = 1, columnwise_op_mass_p%nrow
       columnwise_op_DT_D_p%columnwise_matrix(1,i,cell) = +1.0_r_def
       columnwise_op_DT_D_p%columnwise_matrix(2,i,cell) = +2.0_r_def
       columnwise_op_DT_D_p%columnwise_matrix(3,i,cell) = +1.0_r_def
       columnwise_op_mass_p%columnwise_matrix(1,i,cell) = +1.0_r_def
       densematrix_mass(i,i) = +1.0_r_def
       if(i>1) then
          densematrix_DT_D(i,i-1) = +1.0_r_def
       end if
       densematrix_DT_D(i,i)   = +2.0_r_def
       if(i<columnwise_op_mass_p%nrow) then
          densematrix_DT_D(i,i+1) = +1.0_r_def
       end if
    end do
    call columnwise_op_scaledadd_kernel_code(cell,      &
         columnwise_op_mass_p%ncell_2d,                 &
         columnwise_op_mass_p%columnwise_matrix,        &
         columnwise_op_mass_p%nrow,                     &
         columnwise_op_mass_p%ncol,                     &
         columnwise_op_mass_p%bandwidth,                &
         columnwise_op_mass_p%alpha,                    &
         columnwise_op_mass_p%beta,                     &
         columnwise_op_mass_p%gamma_m,                  &
         columnwise_op_mass_p%gamma_p,                  &
         columnwise_op_DT_D_p%columnwise_matrix,        &
         columnwise_op_DT_D_p%nrow,                     &
         columnwise_op_DT_D_p%ncol,                     &
         columnwise_op_DT_D_p%bandwidth,                &
         columnwise_op_DT_D_p%alpha,                    &
         columnwise_op_DT_D_p%beta,                     &
         columnwise_op_DT_D_p%gamma_m,                  &
         columnwise_op_DT_D_p%gamma_p,                  &
         columnwise_op_mass_DT_D_p%columnwise_matrix,   &
         columnwise_op_mass_DT_D_p%nrow,                &
         columnwise_op_mass_DT_D_p%ncol,                &
         columnwise_op_mass_DT_D_p%bandwidth,           &
         columnwise_op_mass_DT_D_p%alpha,               &
         columnwise_op_mass_DT_D_p%beta,                &
         columnwise_op_mass_DT_D_p%gamma_m,             &
         columnwise_op_mass_DT_D_p%gamma_p,             &
         alpha,                                         &
         beta)

    ! Same calculation with dense matrices
    densematrix_mass_DT_D(:,:) = alpha * densematrix_mass(:,:) &
                               + beta  * densematrix_DT_D(:,:)

    ! Check result by comparing non-zero matrix entries in columnwise and
    ! dense matrices
    do i = 1, columnwise_op_mass_DT_D_p%nrow
       j_minus = ceiling( ( columnwise_op_mass_DT_D_p%alpha*i  &
                          - columnwise_op_mass_DT_D_p%gamma_p) &
                          / (1.0_r_def*columnwise_op_mass_DT_D_p%beta),i_def )
       j_plus = floor( ( columnwise_op_mass_DT_D_p%alpha*i  &
                          + columnwise_op_mass_DT_D_p%gamma_m) &
                          / (1.0_r_def*columnwise_op_mass_DT_D_p%beta),i_def )
       do j = MAX(1,j_minus), MIN( columnwise_op_mass_DT_D_p%ncol, j_plus)
          matrixentry &
             = columnwise_op_mass_DT_D_p%columnwise_matrix(j-j_minus+1,i,cell)
          @assertEqual( densematrix_mass_DT_D(i,j), matrixentry, tol )
          
       end do
    end do

    deallocate(densematrix_mass)
    deallocate(densematrix_DT_D)
    deallocate(densematrix_mass_DT_D)

  end subroutine test_all

end module columnwise_op_scaledadd_kernel_mod_test
