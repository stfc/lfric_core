!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the vorticity rhs computation
!>
module compute_total_pv_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W0, W1, W3
  use pFUnit_Mod
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type, &
                                               quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: compute_total_pv_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: compute_total_pv_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: xi, theta
    real(r_def), allocatable           :: basis_xi(:,:,:,:), &
                                          diff_basis_theta(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_total_pv_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(compute_total_pv_test_type), intent(inout) :: this

    type(field_proxy_type)    :: xi_p, theta_p
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: ndf_xi, undf_xi, dim_xi, &
                      ndf_theta, undf_theta, dim_diff_theta, &
                      ndf_pv, undf_pv, &
                      nqp_h, nqp_v

    integer(i_def) :: mesh_id

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( mesh_id, element_order, W0 )
    this%w1_fs => function_space_collection%get_fs( mesh_id, element_order, W1 )
    this%w3_fs => function_space_collection%get_fs( mesh_id, element_order, W3 )

    this%xi = field_type( vector_space = this%w1_fs )
    xi_p    = this%xi%get_proxy()

    ndf_xi  = xi_p%vspace%get_ndf( )
    undf_xi = xi_p%vspace%get_undf( )
    dim_xi  = xi_p%vspace%get_dim_space( )

    this%theta = field_type( vector_space = this%w0_fs )
    theta_p    = this%theta%get_proxy()

    ndf_theta      = theta_p%vspace%get_ndf( )
    undf_theta     = theta_p%vspace%get_undf( )
    dim_diff_theta = theta_p%vspace%get_dim_space_diff( )

    allocate( this%basis_xi(dim_xi, ndf_xi, nqp_h, nqp_v), &
              this%diff_basis_theta(dim_diff_theta, ndf_theta, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(compute_total_pv_test_type), intent(inout) :: this

    deallocate( this%basis_xi, this%diff_basis_theta )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_total_pv_kernel_mod,only : compute_total_pv_code

    implicit none

    class(compute_total_pv_test_type), intent(inout) :: this

    real(r_def), parameter :: tol    = 1.0e-6_r_def
    real(r_def), parameter :: answer = 0.0_r_def

    type(field_type)        :: chi(3)
    type(field_proxy_type ) :: chi_p(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Required for calling vorticity kernel
    type( field_type )     :: pv

    integer          :: ndf_xi, undf_xi,       &
                        ndf_theta, undf_theta, &
                        ndf_pv, undf_pv,       &
                        nqp_h, nqp_v


    integer, pointer :: map_xi(:) => null(), &
                        map_theta(:) => null(), & 
                        map_chi(:) => null(), & 
                        map_pv(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    type( field_proxy_type )        :: xi_p, theta_p, pv_p


    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*4.0_r_def
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*3.0_r_def
          chi_p(3)%data(map_chi(1) + k) = real(k)  *2.0_r_def
        end do
        cell = cell + 1
      end do
    end do

    ! Test pv computation
    pv    = field_type( vector_space = this%w3_fs )
    pv_p  = pv%get_proxy()

    ndf_pv = pv_p%vspace%get_ndf( )
    undf_pv = pv_p%vspace%get_undf( )

    xi_p    = this%xi%get_proxy()

    ndf_xi  = xi_p%vspace%get_ndf( )
    undf_xi = xi_p%vspace%get_undf( )

    theta_p = this%theta%get_proxy()

    ndf_theta      = theta_p%vspace%get_ndf( )
    undf_theta     = theta_p%vspace%get_undf( )

    call xi_p%vspace%compute_basis_function( &
         this%basis_xi, ndf_xi, nqp_h, nqp_v, xp, zp)
    call theta_p%vspace%compute_diff_basis_function( &
         this%diff_basis_theta, ndf_theta, nqp_h, nqp_v, xp, zp)

    cell = 5
    map_xi    => xi_p%vspace%get_cell_dofmap( cell )
    map_theta => theta_p%vspace%get_cell_dofmap( cell )
    map_pv    => pv_p%vspace%get_cell_dofmap( cell )

    ! Create the data
    xi_p%data(:) = 6.0_r_def
    theta_p%data(:) = 300.0_r_def
    pv_p%data(:) = 0.0_r_def

    call compute_total_pv_code(                                   &
                               xi_p%vspace%get_nlayers(),         &
                               pv_p%data,                         &
                               xi_p%data,                         &
                               theta_p%data,                      &
                               chi_p(1)%data,                     &
                               chi_p(2)%data,                     &
                               chi_p(3)%data,                     &
                               ndf_pv, undf_pv,                   &
                               map_pv,                            &
                               ndf_xi, undf_xi,                   &
                               map_xi,                            &
                               this%basis_xi,                     &
                               ndf_theta, undf_theta,             &
                               map_theta,                         &
                               this%diff_basis_theta,             &
                               ndf_theta, undf_theta,             &
                               map_theta,                         &
                               this%diff_basis_theta,             &
                               nqp_h, nqp_v,                      &
                               wh, wv)

    ! This is only testing the vertical component of vorticity
    @assertEqual(answer, pv_p%data(map_pv(1)), tol)

  end subroutine test_all

end module compute_total_pv_kernel_mod_test
