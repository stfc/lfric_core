!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the flux rhs computation
!>
module flux_rhs_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use halo_routing_collection_mod,   only : halo_routing_collection_type, &
                                            halo_routing_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_xyoz_mod,           only: quadrature_xyoz_type, &
                                           quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,  only: quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: flux_rhs_test_type
    private
      type( quadrature_xyoz_type )          :: qr
      type(quadrature_xyoz_proxy_type) :: qr_proxy
      type(function_space_type), pointer :: w0_fs => null()
      type(function_space_type), pointer :: w2_fs => null()
      type(function_space_type), pointer :: w3_fs => null()
      type(field_type)                   :: chi1
      type(field_type)                   :: chi2
      type(field_type)                   :: chi3
      type(field_type)                   :: f, rhs
      real(r_def), allocatable           :: basis_u(:,:,:,:), &
                                            basis_f(:,:,:,:), &
                                            diff_basis_chi(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type flux_rhs_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : cellshape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: f_p, rhs_p
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_f, dim_f, &
                      ndf_u, dim_u, &
                      ndf_chi, dim_diff_chi

    integer(i_def) :: mesh_id

    integer :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()
    ! Create top level halo_routing collection
    halo_routing_collection = halo_routing_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( mesh_id, element_order, W0 )
    this%w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )
    this%w3_fs => function_space_collection%get_fs( mesh_id, element_order, W3 )

    this%chi1 = field_type( vector_space = this%w0_fs )
    this%chi2 = field_type( vector_space = this%w0_fs )
    this%chi3 = field_type( vector_space = this%w0_fs )
    chi_p(1) = this%chi1%get_proxy()
    chi_p(2) = this%chi2%get_proxy()
    chi_p(3) = this%chi3%get_proxy()

    ndf_chi      = chi_p(1)%vspace%get_ndf( )
    dim_diff_chi = chi_p(1)%vspace%get_dim_space_diff( )

    this%rhs =  field_type( vector_space = this%w2_fs )
    rhs_p    = this%rhs%get_proxy()
    ndf_u    = rhs_p%vspace%get_ndf( )
    dim_u    = rhs_p%vspace%get_dim_space( )

    this%f =  field_type( vector_space = this%w3_fs )
    f_p    = this%f%get_proxy()
    ndf_f  = f_p%vspace%get_ndf( )
    dim_f  = f_p%vspace%get_dim_space( )

    allocate( this%basis_u(dim_u, ndf_u, nqp_h, nqp_v),           &
              this%basis_f(dim_f, ndf_f, nqp_h, nqp_v),           &
              this%diff_basis_chi(dim_diff_chi, ndf_chi, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    deallocate( this%basis_u, this%basis_f, this%diff_basis_chi )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call halo_routing_collection%clear()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use flux_rhs_kernel_mod,        only : flux_rhs_code

    implicit none

    class(flux_rhs_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(field_proxy_type ) :: chi_p(3)

    real(kind=r_def) :: answer(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    ! Variables for testing flux_rhs
    type( field_type ) :: u

    integer          :: ndf_f, undf_f, &
                        ndf_u, undf_u, &
                        ndf_chi, undf_chi, &
                        nqp_h, nqp_v

    integer, pointer :: map_f(:) => null(), &
                        map_u(:) => null(), & 
                        map_chi(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()

    type( field_proxy_type )        :: rhs_p, f_p, u_p


    ! Make a coordinate field with a function space
    chi_p(1) = this%chi1%get_proxy()
    chi_p(2) = this%chi2%get_proxy()
    chi_p(3) = this%chi3%get_proxy()
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*4.0_r_def
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*3.0_r_def
          chi_p(3)%data(map_chi(1) + k) = real(k)  *2.0_r_def
        end do
        cell = cell + 1
      end do
    end do

    ! Test flux rhs
    u   =  field_type( vector_space = this%w2_fs )

    rhs_p = this%rhs%get_proxy()
    f_p   = this%f%get_proxy()
    u_p   = u%get_proxy()

    ndf_u  = rhs_p%vspace%get_ndf( )
    undf_u = rhs_p%vspace%get_undf( )

    ndf_f  = f_p%vspace%get_ndf( )
    undf_f = f_p%vspace%get_undf( )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    call rhs_p%vspace%compute_basis_function( &
         this%basis_u, ndf_u, nqp_h, nqp_v, xp, zp)
    call f_p%vspace%compute_basis_function( &
         this%basis_f, ndf_f, nqp_h, nqp_v, xp, zp)
    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    cell = 5
    map_f   => f_p%vspace%get_cell_dofmap( cell )
    map_u   => rhs_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    f_p%data(:) = 2.0_r_def
    u_p%data(:) = 0.0_r_def
    rhs_p%data(:) = 0.0_r_def

    u_p%data(map_u(5)+0) = 0.0_r_def
    u_p%data(map_u(5)+1) = 1.0_r_def
    u_p%data(map_u(6)+1) = 1.0_r_def
    u_p%data(map_u(6)+2) = 0.0_r_def


    call flux_rhs_code( rhs_p%vspace%get_nlayers(), &
                        rhs_p%data, &                     
                        u_p%data, &
                        f_p%data, &
                        chi_p(1)%data, &
                        chi_p(2)%data, &
                        chi_p(3)%data, &
                        ndf_u, &
                        undf_u, &
                        map_u, &
                        this%basis_u, &
                        ndf_f, &
                        undf_f, &
                        map_f, &
                        this%basis_f, &
                        ndf_chi, &
                        undf_chi, &
                        map_chi, &
                        this%diff_basis_chi, &
                        nqp_h, &
                        nqp_v, &
                        wh, &
                        wv &
                        )

    answer(1) = 5.0_r_def/90.0_r_def
    answer(2) = 5.0_r_def/6.0_r_def * (2.0_r_def/(4.0_r_def*3.0_r_def))*2.0_r_def
    answer(3) = 5.0_r_def/6.0_r_def * (2.0_r_def/(4.0_r_def*3.0_r_def))*2.0_r_def

    @assertEqual(answer, rhs_p%data(map_u(5):map_u(5)+2) , tol)

  end subroutine test_all

end module flux_rhs_kernel_mod_test
