!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the Wtheta mass columnwise matrix computation
!>
module columnwise_op_asm_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : Wtheta
  use function_space_mod,            only : function_space_type

  use operator_mod,                  only : operator_type,                 &
                                            operator_proxy_type,           &
                                            columnwise_operator_type,      &
                                            columnwise_operator_proxy_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: columnwise_op_asm_test_type
    private
    type(function_space_type), pointer :: wtheta_fs => null()
    type(operator_type)                :: mm
    type(columnwise_operator_type)     :: columnwise_mm
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type columnwise_op_asm_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(columnwise_op_asm_test_type), intent(inout) :: this

    integer :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(                          &
             cellshape=finite_element_cellshape_quadrilateral, &
             coordinate_order=0,                               &
             element_order=0,                                  &
             rehabilitate=.true.,                              &
             supg=.false.,                                     &
             vorticity_in_w1=.false. )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%wtheta_fs => function_space_collection%get_fs( this%mesh_id, &
                                                        element_order, &
                                                        WTHETA )

    this%mm = operator_type( this%wtheta_fs, this%wtheta_fs )

    this%columnwise_mm = columnwise_operator_type( this%wtheta_fs, this%wtheta_fs )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(columnwise_op_asm_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use columnwise_op_asm_kernel_mod, only : &
         columnwise_op_asm_kernel_code

    implicit none

    class(columnwise_op_asm_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    real(kind=r_def) :: answer

    integer :: i, j, k, cell

    integer :: nlayers, ndf

    type(operator_proxy_type) :: mm_p
    type(columnwise_operator_proxy_type) :: columnwise_mm_p

    real(kind=r_def), allocatable, dimension(:,:,:) :: local_stencil

    ! Test mass matrix kernel
    mm_p = this%mm%get_proxy()
    columnwise_mm_p = this%columnwise_mm%get_proxy()

    nlayers  = columnwise_mm_p%fs_from%get_nlayers()
    ndf  = columnwise_mm_p%fs_from%get_ndf()

    allocate(local_stencil(ndf,ndf,mm_p%ncell_3d))

    cell = 1

    do i=1, mm_p%ncell_3d
       local_stencil(:,:,i) = reshape( (/ 1.0_r_def/3.0_r_def, 1.0_r_def/6.0_r_def, &
                                 1.0_r_def/6.0_r_def, 1.0_r_def/3.0_r_def /), (/2, 2/))
    end do

    call columnwise_op_asm_kernel_code(cell,                      &
                                       nlayers,                   &
                                       columnwise_mm_p%ncell_2d,  &
                                       mm_p%ncell_3d,             &
                                       local_stencil,             &
                                       columnwise_mm_p%columnwise_matrix,    &
                                       columnwise_mm_p%nrow,      &
                                       columnwise_mm_p%ncol,      &
                                       columnwise_mm_p%bandwidth, &
                                       columnwise_mm_p%alpha,     &
                                       columnwise_mm_p%beta,      &
                                       columnwise_mm_p%gamma_m,   &
                                       columnwise_mm_p%gamma_p,   &
                                       ndf,                       &
                                       columnwise_mm_p%column_banded_dofmap_to, &
                                       ndf,                       &
                                       columnwise_mm_p%column_banded_dofmap_from)
    do i=1, columnwise_mm_p%nrow
      if (i == 1) then
         ! First row should be ( *, 1/3, 1/6 )
         answer = (1.0_r_def/3.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(2,i,1), tol)
         answer = (1.0_r_def/6.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(3,i,1), tol)
      else if (i == columnwise_mm_p%nrow) then
         ! Final row should be ( 1/6, 1/3, * )
         answer = (1.0_r_def/6.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(1,i,1), tol)
         answer = (1.0_r_def/3.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(2,i,1), tol)
      else
         ! All other rows should be ( 1/6, 2/3, 1/6 )

         answer = (1.0_r_def/6.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(1,i,1), tol)
         answer = (2.0_r_def/3.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(2,i,1), tol)
         answer = (1.0_r_def/6.0_r_def)
         @assertEqual(answer, columnwise_mm_p%columnwise_matrix(3,i,1), tol)
      end if
    end do

    deallocate(local_stencil)

  end subroutine test_all

end module columnwise_op_asm_kernel_mod_test
