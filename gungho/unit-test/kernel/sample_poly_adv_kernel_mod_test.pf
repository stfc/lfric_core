!-----------------------------------------------------------------------------
! Copyright (c) 2019,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

module sample_poly_adv_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use pFUnit_Mod
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: sample_poly_adv_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_poly_adv_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use gungho_feign_config_mod,   only : feign_transport_config,   &
                                          feign_timestepping_config

    use timestepping_config_mod,   only : timestepping_method_semi_implicit, &
                                          timestepping_runge_kutta_method_ssp3

    use transport_config_mod,      only : transport_scheme_method_of_lines, &
                                          transport_operators_fv
    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_transport_config(scheme = transport_scheme_method_of_lines, &
                                operators = transport_operators_fv,        &
                                fv_flux_order = 0,                         &
                                fv_advective_order = 1,                    &
                                consistent_metric = .true.,                &
                                enforce_monotonicity = .false. )

    ! Set dt to dummy value - it should not be used by kernel for enforce_monotonicity = .false.
    call feign_timestepping_config(method=timestepping_method_semi_implicit,          &
                                   dt=-1.0_r_def, alpha=0.5_r_def, tau_u=0.5_r_def,   &
                                   tau_t=0.5_r_def, tau_r=0.5_r_def,                  &
                                   outer_iterations=1_i_def, inner_iterations=1_i_def,&
                                   runge_kutta_method=timestepping_runge_kutta_method_ssp3,    &
                                   spinup_period=0.0_r_def, spinup_alpha=.false.,     &
                                   spinup_winds=.false.    )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this

    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use sample_poly_adv_kernel_mod,     only : sample_poly_adv_code, &
                                               sample_poly_adv_init

    use get_unit_test_wthetanodal_basis_mod, only: get_w2_wthetanodal_basis,    &
                                                   get_w0_wthetanodal_basis,    &
                                                   get_w0_wthetanodal_diff_basis

    use get_unit_test_m3x3_q3x3x3_sizes_mod, only: get_w0_m3x3_q3x3x3_size,    &
                                                   get_w2_m3x3_q3x3x3_size,    &
                                                   get_wtheta_m3x3_q3x3x3_size

    use get_unit_test_m3x3_dofmap_mod, only: get_m3x3_stencil_dofmap_cross, &
                                             get_w0_m3x3_dofmap,            &
                                             get_w2_m3x3_dofmap,            &
                                             get_wtheta_m3x3_dofmap

    implicit none

    class(sample_poly_adv_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def
    real(r_def), parameter :: dy = 2000.0_r_def
    real(r_def), parameter :: dz = 2000.0_r_def

    real(r_def), parameter :: tol = 1.0e-12_r_def

    real(r_def) :: answer

    real (r_def), allocatable :: chi_data(:,:)
    real (r_def), allocatable :: adv_data(:)
    real (r_def), allocatable :: theta_data(:)
    real (r_def), allocatable :: u_data(:)
    real (r_def), allocatable :: mt_lumped_inv_data(:)

    real (r_def), allocatable :: basis_w2(:,:,:)
    real (r_def), allocatable :: basis_w0(:,:,:)
    real (r_def), allocatable :: diff_basis_w0(:,:,:)

    integer(i_def) :: cell, i, j, k

    ! Variables for testing sample_poly_adv

    integer(i_def)     :: ndf_w0, undf_w0
    integer(i_def)     :: ndf_wtheta, undf_wtheta
    integer(i_def)     :: ndf_w2, undf_w2
    integer(i_def)     :: nlayers

    integer, allocatable    :: stencil_map_wtheta(:,:,:)
    integer, allocatable    :: stencil_map_w0(:,:,:)

    integer, allocatable    :: map_w2(:,:)
    integer, allocatable    :: map_w0(:,:)
    integer, allocatable    :: map_wtheta(:,:)

    integer(i_def) :: stencil_size

    ! Dummy variable for passing into multi-getter routines where item is not needed
    integer(i_def) :: unused

    ! Get canned data for a 3-layer mesh
    nlayers=3

    ! Get sizes
    call get_w0_m3x3_q3x3x3_size( ndf_w0, undf_w0, unused, &
                                  unused, unused,          &
                                  unused, unused,          &
                                  nlayers )

    call get_w2_m3x3_q3x3x3_size( ndf_w2, undf_w2, unused, &
                                  unused, unused,          &
                                  unused, unused,          &
                                  nlayers )

    call get_wtheta_m3x3_q3x3x3_size( ndf_wtheta, undf_wtheta, unused, &
                                      unused, unused,                  &
                                      unused, unused,                  &
                                      nlayers )

    ! Get dofmaps
    call get_w0_m3x3_dofmap(map_w0)
    call get_w2_m3x3_dofmap(map_w2)
    call get_wtheta_m3x3_dofmap(map_wtheta)

    ! Get cross stencil maps
    call get_m3x3_stencil_dofmap_cross(stencil_map_w0, map_w0)
    call get_m3x3_stencil_dofmap_cross(stencil_map_wtheta, map_wtheta)
    stencil_size=5

    ! Create chi field
    allocate(chi_data(undf_w0,3))

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        do k = 0,3
          chi_data(map_w0(1,cell) + k, 1) = real(i-1)*dx
          chi_data(map_w0(1,cell) + k, 2) = real(j-1)*dy
          chi_data(map_w0(1,cell) + k, 3) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Get basis functions
    call get_w2_wthetanodal_basis(basis_w2)
    call get_w0_wthetanodal_basis(basis_w0)
    call get_w0_wthetanodal_diff_basis(diff_basis_w0)

    ! Create fields
    allocate(theta_data(undf_wtheta))
    allocate(mt_lumped_inv_data(undf_wtheta))
    allocate(adv_data(undf_wtheta))
    allocate(u_data(undf_w2))

    ! Create the data
    cell = 5
    u_data(:) = 0.0_r_def
    theta_data(:) = 2.0_r_def
    u_data(map_w2(1,cell)+0) = 2.0_r_def
    u_data(map_w2(3,cell)+0) = 3.0_r_def
    theta_data(stencil_map_wtheta(1,1,cell)) = 3.0_r_def
    theta_data(stencil_map_wtheta(1,2,cell)) = 1.0_r_def
    theta_data(stencil_map_wtheta(1,4,cell)) = 5.0_r_def

    ! This isn't used unless enforce_monotonicity=.true., so set to
    ! something sensible
    mt_lumped_inv_data(:) = 1.0_r_def

    ! Initialise kernel
    call sample_poly_adv_init(1, nlayers)
    adv_data(:) = 0.0_r_def

    call sample_poly_adv_code(nlayers,                      &
                              adv_data,                     &
                              theta_data,                   &
                              stencil_size,                 &
                              stencil_map_wtheta(:,:,cell), &
                              u_data,                       &
                              mt_lumped_inv_data,           &
                              chi_data(:,1),                &
                              stencil_size,                 &
                              stencil_map_w0(:,:,cell),     &
                              chi_data(:,2),                &
                              chi_data(:,3),                &
                              ndf_wtheta,                   &
                              undf_wtheta,                  &
                              map_wtheta(:,cell),           &
                              ndf_w2,                       &
                              undf_w2,                      &
                              map_w2(:,cell),               &
                              basis_w2,                     &
                              ndf_w0,                       &
                              undf_w0,                      &
                              map_w0(:,cell),               &
                              basis_w0,                     &
                              diff_basis_w0                 &
                              )
     ! u.grad(theta) = 5, but need extra half factor to account for
     ! weighting of mass matrix term: int(1-z,z=0..1) = 1/2 that is
     ! not included here but would be for a full run
     answer = 2.5_r_def
     @assertEqual(answer, adv_data(stencil_map_wtheta(1,1,cell) ), tol)
     answer = 0.0_r_def
     @assertEqual(answer, adv_data(stencil_map_wtheta(1,1,cell)+1), tol)

     deallocate(chi_data)
     deallocate(adv_data)
     deallocate(theta_data)
     deallocate(u_data)
     deallocate(mt_lumped_inv_data)
     deallocate(basis_w2)
     deallocate(basis_w0)
     deallocate(diff_basis_w0)
     deallocate(stencil_map_wtheta)
     deallocate(stencil_map_w0)
     deallocate(map_w2)
     deallocate(map_w0)
     deallocate(map_wtheta)

  end subroutine test_all

end module sample_poly_adv_kernel_mod_test
