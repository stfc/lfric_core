!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the remapping of scalar fields on the cubed-sphere panel extension
module remap_on_extended_mesh_kernel_mod_test

  use constants_mod, only: l_def, i_def, r_def, pi
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: remap_on_extended_mesh_test_type
    private
  contains
    procedure test_all
  end type remap_on_extended_mesh_test_type

contains

  @Test
  subroutine test_all( this )

    use remap_on_extended_mesh_kernel_mod, only: remap_on_extended_mesh_code
    use coord_transform_mod,               only: alphabetar2llr

    implicit none

    class(remap_on_extended_mesh_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-5_r_def
    real(r_def)            :: long, lat

    integer(i_def), parameter :: nlayers = 1
    integer(i_def), parameter :: ncells = 6
    integer(i_def), parameter :: ndf_ws = 1
    integer(i_def), parameter :: undf_ws = ncells*ndf_ws*nlayers
    integer(i_def), parameter :: ndf_wx = 1
    integer(i_def), parameter :: undf_wx = ncells*ndf_wx*nlayers
    integer(i_def), parameter :: ndf_pid = 1
    integer(i_def), parameter :: undf_pid = 1 + ncells*ndf_pid*nlayers

    integer(i_def), dimension(ndf_ws)  :: map_ws
    integer(i_def), dimension(ndf_wx)  :: map_wx
    integer(i_def), dimension(ndf_pid) :: map_pid

    integer(i_def), dimension(4) :: stencil_size
    integer(i_def), parameter    :: max_length = 3
    integer(i_def), dimension(ndf_ws,  max_length, 4) :: stencil
    integer(i_def), dimension(ndf_wx,  max_length, 4) :: wx_stencil
    integer(i_def), dimension(ndf_pid, max_length, 4) :: pid_stencil

    logical(l_def)               :: linear_remap

    real(r_def), dimension(1, ndf_wx, ndf_ws) :: basis_wx


    real(r_def), dimension(undf_ws)  :: remap_field, field
    real(r_def), dimension(undf_wx)  :: alpha_ext, beta_ext, height_ext
    real(r_def), dimension(undf_wx)  :: alpha, beta, height
    real(r_def), dimension(undf_pid) :: panel_id

    real(r_def), parameter :: alpha0 = -pi/4.0_r_def
    real(r_def), parameter :: beta0  = 0.3_r_def
    real(r_def), parameter :: delta_alpha = 0.1_r_def
    real(r_def), parameter :: delta_beta  = 0.1_r_def

    integer(i_def) :: df

    linear_remap = .false.

    ! Remap from panel 2 to panel 1
    stencil_size(:) = 3
    stencil_size(1) = 2
    stencil_size(3) = 1

    ! Stencil shape is:
    !     | 6 |
    !     | 4 |
    ! | 2 | 1 |
    !     | 3 |
    !     | 5 |

    map_ws(1) = 1
    stencil(1,1,:) = map_ws(1)
    stencil(1,2,1) = 2
    stencil(1,2,2) = 3
    stencil(1,2,3) = -1
    stencil(1,2,4) = 4
    stencil(1,3,2) = 5
    stencil(1,3,4) = 6

    ! First entry of panel_id is assumed to be the owned panel
    ! so the map points to the second entry
    map_pid(1) = 2

    pid_stencil(1,1,:) = map_pid(1)
    pid_stencil(1,2,1) = 3
    pid_stencil(1,2,2) = 4
    pid_stencil(1,2,3) = -1
    pid_stencil(1,2,4) = 5
    pid_stencil(1,3,2) = 6
    pid_stencil(1,3,4) = 7


    panel_id(1) = 1.0_r_def
    panel_id(2) = 2.0_r_def
    panel_id(3) = 1.0_r_def
    panel_id(4) = 2.0_r_def
    panel_id(5) = 2.0_r_def
    panel_id(6) = 2.0_r_def
    panel_id(7) = 2.0_r_def

    ! Coordinate arrays only include the centre point
    map_wx(1) = 1
    wx_stencil(1,1,:) = map_wx(1)
    wx_stencil(1,2,1) = 2
    wx_stencil(1,2,2) = 3
    wx_stencil(1,2,3) = -1
    wx_stencil(1,2,4) = 4
    wx_stencil(1,3,2) = 5
    wx_stencil(1,3,4) = 6


    basis_wx(1,1,1) = 1.0_r_def

    ! height fields unchanged by the remapping
    height(:) = 1.0_r_def
    height_ext(:) = 1.0_r_def

    ! Original coordinates
    ! Cell 2 on edge of panel 1
    alpha(2) = -alpha0 + 0.5_r_def*delta_alpha
    beta(2)  = beta0
    ! All other cells are on the edge of panel 2
    alpha(1) = alpha0 - 0.5_r_def*delta_alpha
    beta(1)  = beta0

    alpha(3) = alpha0 - 0.5_r_def*delta_alpha
    beta(3)  = beta0 - delta_beta

    alpha(4) = alpha0 - 0.5_r_def*delta_alpha
    beta(4)  = beta0 + delta_beta

    alpha(5) = alpha0 - 0.5_r_def*delta_alpha
    beta(5)  = beta0 - 2.0_r_def*delta_beta

    alpha(6) = alpha0 - 0.5_r_def*delta_alpha
    beta(6)  = beta0 + 2.0_r_def*delta_beta



    ! Extended coordinates, all points count as being on panel 1
    alpha_ext(2) = alpha(2)
    beta_ext(2)  = beta(2)

    alpha_ext(1) = - alpha0 - 0.5_r_def*delta_alpha
    beta_ext(1)  = beta0

    alpha_ext(3) = - alpha0 - 0.5_r_def*delta_alpha
    beta_ext(3)  = beta0 - delta_beta

    alpha_ext(4) = - alpha0 - 0.5_r_def*delta_alpha
    beta_ext(4)  = beta0 + delta_beta

    alpha_ext(5) = - alpha0 - 0.5_r_def*delta_alpha
    beta_ext(5)  = beta0 - 2.0_r_def*delta_beta

    alpha_ext(6) = - alpha0 - 0.5_r_def*delta_alpha
    beta_ext(6)  = beta0 + 2.0_r_def*delta_beta

    ! Set field to be latitude then remap field should have the
    ! same value in it
    do df = 1, undf_ws
      call alphabetar2llr(alpha(df), beta(df), height(df), int(panel_id(df),i_def), long, lat )
      field(df) = lat
    end do
    call alphabetar2llr(alpha_ext(1), beta_ext(1), height_ext(1), int(panel_id(1),i_def), long, lat)

    remap_field(:) = 0.0_r_def

    call remap_on_extended_mesh_code(nlayers,                               &
                                     remap_field,                           &
                                     field,                                 &
                                     stencil_size, stencil, max_length,     &
                                     alpha_ext, beta_ext, height_ext,       &
                                     alpha, beta, height,                   &
                                     stencil_size, wx_stencil, max_length,  &
                                     panel_id,                              &
                                     stencil_size, pid_stencil, max_length, &
                                     linear_remap,                          &
                                     ndf_ws, undf_ws, map_ws,               &
                                     ndf_wx, undf_wx, map_wx, basis_wx,     &
                                     ndf_pid, undf_pid, map_pid             &
                                    )

    @assertEqual(lat, remap_field(1), tol)

  end subroutine test_all

end module remap_on_extended_mesh_kernel_mod_test
