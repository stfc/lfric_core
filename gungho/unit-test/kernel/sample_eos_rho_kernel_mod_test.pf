!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the computation of density from the equation of state
!>
module sample_eos_rho_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, &
                                            field_proxy_type
  use function_space_mod,            only : function_space_type
  use fs_continuity_mod,             only : W3, WTHETA
  use pFUnit_Mod
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type, &
                                               quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,      only: quadrature_rule_gaussian_type
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: sample_eos_rho_test_type, test_all

  @TestCase
  type, extends(MPITestCase) :: sample_eos_rho_test_type
    private
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type)      :: qr_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    type(function_space_type), pointer :: wt_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: exner, theta, rho
    real(r_def), allocatable           :: basis_w3(:,:,:,:)
    real(r_def), allocatable           :: basis_wt(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type sample_eos_rho_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: bvf_square = 0.0001_r_def
  real(r_def), parameter :: theta_surf = 300.0_r_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(sample_eos_rho_test_type), intent(inout) :: this

    type(field_proxy_type)    :: exner_proxy, theta_proxy, rho_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf_wt, ndf_w3
    integer(i_def) :: undf_wt, undf_w3
    integer(i_def) :: dim_wt, dim_w3
    integer(i_def) :: mesh_id

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_base_mesh_config                             &
             ( filename='foo', prime_mesh_name='unit_test', &
               geometry=base_mesh_geometry_planar,          &
               partitioner=base_mesh_partitioner_planar,    &
               fplane=.false., f_lat_deg=0.0_r_def )

    call feign_initial_temperature_config( bvf_square=bvf_square, &
                                           theta_surf=theta_surf )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wt_fs => function_space_collection%get_fs(mesh_id, element_order, WTHETA)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%exner   = field_type( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()

    this%rho   = field_type( vector_space = this%w3_fs )
    rho_proxy = this%rho%get_proxy()

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    undf_w3 = exner_proxy%vspace%get_undf()
    dim_w3  = exner_proxy%vspace%get_dim_space( )
    allocate(this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v))

    this%theta = field_type( vector_space = this%wt_fs )
    theta_proxy = this%theta%get_proxy()

    ndf_wt  = theta_proxy%vspace%get_ndf( )
    undf_wt = theta_proxy%vspace%get_undf()
    dim_wt  = theta_proxy%vspace%get_dim_space( )
    allocate(this%basis_wt(dim_wt,ndf_wt,nqp_h,nqp_v))

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(sample_eos_rho_test_type), intent(inout) :: this

    deallocate( this%basis_w3, this%basis_wt )

    call function_space_collection%clear()
    call mesh_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test( npes=[1] )
  subroutine test_all( this )

    use sample_eos_rho_kernel_mod, only : sample_eos_rho_code

    implicit none

    class(sample_eos_rho_test_type), intent(inout) :: this

    type(field_proxy_type)    :: exner_proxy, theta_proxy, rho_proxy

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-7_r_def
    real(r_def)            :: answer

    integer :: i, j, k, cell, err

    integer                 :: nlayers, nqp_h, nqp_v
    integer                 :: ndf_wt, ndf_w3
    integer                 :: undf_wt, undf_w3
    integer                 :: element_order
    integer, pointer        :: map_w3(:), map_wt(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    ! Test kernel
    exner_proxy  = this%exner%get_proxy()
    theta_proxy = this%theta%get_proxy()
    rho_proxy = this%theta%get_proxy()

    nlayers = exner_proxy%vspace%get_nlayers()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z

    ndf_w3  = exner_proxy%vspace%get_ndf( )
    undf_w3 = exner_proxy%vspace%get_undf()

    call rho_proxy%vspace%compute_basis_function( &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp )

    ndf_wt  = theta_proxy%vspace%get_ndf( )
    undf_wt = theta_proxy%vspace%get_undf()

    call theta_proxy%vspace%compute_basis_function( &
         this%basis_wt, ndf_wt, nqp_h, nqp_v, xp, zp )

    cell = 1

    map_w3  => exner_proxy%vspace%get_cell_dofmap( cell )
    map_wt  => theta_proxy%vspace%get_cell_dofmap( cell )

    ! Create the data
    exner_proxy%data(:) = 1.0_r_def
    rho_proxy%data(:)   = 0.0_r_def
    theta_proxy%data(:) = 300.0_r_def

    call sample_eos_rho_code( nlayers,                 &
                              rho_proxy%data,          &
                              exner_proxy%data,        &
                              theta_proxy%data,        &
                              ndf_w3, undf_w3,         &
                              map_w3, this%basis_w3,   &
                              ndf_wt, undf_wt, map_wt, &
                              this%basis_wt            &
                                 )

    k = 1
    answer = 0.8405948_r_def
    @assertEqual(answer, rho_proxy%data(map_w3(1)+k), tol)

  end subroutine test_all

end module sample_eos_rho_kernel_mod_test
