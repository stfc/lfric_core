!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the ru_bd computation
!>
module ru_bd_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_mod,                      only : mesh_type
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use reference_element_mod,         only : reference_element_type
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : Wtheta, W0, W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_face_mod,           only : quadrature_face_type, &
                                            quadrature_face_proxy_type
  use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
  use stencil_dofmap_mod,            only : stencil_dofmap_type, STENCIL_CROSS
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public   :: ru_bd_test_type
    private
    type(quadrature_face_type)         :: qr
    type(quadrature_face_proxy_type)   :: qr_proxy
    type(function_space_type), pointer :: wtheta_fs => null()
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_u_bd, exner, theta
    real(r_def), allocatable           :: basis_w2_face(:,:,:,:), &
                                          basis_wtheta_face(:,:,:,:), &
                                          basis_w3_face(:,:,:,:)

    type(stencil_dofmap_type), pointer :: cross_stencil_w2 => null()
    type(stencil_dofmap_type), pointer :: cross_stencil_w3 => null()

    integer, pointer        :: cross_stencil_w3_map(:,:,:) => null()
    integer                 :: cross_stencil_w3_size


  contains
    procedure setup
    procedure tearDown
    procedure test_all
  end type ru_bd_test_type

  integer(i_def) :: element_order = 0

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

  real(r_def), parameter :: bvf_square = 0.0001_r_def
  real(r_def), parameter :: theta_surf = 300.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use base_mesh_config_mod,      only : base_mesh_geometry_planar, &
                                          base_mesh_partitioner_planar
    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_base_mesh_config,           &
                                          feign_initial_temperature_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(ru_bd_test_type), intent(inout) :: this

    type(field_proxy_type) :: r_u_bd_proxy, exner_proxy, theta_proxy
    type( quadrature_rule_gaussian_type ) :: quadrature_rule
    class(mesh_type),              pointer :: mesh              => null()
    class(reference_element_type), pointer :: reference_element => null()

    integer(i_def) :: nqp
    integer(i_def) :: ndf_wtheta, undf_wtheta, dim_wtheta
    integer(i_def) :: ndf_w2, undf_w2, dim_w2
    integer(i_def) :: ndf_w3, undf_w3, dim_w3
    integer(i_def) :: mesh_id
    integer(i_def) :: nfaces_h

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())


    call feign_base_mesh_config( filename='foo', prime_mesh_name='unit_test', &
                                 geometry=base_mesh_geometry_planar,          &
                                 partitioner=base_mesh_partitioner_planar,    &
                                 fplane=.false., f_lat_deg=0.0_r_def )

    call feign_initial_temperature_config( bvf_square=bvf_square, &
                                           theta_surf=theta_surf )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%wtheta_fs => function_space_collection%get_fs(mesh_id, element_order, Wtheta)
    this%w0_fs     => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w2_fs     => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs     => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%r_u_bd  = field_type( vector_space = this%w2_fs )
    r_u_bd_proxy = this%r_u_bd%get_proxy()
    ndf_w2       = r_u_bd_proxy%vspace%get_ndf( )
    dim_w2       = r_u_bd_proxy%vspace%get_dim_space( )
    mesh              => r_u_bd_proxy%vspace%get_mesh()
    reference_element => mesh%get_reference_element()
    nfaces_h = reference_element%get_number_horizontal_faces()

    this%qr = quadrature_face_type(3, .true., .false., reference_element, &
                                   quadrature_rule)
    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp=this%qr_proxy%np_xyz

    allocate( this%basis_w2_face(dim_w2,ndf_w2,nqp,nfaces_h) )

    this%theta        = field_type( vector_space = this%wtheta_fs )
    theta_proxy       = this%theta%get_proxy()
    ndf_wtheta        = theta_proxy%vspace%get_ndf( )
    dim_wtheta        = theta_proxy%vspace%get_dim_space( )
    undf_wtheta       = theta_proxy%vspace%get_undf()
    allocate( this%basis_wtheta_face(dim_wtheta,ndf_wtheta,nqp,nfaces_h) )

    this%exner  = field_type( vector_space = this%w3_fs )
    exner_proxy = this%exner%get_proxy()
    ndf_w3    = exner_proxy%vspace%get_ndf( )
    dim_w3    = exner_proxy%vspace%get_dim_space( )
    allocate( this%basis_w3_face(dim_w3,ndf_w3,nqp,nfaces_h) )

    this%cross_stencil_w3 => exner_proxy%vspace%get_stencil_dofmap(STENCIL_CROSS, 1)
    this%cross_stencil_w3_map => this%cross_stencil_w3%get_whole_dofmap()
    this%cross_stencil_w3_size = this%cross_stencil_w3%get_size()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(ru_bd_test_type), intent(inout) :: this

    deallocate( this%basis_w2_face,      &
                this%basis_wtheta_face,  &
                this%basis_w3_face )

    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use ru_bd_kernel_mod,      only : ru_bd_code
    use mesh_mod,              only : mesh_type
    use function_space_mod,    only : BASIS
    implicit none

    class(ru_bd_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type)   :: chi_p(3)

    real(r_def), parameter :: tol_v    = 1.0e-6_r_def
    real(r_def), parameter :: tol_h    = 1.0e+1_r_def
    real(r_def), parameter :: answer_v = 0.0_r_def
    real(r_def), parameter :: answer_h = -450000.0_r_def

    integer :: cell

    ! Required for calling kernel 
    type(field_type)  :: theta

    integer                 :: nlayers, nqp
    integer                 :: ndf_wtheta, undf_wtheta
    integer                 :: ndf_w2, undf_w2
    integer                 :: ndf_w3, undf_w3

    integer                 :: ii, jj, k, i, j

    type(field_proxy_type)  :: theta_proxy,  &
                               r_u_bd_proxy, &
                               exner_proxy

    real(kind=r_def), pointer :: wqp(:,:)       => null()

    integer, pointer :: adjacent_face(:,:) => null()
    integer, pointer :: map_w2(:)          => null()
    integer, pointer :: map_w0(:)          => null()
    integer, pointer :: map_wt(:)          => null()

    class(reference_element_type), pointer :: reference_element => null()
    class(mesh_type),              pointer :: mesh              => null()

    integer :: a_face(4)
    integer(i_def) :: nfaces_h

    real(r_def), allocatable :: out_face_normal(:,:)

    mesh => this%r_u_bd%get_mesh()
    reference_element => mesh%get_reference_element()
    nfaces_h = reference_element%get_number_horizontal_faces()

    ! Make a coordinate field with a function space
    do i = 1,3
      chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_w0 => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_w0(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_w0(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_w0(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test kernel

    r_u_bd_proxy = this%r_u_bd%get_proxy()
    theta_proxy  = this%theta%get_proxy()
    exner_proxy  = this%exner%get_proxy()

    nlayers = r_u_bd_proxy%vspace%get_nlayers()

    nqp =  this%qr_proxy%np_xyz
    wqp => this%qr_proxy%weights_xyz

    ndf_w2      = r_u_bd_proxy%vspace%get_ndf( )
    undf_w2     = r_u_bd_proxy%vspace%get_undf()

    ndf_wtheta      = theta_proxy%vspace%get_ndf( )
    undf_wtheta     = theta_proxy%vspace%get_undf()

    ndf_w3      = exner_proxy%vspace%get_ndf( )
    undf_w3     = exner_proxy%vspace%get_undf()

    call this%qr%compute_function(BASIS, r_u_bd_proxy%vspace, 3, ndf_w2,     this%basis_w2_face)
    call this%qr%compute_function(BASIS, exner_proxy%vspace,  1, ndf_w3,     this%basis_w3_face)
    call this%qr%compute_function(BASIS, theta_proxy%vspace,  1, ndf_wtheta, this%basis_wtheta_face)

    ! Create the data
    r_u_bd_proxy%data(:) = 0.0_r_def
    theta_proxy%data(:) = 300.0_r_def
    exner_proxy%data(:) = 1.5_r_def

    cell = 1

    map_w2 => r_u_bd_proxy%vspace%get_cell_dofmap( cell )
    map_wt => theta_proxy%vspace%get_cell_dofmap( cell )

    adjacent_face => mesh%get_adjacent_face()
    call reference_element%get_normals_to_out_faces( out_face_normal )

    call ru_bd_code( nlayers,                                 &
                     ndf_w2, undf_w2,                         &
                     map_w2,                                  &
                     ndf_w3, undf_w3,                         &
                     this%cross_stencil_w3_map(:,:,cell),     &
                     this%cross_stencil_w3_size,              &
                     ndf_wtheta, undf_wtheta,                 &
                     map_wt,                                  &
                     r_u_bd_proxy%data,                       &
                     exner_proxy%data,                        &
                     theta_proxy%data,                        &
                     nqp, wqp,                                &
                     this%basis_w2_face, this%basis_w3_face,  &
                     this%basis_wtheta_face,                  &
                     adjacent_face(:,cell),                   &
                     out_face_normal )

    k=1
    ! Simple test, check that the kernel does not produce contributions on horizontal faces
    @assertEqual( answer_v, r_u_bd_proxy%data(map_w2(5)+k) , tol_v )
    @assertEqual( answer_v, r_u_bd_proxy%data(map_w2(6)+k) , tol_v )

    ! Check contribution on a vertical face
    @assertEqual( answer_h, r_u_bd_proxy%data(map_w2(2)+k) , tol_h )

  end subroutine test_all

end module ru_bd_kernel_mod_test
