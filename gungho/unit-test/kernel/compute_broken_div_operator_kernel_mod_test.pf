!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Test the broken divergence operator computation
!>
module compute_broken_div_operator_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2broken, W3
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_xyoz_mod,           only : quadrature_xyoz_type, &
                                              quadrature_xyoz_proxy_type
    use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
    use yaxt,                          only : xt_initialize, xt_finalize
    use mpi_mod,                       only : store_comm, clear_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: compute_broken_div_operator_test_type
    private
    type( quadrature_xyoz_type )       :: qr
    type(quadrature_xyoz_proxy_type)   :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2b_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3)
    type(operator_type)                :: broken_div
    real(kind=r_def), allocatable      :: diff_basis_w2b(:,:,:,:), &
                                          basis_w3(:,:,:,:),       &
                                          diff_basis_chi(:,:,:,:)
    integer(kind=i_def)                :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_broken_div_operator_test_type

  integer(kind=i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(compute_broken_div_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: broken_div_p
    type(field_proxy_type)    :: chi_p(3)
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(kind=i_def) :: nlayers, diff_dim, dim_w3, diff_dim_w2b
    integer(kind=i_def) :: ndf_chi, ndf_w2b, ndf_w3
    integer(kind=i_def) :: nqp_h, nqp_v

    integer(kind=i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_finite_element_config(                          &
             cellshape=finite_element_cellshape_quadrilateral, &
             coordinate_order=0,                               &
             element_order=1,                                  &
             rehabilitate=.true.,                              &
             supg=.false.,                                     &
             vorticity_in_w1=.false.)

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                    element_order, &
                                                    W0 )
    this%w2b_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                     element_order, &
                                                     W2broken )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                    element_order, &
                                                    W3 )

    this%broken_div = operator_type(this%w3_fs, this%w2b_fs)

    do i = 1, 3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate(this%diff_basis_chi(diff_dim,ndf_chi,nqp_h,nqp_v) )

    broken_div_p = this%broken_div%get_proxy()   
 
    ndf_w2b  = broken_div_p%fs_from%get_ndf()
    diff_dim_w2b = broken_div_p%fs_from%get_dim_space_diff( )
    allocate(this%diff_basis_w2b(diff_dim_w2b, ndf_w2b, nqp_h, nqp_v) )

    ndf_w3 = broken_div_p%fs_to%get_ndf()
    dim_w3 = broken_div_p%fs_to%get_dim_space( )
    allocate(this%basis_w3(dim_w3, ndf_w3, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(compute_broken_div_operator_test_type), intent(inout) :: this

    deallocate( this%basis_w3, this%diff_basis_w2b, this%diff_basis_chi)

    call mesh_collection%clear()
    call function_space_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_broken_div_operator_kernel_mod, only : compute_broken_div_operator_code
    use mesh_mod,                               only : mesh_type

    implicit none

    class(compute_broken_div_operator_test_type), intent(inout) :: this

    type(operator_proxy_type)   :: broken_div_p
    type(field_proxy_type )     :: chi_p(3) 

    type(mesh_type), pointer    :: mesh => null()

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def

    integer(kind=i_def) :: i, j, k, cell
    integer(kind=i_def) :: err
    real(kind=r_def)    :: dz, dz_test(3)
     
    integer(kind=i_def) :: nlayers
    integer(kind=i_def) :: ndf_chi, undf_chi, ndf_w2b, ndf_w3
    integer(kind=i_def) :: nqp_h, nqp_v

    integer(kind=i_def), pointer      :: map_chi(:)  => null()

    real(kind=r_def), pointer         :: xp(:,:) => null()
    real(kind=r_def), pointer         :: zp(:)   => null()
    real(kind=r_def), pointer         :: wh(:) => null()
    real(kind=r_def), pointer         :: wv(:) => null()
    real(kind=r_def), dimension(1, 6) :: answer

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    ! Make a coordinate field with a function space
    do i = 1, 3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1, 3
      do i = 1, 3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0, 3
          chi_p(1)%data(map_chi(1) + k) = real(i-1, r_def)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1, r_def)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k, r_def)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test the operator kernel
    broken_div_p = this%broken_div%get_proxy()

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()
    call chi_p(1)%vspace%compute_diff_basis_function(                  &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp )

    nlayers = broken_div_p%fs_from%get_nlayers()
    ndf_w2b  = broken_div_p%fs_from%get_ndf()
    call broken_div_p%fs_from%compute_diff_basis_function(             &
         this%diff_basis_w2b, ndf_w2b, nqp_h, nqp_v, xp, zp ) 

    ndf_w3 = broken_div_p%fs_to%get_ndf()
    call broken_div_p%fs_to%compute_basis_function(                    &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)
    broken_div_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_broken_div_operator_code( cell,                              &
                                           nlayers,                           &
                                           broken_div_p%ncell_3d,             &
                                           broken_div_p%local_stencil,        &
                                           chi_p(1)%data,                     &
                                           chi_p(2)%data,                     &
                                           chi_p(3)%data,                     &
                                           ndf_w3,                            &
                                           this%basis_w3,                     &
                                           ndf_w2b,                           &
                                           this%diff_basis_w2b,               &
                                           ndf_chi,                           &
                                           undf_chi,                          &
                                           map_chi,                           &
                                           this%diff_basis_chi,               &
                                           nqp_h,                             &
                                           nqp_v,                             &
                                           wh,                                &
                                           wv                                 &
                                         )

    call broken_div_operator(answer)
    ! Locally, the broken divergence operator is identical to the conforming div
    @assertEqual(answer, broken_div_p%local_stencil(:, :, 1), tol)

  end subroutine test_all

  subroutine broken_div_operator( broken_div ) 
  
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(1, 6), intent(inout) :: broken_div
    real(kind=r_def), dimension(1, 1)                :: m3
    real(kind=r_def), dimension(1, 6)                :: d32

    m3 = reshape( [                              &
                    1.0000000000000000E+00_r_def &
                  ], shape(m3) )

    d32 = real(reshape( [                       &
                         -1, 1, 1, -1, -1, 1    &
                        ], shape(d32) ), r_def) 

    broken_div = matmul(m3, d32)

    return

  end subroutine broken_div_operator

end module compute_broken_div_operator_kernel_mod_test
