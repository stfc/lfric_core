!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the computation of the cubed-sphere panel extension
module extend_chi_field_kernel_mod_test

  use constants_mod, only: i_def, r_def, pi
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: extend_chi_field_test_type
    private
  contains
    procedure test_all
  end type extend_chi_field_test_type

contains

  @Test
  subroutine test_all( this )

    use extend_chi_field_kernel_mod,   only: extend_chi_field_code
    use, intrinsic :: iso_fortran_env, only : real64

    implicit none

    class(extend_chi_field_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def
    real(r_def)            :: use_tol

    integer(i_def), parameter :: nlayers = 1
    integer(i_def), parameter :: ndf_wx = 4
    integer(i_def), parameter :: undf_wx = ndf_wx*nlayers
    integer(i_def), parameter :: ndf_pid = 1
    integer(i_def), parameter :: undf_pid = 2*ndf_pid*nlayers

    integer(i_def), dimension(ndf_wx)  :: map_wx
    integer(i_def), dimension(ndf_pid) :: map_pid

    real(r_def), dimension(undf_wx)  :: alpha_ext, beta_ext, height_ext
    real(r_def), dimension(undf_pid) :: panel_id

    integer(r_def) :: df

    real(r_def), parameter :: delta_alpha = 0.1_r_def
    real(r_def), parameter :: delta_beta  = 0.1_r_def
    real(r_def), parameter :: alpha0 = pi/4.0_r_def
    real(r_def), parameter :: beta0 = 0.5_r_def
    real(r_def), parameter :: h0 = 1.0_r_def

    integer(i_def), dimension(2) :: east_dofs, west_dofs, south_dofs, north_dofs

    do df = 1, ndf_wx
      map_wx(df) = df
    end do
    map_pid(1) = 2

    ! Test extension from panel2 to panel1
    panel_id(1) = 1.0_r_def
    panel_id(2) = 2.0_r_def

    ! On input the coordinates (alpha, beta, h) are points on the WEST edge of
    ! panel 2 (corresponding to the EAST edge of panel 1)

    ! Flags for Wx dofs on the east edge of the cell: alpha = -pi/4 +  delta_alpha
    east_dofs = (/ 2, 4 /)
    ! Flags for Wx dofs on the west  edge of the cell: alpha = -pi/4
    west_dofs = (/ 1, 3 /)
    ! Flags for Wx dofs on the north edge of the cell: beta0 + delta_beta
    north_dofs = (/ 3, 4 /)
    ! Flags for Wx dofs on the south edge of the cell: beta0
    south_dofs = (/ 1, 2 /)

    do df = 1,2
      alpha_ext(west_dofs(df)) = -alpha0
      alpha_ext(east_dofs(df)) = -alpha0 + delta_alpha
      beta_ext(south_dofs(df)) = beta0
      beta_ext(north_dofs(df)) = beta0 + delta_beta
   end do
   height_ext(:) = h0

   call  extend_chi_field_code(nlayers,                   &
                               alpha_ext,                 &
                               beta_ext,                  &
                               height_ext,                &
                               panel_id,                  &
                               ndf_wx, undf_wx, map_wx,   &
                               ndf_pid, undf_pid, map_pid &
                               )
    if ( r_def == real64 ) then
       use_tol = tol
    else
       use_tol = 10.0_r_def*spacing( height_ext(1) )
    end if
    ! Height field should be unchanged
    do df = 1, ndf_wx
      @assertEqual(1.0_r_def, height_ext(df), use_tol)
    end do
    ! Beta field should be unchanged
    do df = 1, 2
      @assertEqual(beta0, beta_ext(south_dofs(df)), use_tol)
      @assertEqual(beta0 + delta_beta, beta_ext(north_dofs(df)), use_tol)
    end do
    ! Alpha field should be (+pi/4, + pi/4 + delta_alpha)
    do df = 1, 2
      @assertEqual(alpha0, alpha_ext(west_dofs(df)), use_tol)
      @assertEqual(alpha0 + delta_alpha, alpha_ext(east_dofs(df)), use_tol)
    end do

  end subroutine test_all

end module extend_chi_field_kernel_mod_test
