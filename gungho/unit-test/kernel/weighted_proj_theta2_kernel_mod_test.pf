!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the theta weighted divergence operator computation
!>
module weighted_proj_theta2_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2, W3
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_xyoz_mod,           only : quadrature_xyoz_type, &
                                              quadrature_xyoz_proxy_type
    use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: weighted_proj_theta2_test_type
    private
    integer(i_def)                     :: mesh_id
    type( quadrature_xyoz_type )          :: qr
    type(quadrature_xyoz_proxy_type) :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: theta
    type(operator_type)                :: proj
    real(r_def), allocatable           :: diff_basis_w0(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type weighted_proj_theta2_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(weighted_proj_theta2_test_type), intent(inout) :: this

    type(operator_proxy_type) :: proj_p
    type(field_proxy_type)    :: theta_p
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer :: nlayers, dim, diff_dim, dim_w2
    integer :: ndf_w0, ndf_w2
    integer :: nqp_h, nqp_v

    integer :: i

    call feign_finite_element_config( finite_element_cellshape_quadrilateral, &
                                      0, 1, .true., .false., .false., .false.)

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level function space collection

    function_space_collection = function_space_collection_type()

    this%qr = quadrature_xyoz_type(3, quadrature_rule)

    this%qr_proxy = this%qr%get_quadrature_proxy()
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_collection = mesh_collection_type()
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, element_order, W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, element_order, W2 )

    this%proj = operator_type(this%w0_fs,this%w2_fs)

    this%theta = field_type( vector_space = this%w0_fs )
    theta_p    = this%theta%get_proxy()


    diff_dim = theta_p%vspace%get_dim_space_diff( )
    dim      = theta_p%vspace%get_dim_space( )
    ndf_w0   = theta_p%vspace%get_ndf()
    allocate(this%diff_basis_w0(diff_dim,ndf_w0,nqp_h,nqp_v) )
    allocate(this%basis_w0(dim,ndf_w0,nqp_h,nqp_v) )

    proj_p = this%proj%get_proxy()   
 
    ndf_w2  = proj_p%fs_from%get_ndf()
    dim_w2 = proj_p%fs_from%get_dim_space( )
    allocate(this%basis_w2(dim_w2 ,ndf_w2, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(weighted_proj_theta2_test_type), intent(inout) :: this

    deallocate( this%diff_basis_w0, &
                this%basis_w2, this%basis_w0)

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use weighted_proj_theta2_kernel_mod, only : weighted_proj_theta2_code
    use calc_exner_pointwise_mod,        only : calc_exner_pointwise

    implicit none

    class(weighted_proj_theta2_test_type), intent(inout) :: this

    type(operator_proxy_type) :: proj_p
    type(field_proxy_type )   :: theta_p

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def
    real(kind=r_def), parameter :: theta0 = 300.0_r_def
    real(kind=r_def)            :: dtheta(3)

    integer :: i, j, k, cell
    integer(i_def) :: err

    integer :: nlayers
    integer :: ndf_w0, undf_w0, ndf_w2
    integer :: nqp_h, nqp_v

    integer, pointer :: map_w0(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def), dimension(8,6) :: answer

    ! Test the operator kernel
    proj_p = this%proj%get_proxy()

    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    theta_p = this%theta%get_proxy()
    ndf_w0  = theta_p%vspace%get_ndf()
    undf_w0 = theta_p%vspace%get_undf()
    call theta_p%vspace%compute_basis_function(                    &
         this%basis_w0,ndf_w0, nqp_h, nqp_v, xp, zp )
    call theta_p%vspace%compute_diff_basis_function(               &
         this%diff_basis_w0,ndf_w0, nqp_h, nqp_v, xp, zp )


    nlayers = proj_p%fs_from%get_nlayers()
    ndf_w2  = proj_p%fs_from%get_ndf()
    call proj_p%fs_from%compute_basis_function(                     &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_w0 => theta_p%vspace%get_cell_dofmap(cell)
    proj_p%local_stencil(:,:,:) = 0.0_r_def
    theta_p%data(:) = theta0
     
    ! Set grad(theta) = (10,-5,1) in first cell
    dtheta(:) = (/ 10.0_r_def, -5.0_r_def, 1.0_r_def /)
    theta_p%data(map_w0(1)) = theta0
    theta_p%data(map_w0(2)) = theta0 + dtheta(1)
    theta_p%data(map_w0(3)) = theta0 + dtheta(1) + dtheta(2)
    theta_p%data(map_w0(4)) = theta0             + dtheta(2)
    theta_p%data(map_w0(5)) = theta0                         + dtheta(3)
    theta_p%data(map_w0(6)) = theta0 + dtheta(1)             + dtheta(3)
    theta_p%data(map_w0(7)) = theta0 + dtheta(1) + dtheta(2) + dtheta(3)
    theta_p%data(map_w0(8)) = theta0             + dtheta(2) + dtheta(3)

    call weighted_proj_theta2_code( cell,                              &
                                    nlayers,                           &
                                    proj_p%ncell_3d,                   &
                                    proj_p%local_stencil,              &
                                    theta_p%data,                      &
                                    1.0_r_def,                         &
                                    ndf_w0,                            &
                                    undf_w0,                           &
                                    map_w0,                            &
                                    this%basis_w0,                     &
                                    this%diff_basis_w0,                &
                                    ndf_w2,                            &
                                    this%basis_w2,                     &
                                    nqp_h,                             &
                                    nqp_v,                             &
                                    wh,                                &
                                    wv                                 &
                                   )
    call proj_operator(answer)
    @assertEqual(answer, proj_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine proj_operator( proj ) 
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(8,6), intent(inout) :: proj


  proj = real(reshape( [ &
   20, 10, 10, 20, 20, 10, 10, 20, &
   10, 10,  5,  5, 10, 10,  5,  5, &
   10, 20, 20, 10, 10, 20, 20, 10, &
    5,  5, 10, 10,  5,  5, 10, 10, &
    2,  2,  2,  2,  1,  1,  1,  1, &   
    1,  1,  1,  1,  2,  2,  2,  2  &   
 ], shape(proj) ),r_def)/24.0_r_def

    return
  end subroutine proj_operator

end module weighted_proj_theta2_kernel_mod_test
