!-----------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the 'vertical W2' mass matrix used in the damping layer term
!>
module compute_dl_matrix_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use extrusion_config_mod,          only : method, domain_top, number_of_layers
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W0, W2
  use function_space_mod,            only : function_space_type
  use operator_mod,                  only : operator_type, &
                                            operator_proxy_type
  use pFUnit_Mod
  use quadrature_xyoz_mod,           only : quadrature_xyoz_type, &
                                            quadrature_xyoz_proxy_type
  use quadrature_rule_gaussian_mod,  only : quadrature_rule_gaussian_type
  use damping_layer_config_mod,      only : dl_base, dl_str

  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm, clear_comm


  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: compute_dl_matrix_test_type
    private
    type( quadrature_xyoz_type )       :: qr
    type(quadrature_xyoz_proxy_type)   :: qr_proxy
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(operator_type)                :: mm
    real(r_def ), allocatable          :: basis_chi(:,:,:,:),      &
                                          diff_basis_chi(:,:,:,:), &
                                          basis_w2(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_dl_matrix_test_type

  integer(i_def), parameter :: element_order = 0_i_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6000000_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use mesh_mod,                  only: PLANE_BI_PERIODIC
    use gungho_feign_config_mod,   only: feign_damping_layer_config,  &
                                         feign_extrusion_config,      & 
                                         feign_timestepping_config,   & 
                                         feign_finite_element_config, &
                                         feign_planet_config

    use timestepping_config_mod,   only: timestepping_method_semi_implicit, &
                                         timestepping_runge_kutta_method_ssp3
    use extrusion_config_mod,      only: extrusion_method_uniform
    use finite_element_config_mod, only: finite_element_cellshape_quadrilateral

    implicit none

    class(compute_dl_matrix_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(operator_proxy_type) :: mm_p
    
    type( quadrature_rule_gaussian_type ) :: quadrature_rule

    integer(i_def) :: dim_mm, dim_chi, diff_dim_chi
    integer(i_def) :: nqp_h, nqp_v
    integer(i_def) :: ndf, ndf_chi

    integer(i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    ! Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    call feign_damping_layer_config( dl_base=800.0_r_def, dl_str=0.015_r_def )

    call feign_extrusion_config( domain_top=6000.0_r_def,         &
                                 method=extrusion_method_uniform, &
                                 number_of_layers=3_i_def )

    call feign_timestepping_config(                                   &
             method=timestepping_method_semi_implicit,                &
             dt=1.0_r_def, alpha=0.5_r_def, tau_u=0.5_r_def,          &
             tau_t=0.5_r_def, tau_r=0.5_r_def,                        &
             outer_iterations=4_i_def, inner_iterations=1_i_def,      &
             runge_kutta_method=timestepping_runge_kutta_method_ssp3, &
             spinup_period=0.0_r_def, spinup_alpha=.false.,           &
             spinup_winds=.false. )

    call feign_finite_element_config(                          &
             cellshape=finite_element_cellshape_quadrilateral, &
             coordinate_order=0_i_def,                         &
             element_order=element_order,                      &
             rehabilitate=.true.,                              &
             supg=.false.,                                     &
             vorticity_in_w1=.false. )

    call feign_planet_config( gravity=gravity, radius=radius, omega=omega, &
                              rd=rd, cp=cp, p_zero=p_zero,                 &
                              scaling_factor=scaling )

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr       = quadrature_xyoz_type(3, quadrature_rule)
    this%qr_proxy = this%qr%get_quadrature_proxy()
    
    nqp_h=this%qr_proxy%np_xy
    nqp_v=this%qr_proxy%np_z

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )


    this%w0_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                    element_order, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                    element_order, &
                                                    W2 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    dim_chi = chi_p(1)%vspace%get_dim_space( )
    diff_dim_chi = chi_p(1)%vspace%get_dim_space_diff( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate( this%basis_chi( dim_chi, ndf_chi, nqp_h, nqp_v ) )
    allocate( this%diff_basis_chi( diff_dim_chi, ndf_chi, nqp_h, nqp_v ) )

    this%mm = operator_type( this%w2_fs, this%w2_fs )
    mm_p    = this%mm%get_proxy()

    ndf    = mm_p%fs_from%get_ndf()
    dim_mm = mm_p%fs_from%get_dim_space( )
    allocate( this%basis_w2( dim_mm, ndf, nqp_h, nqp_v ) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(compute_dl_matrix_test_type), intent(inout) :: this

    deallocate( this%basis_chi, this%basis_w2, this%diff_basis_chi )

    call mesh_collection%clear()
    call function_space_collection%clear()
    call final_configuration()
    
    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use compute_dl_matrix_kernel_mod, only : compute_dl_matrix_code
    use mesh_mod,                     only : mesh_type

    implicit none

    class(compute_dl_matrix_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def
    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def

    type(field_proxy_type)   :: chi_p(3)

    real(kind=r_def) :: answer(6,6)

    integer(i_def) :: i, j, k, cell
    integer(i_def) :: err

    ! Required for calling mass matrix kernel

    integer(i_def) :: nlayers, ndf
    integer(i_def) :: ndf_chi, undf_chi
    integer(i_def) :: nqp_h, nqp_v

    integer(i_def), pointer   :: map_chi(:)  => null()
    type(operator_proxy_type) :: mm_p

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:)   => null()
    real(kind=r_def), pointer :: wv(:)   => null()
    type(mesh_type),  pointer :: mesh => null()

    mesh => mesh_collection%get_mesh( this%mesh_id )

    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1, r_def)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1, r_def)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k, r_def)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test mass matrix kernel
    mm_p = this%mm%get_proxy()

    nqp_h = this%qr_proxy%np_xy
    nqp_v = this%qr_proxy%np_z
    
    xp=>this%qr_proxy%points_xy
    zp=>this%qr_proxy%points_z
    wh=>this%qr_proxy%weights_xy
    wv=>this%qr_proxy%weights_z

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()

    nlayers = mm_p%fs_from%get_nlayers()
    ndf     = mm_p%fs_from%get_ndf()

    call chi_p(1)%vspace%compute_basis_function(                    &
         this%basis_chi,ndf_chi, nqp_h, nqp_v, xp, zp )

    call chi_p(1)%vspace%compute_diff_basis_function(               &
         this%diff_basis_chi,ndf_chi, nqp_h, nqp_v, xp, zp )

    call mm_p%fs_from%compute_basis_function(                       &
         this%basis_w2, ndf, nqp_h, nqp_v, xp, zp )

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)

    mm_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_dl_matrix_code( cell,                              &
                                 nlayers,                           &
                                 mm_p%ncell_3d,                     &
                                 mm_p%local_stencil,                &
                                 chi_p(1)%data,                     &
                                 chi_p(2)%data,                     &
                                 chi_p(3)%data,                     &
                                 ndf,                               &
                                 this%basis_w2,                     &
                                 ndf_chi,                           &
                                 undf_chi,                          &
                                 map_chi,                           &
                                 this%basis_chi,                    &
                                 this%diff_basis_chi,               &
                                 nqp_h,                             &
                                 nqp_v,                             &
                                 wh,                                &
                                 wv                                 &
                                 )

    call mass_matrix_dl(answer)

    ! Normalise by 1/average edge length
    answer(:,:) = answer(:,:) * ( dx + dy + dz ) / 3.0_r_def

    mm_p%local_stencil(:, :, 1 ) = mm_p%local_stencil(:, :, 1 ) &
                                    * ( dx + dy + dz ) / 3.0_r_def

    @assertEqual( answer, mm_p%local_stencil(:, :, 1 ), tol )

  end subroutine test_all

  subroutine mass_matrix_dl( mm )
    implicit none

    real(kind=r_def), dimension(6,6), intent(inout) :: mm
    mm = reshape( [ &
      0.9999999999999905E-03_r_def, &
      0.0000000000000000E+00_r_def, &
      0.4999999999999963E-03_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.2777777777777752E-04_r_def, &
      0.0000000000000000E+00_r_def, &
      0.1388888888888879E-04_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.4999999999999963E-03_r_def, &
      0.0000000000000000E+00_r_def, &
      0.9999999999999907E-03_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.1388888888888879E-04_r_def, &
      0.0000000000000000E+00_r_def, &
      0.2777777777777752E-04_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.1111146217692656E-03_r_def, &
      0.5556927496794279E-04_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.0000000000000000E+00_r_def, &
      0.5556927496794279E-04_r_def, &
      0.1112052038755525E-03_r_def  &
      ], shape(mm) )
    return
  end subroutine mass_matrix_dl

end module compute_dl_matrix_kernel_mod_test
