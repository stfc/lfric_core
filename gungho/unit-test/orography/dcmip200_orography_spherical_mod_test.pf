!-----------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------
!> @brief Test suite to verify calculation of DCMIP2.0.0. mountain orography
!>        profile in spherical coordinates.
!-------------------------------------------------------------------------------
module dcmip200_orography_spherical_mod_test

  use constants_mod,              only : i_def, r_def, PI, str_def, &
                                         str_max_filename
  use global_mesh_collection_mod, only : global_mesh_collection_type, &
                                         global_mesh_collection
  use global_mesh_mod,            only : global_mesh_type
  use mesh_collection_mod,        only : mesh_collection_type, &
                                         mesh_collection
  use analytic_orography_mod,     only : orography_profile
  use yaxt,                       only : xt_initialize, xt_finalize
  use mpi_mod,                    only : store_comm, clear_comm

  use pFUnit_Mod

  implicit none

  private
  public :: setUp, tearDown, test_dcmip200_orography_spherical

  @testCase
  type, public, extends(MPITestCase) :: dcmip200_orography_spherical_test_type
    real(r_def) :: mountain_height
    real(r_def) :: radius_dec
    real(r_def) :: osc_half_width_dec
    real(r_def) :: lambda_centre_dec
    real(r_def) :: phi_centre_dec
  contains
    procedure     :: setUp
    procedure     :: tearDown
    procedure     :: test_dcmip200_orography_spherical
  end type dcmip200_orography_spherical_test_type

  character(str_def), parameter   :: mesh_name  = 'unit_test'
  character(len=str_max_filename) :: filename   = "data/mesh_C16.nc"
  real(r_def),        parameter   :: domain_top = 10000.0_r_def
  integer(i_def),     parameter   :: npanels    = 6_i_def
  integer(i_def),     parameter   :: nlayers    = 1_i_def
  integer(i_def),     parameter   :: element_order = 0_i_def
  integer(i_def),     parameter   :: total_ranks   = 1_i_def
  integer(i_def),     parameter   :: local_rank    = 0_i_def
  integer(i_def),     parameter   :: xproc         = 1_i_def
  integer(i_def),     parameter   :: yproc         = 1_i_def

  real(r_def), parameter :: gravity = 10.0_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 8.0E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 300.0_r_def
  real(r_def), parameter :: cp      = 1000.0_r_def
  real(r_def), parameter :: scaling = 125.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp(this)

    use dcmip200_orography_spherical_mod, only : dcmip200_spherical_type
    use finite_element_config_mod,        only : finite_element_cellshape_quadrilateral
    use base_mesh_config_mod,             only : base_mesh_geometry_spherical, &
                                                 base_mesh_partitioner_cubedsphere
    use gungho_feign_config_mod,          only :                  &
                                          feign_base_mesh_config, &
                                          feign_planet_config,    &
                                          feign_orography_dcmip200_spherical_config
    use orography_dcmip200_spherical_config_mod, only :                    &
                                                 radius, osc_half_width,   &
                                                 lambda_centre, phi_centre

    implicit none

    class(dcmip200_orography_spherical_test_type), intent(inout) :: this

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level collections
    global_mesh_collection = global_mesh_collection_type()
    mesh_collection = mesh_collection_type()

    ! Set mesh configuration
    call feign_base_mesh_config(                                              &
                         filename        = filename,                          & 
                         prime_mesh_name = mesh_name,                         &
                         geometry        = base_mesh_geometry_spherical,      &
                         partitioner     = base_mesh_partitioner_cubedsphere, &
                         fplane          = .false.,                           &
                         f_lat_deg       = 0.0_r_def )

    ! Set planet parameters
    call feign_planet_config( gravity        = gravity, &
                              radius         = radius,  &
                              omega          = omega,   & 
                              rd             = rd,      &
                              cp             = cp,      &  
                              p_zero         = p_zero,  &
                              scaling_factor = scaling )

    ! Set DCMIP2.0.0 spherical configuration
    this%mountain_height     = 2000.0_r_def
    this%radius_dec          = 0.75_r_def
    this%osc_half_width_dec  = 0.0625_r_def
    this%lambda_centre_dec   = 1.5_r_def
    this%phi_centre_dec      = 0.0_r_def
    call feign_orography_dcmip200_spherical_config(                             &
                                  mountain_height    = this%mountain_height,    &
                                  radius_dec         = this%radius_dec,         &
                                  osc_half_width_dec = this%osc_half_width_dec, &
                                  lambda_centre_dec  = this%lambda_centre_dec,  &
                                  phi_centre_dec     = this%phi_centre_dec)

    ! Initialise DCMIP2.0.0 spherical orography type
    allocate( orography_profile,                                      &
              source = dcmip200_spherical_type( this%mountain_height, &
                                                radius,               &
                                                osc_half_width,        &
                                                lambda_centre,        &
                                                phi_centre ) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown(this)

    use gungho_configuration_mod, only: final_configuration

    implicit none

    class(dcmip200_orography_spherical_test_type), intent(inout) :: this

    deallocate( orography_profile )
    call global_mesh_collection%clear()
    call mesh_collection%clear()
    call final_configuration()

    ! Finalise YAXT
    call xt_finalize()
    ! Clear the stored MPI communicator
    call clear_comm()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_dcmip200_orography_spherical(this)

    use extrusion_mod,        only : uniform_extrusion_type
    use mesh_mod,             only : mesh_type
    use partition_mod,        only : partition_type,        &
                                     partitioner_interface, &
                                     partitioner_cubedsphere_serial
    use extrusion_config_mod, only : extrusion_method_uniform

    implicit none
    ! Arguments
    class(dcmip200_orography_spherical_test_type), intent(inout) :: this
    ! Mesh and partition types
    integer(i_def)                            :: global_mesh_id
    integer(i_def)                            :: mesh_id
    type(global_mesh_type),           pointer :: global_mesh => null()
    type(mesh_type),                  pointer :: mesh => null()
    type(partition_type)                      :: partition
    procedure(partitioner_interface), pointer :: partitioner_ptr => null()
    type(uniform_extrusion_type)              :: extrusion
    ! Test parameters
    real(r_def), parameter :: tol = 1.0e-3_r_def
    real(r_def)            :: vertex_coords_2d(3,1), chi_surf, chi_test
    integer(i_def)         :: itest

    ! Add global mesh
    global_mesh_id =  global_mesh_collection%add_new_global_mesh( filename,  &
                                                                  mesh_name, &
                                                                  npanels )
    global_mesh    => global_mesh_collection%get_global_mesh( global_mesh_id )

    ! Assign partitioner
    partitioner_ptr => partitioner_cubedsphere_serial
    partition = partition_type( global_mesh,     &
                                partitioner_ptr, &
                                xproc,           &
                                yproc,           &
                                1,               &
                                local_rank,      &
                                total_ranks )
    extrusion = uniform_extrusion_type( radius, domain_top, nlayers )

    ! Construct a mesh which is uniform in vertical for purpose of the test
    mesh_id = mesh_collection%add_new_mesh( global_mesh, &
                                            partition,   &
                                            extrusion )
    mesh => mesh_collection%get_mesh( mesh_id )

    ! Calculate surface height for selected number of points
    itest = 905
    call global_mesh%get_vert_coords(itest,vertex_coords_2d(:,1))
    chi_surf = orography_profile%analytic_orography(vertex_coords_2d(1,1), &
                                                    vertex_coords_2d(2,1))
    chi_test = this%mountain_height
    @assertEqual( chi_surf, chi_test, tol )

    itest = 202
    call global_mesh%get_vert_coords(itest,vertex_coords_2d(:,1))
    chi_surf = orography_profile%analytic_orography(vertex_coords_2d(1,1), &
                                                    vertex_coords_2d(2,1))


    chi_test = 5.61486_r_def
    @assertEqual( chi_surf, chi_test, tol )

    return
  end subroutine test_dcmip200_orography_spherical

end module dcmip200_orography_spherical_mod_test
