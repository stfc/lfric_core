!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the fuunction space collection object
!>
module function_space_collection_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2, W3
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: function_space_collection_test_type
    private
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(function_space_type), pointer :: temp_w0_fs => null()
    integer(i_def)                     :: mesh_id

    integer, allocatable :: dummy_for_gcc

  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type function_space_collection_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use gungho_feign_config_mod,   only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(function_space_collection_test_type), intent(inout) :: this


    call feign_finite_element_config( finite_element_cellshape_quadrilateral, &
                                      0, 1, .true., .false., .false., .false.)

    reference_element = finite_element_cellshape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! create the mesh
    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(function_space_collection_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )


    implicit none

    class(function_space_collection_test_type), intent(inout) :: this

    integer(i_def) :: answer = 3
    integer(i_def) :: fs_0
    integer(i_def) :: fs_2
    integer(i_def) :: fs_3

    type(function_space_type), pointer :: test_fs => null()

    ! create 3 function spaces

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W2 )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W3 )

    ! Check we have 3 function spaces in the collection

    @assertEqual(answer, function_space_collection%get_fs_collection_size())

    ! Check they have unique ids
    fs_0 = this%w0_fs%get_id()
    fs_2 = this%w2_fs%get_id()
    fs_3 = this%w3_fs%get_id()

    @assertFalse(fs_0 == fs_2)
    @assertFalse(fs_2 == fs_3)
    @assertFalse(fs_3 == fs_0)

    ! try to get a function space already in the list
    ! it should have the correct id and the number of
    ! function spaces in the collection should not increase

    this%temp_w0_fs => function_space_collection%get_fs( this%mesh_id,  &
                                                         element_order, &
                                                         W0 )

    @assertEqual(fs_0, this%temp_w0_fs%get_id())

    @assertEqual(answer, function_space_collection%get_fs_collection_size())

    ! Test you can get the same function space by it's id
    test_fs => function_space_collection%get_fs_by_id(fs_0)
    @assertAssociated(test_fs, this%temp_w0_fs)
    
    
  end subroutine test_all



end module function_space_collection_mod_test
