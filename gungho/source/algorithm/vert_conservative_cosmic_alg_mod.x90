!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!-------------------------------------------------------------------------------
!> @brief Calculates the mass flux in the z direction.
!> @details The algorithm below outputs the mass flux at timestep n+1 (np1)
!>          given the wind fields at timestep n and n+1 and the density field at
!>          timestep n. A positive mass flux in the z direction refers to an
!>          upward flow (i.e. positive z direction with outward normal).
module vert_conservative_cosmic_alg_mod

  use constants_mod,             only: r_def
  use field_mod,                 only: field_type
  use function_space_mod,        only: function_space_type
  use psykal_lite_mod,           only: invoke_vertical_flux_kernel
  use subgrid_config_mod,        only: rho_approximation_constant_subgrid,     &
                                       rho_approximation_constant_positive,    &
                                       rho_approximation_ppm_no_limiter,       &
                                       rho_approximation_ppm_positive_only,    &
                                       rho_approximation_ppm_positive_monotone
  use subgrid_config_mod,        only: rho_approximation
  use log_mod,                   only: log_event, LOG_LEVEL_ERROR
  use vert_ppm_no_limiter_kernel_mod, only: vert_ppm_no_limiter_kernel_type

  implicit none

  private
  public :: vert_conservative_cosmic_alg

contains

  !> @brief   Calculates the mass flux in the z direction.
  !> @details The algorithm below outputs the mass flux at timestep n+1 (np1)
  !>          given the wind fields at timestep n and n+1 and the density field at
  !>          timestep n. A positive mass flux in the z direction refers to an
  !>          upward flow (i.e. positive z direction with outward normal).
  !> @param[out] mass_flux_z       Mass flux in z direction used to update density
  !> @param[in]  rho               Density at time level n
  !> @param[in]  dep_pts           Departure points
  !> @param[in]  dt                Timestep length
  subroutine vert_conservative_cosmic_alg(  mass_flux_z,        &
                                            rho,                &
                                            dep_pts,            &
                                            dt )

    implicit none

    type(field_type), intent(inout) :: mass_flux_z
    type(field_type), intent(in)    :: rho
    type(field_type), intent(in)    :: dep_pts
    real(r_def),      intent(in)    :: dt

    type(field_type) :: a0, a1, a2
    type(function_space_type), pointer :: w3_fs     => null()

    w3_fs => rho%get_function_space()

    call a0%initialise( vector_space = w3_fs )
    call a1%initialise( vector_space = w3_fs )
    call a2%initialise( vector_space = w3_fs )

    ! Calculate a0, a1 and a2 which are coefficients of subgrid representation
    ! of density where rho(x) = a0 + a1*x+a2*x**2 with 0<x<1.
    select case(rho_approximation)
      case (rho_approximation_constant_subgrid)
        call invoke( setval_X(a0, rho) )
        call invoke( setval_c(a1, 0.0_r_def) )
        call invoke( setval_c(a2, 0.0_r_def) )
      case (rho_approximation_ppm_no_limiter)
        call invoke( vert_ppm_no_limiter_kernel_type(a0,a1,a2,rho) )
      case (rho_approximation_constant_positive, &
            rho_approximation_ppm_positive_only, &
            rho_approximation_ppm_positive_monotone)
        call log_event( "Error: this method has not yet been committed to trunk, see #1311", LOG_LEVEL_ERROR )
    end select

    ! Calculate mass flux in one-dimesion.
    ! The integral summation allows for CFL values larger than 1.0.
    call invoke_vertical_flux_kernel(mass_flux_z,dep_pts,rho,a0,a1,a2,dt)

    nullify( w3_fs )

  end subroutine vert_conservative_cosmic_alg

end module vert_conservative_cosmic_alg_mod
