!-----------------------------------------------------------------------------
! (c) Crown copyright 2019 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief Compute the advective update A of a variable f by a wind U: A = U.grad(f)
!> @todo There are number of workarounds in this algorithm for aspects which
!!       aren't yet supported by PSyclone/LFRic:
!!       1. Issue #868 Multidata fields are not natively supported in PSyclone.
!!                     Currently we treat these as normal fields but need to
!!                     additionally pass in ndata_h or ndata_v to the kernels
!!                     as extra integer arguments.
!!       2. Issue #1246 Region stencils require both the global maximum size of
!!                      the stencil and local column size of the stencil.
!!                      PSyclone currently only passes in the local size and so
!!                      until this is resolved we also pass in the global maximum
!!                      size as an integer argument.
!!       3. Issue #1248 There is no PSyclone support for passing logical types
!!                      to kernels and so as an intermediate solution we create
!!                      an integer argument that = 1 (.true.) or = 0 (.false.).
module advective_update_alg_mod

  use constants_mod,                      only: r_def, i_def, l_def
  use geometric_constants_mod,            only: get_coordinates,                &
                                                get_panel_id
  use fem_constants_mod,                  only: get_mass_matrix_diagonal,       &
                                                get_inverse_lumped_mass_matrix, &
                                                get_multiplicity
  use field_mod,                          only: field_type
  use finite_element_config_mod,          only: element_order,          &
                                                nqp_exact,              &
                                                spherical_coord_system, &
                                                spherical_coord_system_xyz
  use fs_continuity_mod,                  only: W1, Wtheta
  use function_space_mod,                 only: function_space_type
  use quadrature_rule_gauss_lobatto_mod,  only: quadrature_rule_gauss_lobatto_type
  use quadrature_rule_gaussian_mod,       only: quadrature_rule_gaussian_type
  use quadrature_xyoz_mod,                only: quadrature_xyoz_type
  use quadrature_face_mod,                only: quadrature_face_type
  use quadrature_edge_mod,                only: quadrature_edge_type
  use rtheta_kernel_mod,                  only: rtheta_kernel_type
  use rtheta_bd_kernel_mod,               only: rtheta_bd_kernel_type
  use transport_config_mod,               only: operators,            &
                                                fv_advective_order,   &
                                                operators_fv,         &
                                                operators_fem,        &
                                                oned_reconstruction,  &
                                                enforce_monotonicity, &
                                                consistent_metric
  use log_mod,                            only: log_event,         &
                                                LOG_LEVEL_ERROR,   &
                                                LOG_LEVEL_INFO
  use io_config_mod,                      only: subroutine_timers
  use timer_mod,                          only: timer
  use multiplicity_kernel_mod,            only: multiplicity_kernel_type
  use poly_advective_kernel_mod,          only: poly_advective_kernel_type
  use poly1d_adv_recon_kernel_mod,        only: poly1d_adv_recon_kernel_type
  use poly1d_vert_adv_coeffs_kernel_mod,  only: poly1d_vert_adv_coeffs_kernel_type
  use poly1d_advective_coeffs_kernel_mod, only: poly1d_advective_coeffs_kernel_type
  use poly1d_vert_adv_kernel_mod,         only: poly1d_vert_adv_kernel_type
  use poly2d_advective_coeffs_kernel_mod, only: poly2d_advective_coeffs_kernel_type
  use poly2d_adv_recon_kernel_mod,        only: poly2d_adv_recon_kernel_type
  use runtime_tools_mod,                  only: find_mesh_index

  implicit none

  private

  logical(kind=l_def) :: variables_allocated = .false.

  integer(kind=i_def), allocatable, target :: adv_stencil_extent_list(:)
  integer(kind=i_def), allocatable, target :: adv_2d_stencil_size_list(:)
  integer(kind=i_def), allocatable, target :: ndata_h_list(:)
  integer(kind=i_def), allocatable, target :: ndata_v_list(:)
  type(field_type),    allocatable, target :: adv_coeffs_list(:)
  type(field_type),    allocatable, target :: vert_adv_coeffs_list(:)
  type(field_type),    allocatable, target :: chi3_at_wt_list(:)

  ! Private lists for variables that are initialised at the start but updated
  type(field_type),    allocatable :: wt_metrics(:)
  type(field_type),    allocatable :: advective_av(:)
  type(field_type),    allocatable :: advective_av_chi3(:)
  type(field_type),    allocatable :: u_grad_f(:)

  public :: advective_update_set_num_meshes
  public :: advective_update_alg_init
  public :: advective_update_alg_final
  public :: advective_update_alg
  public :: get_adv_stencil_extent
  public :: get_adv_2d_stencil_size
  public :: get_adv_ndata_h
  public :: get_adv_ndata_v
  public :: get_adv_coeffs
  public :: get_vert_adv_coeffs
  public :: get_chi3_at_wt

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !> @brief Allocates the lists of advection coefficients
  !>
  !> @param[in] number_of_meshes Number of meshes to have coefficients on.
  subroutine advective_update_set_num_meshes( number_of_meshes )

    implicit none

    integer(kind=i_def), intent(in) :: number_of_meshes

    if (.not. variables_allocated) then
      if (.not. allocated(adv_stencil_extent_list)) allocate(adv_stencil_extent_list(number_of_meshes))
      if (.not. allocated(adv_2d_stencil_size_list)) allocate(adv_2d_stencil_size_list(number_of_meshes))
      if (.not. allocated(ndata_h_list)) allocate(ndata_h_list(number_of_meshes))
      if (.not. allocated(ndata_v_list)) allocate(ndata_v_list(number_of_meshes))
      if (.not. allocated(adv_coeffs_list)) allocate(adv_coeffs_list(number_of_meshes))
      if (.not. allocated(vert_adv_coeffs_list)) allocate(vert_adv_coeffs_list(number_of_meshes))
      if (.not. allocated(chi3_at_wt_list)) allocate(chi3_at_wt_list(number_of_meshes))

      if (.not. allocated(wt_metrics)) allocate(wt_metrics(number_of_meshes))
      if (.not. allocated(advective_av)) allocate(advective_av(number_of_meshes))
      if (.not. allocated(advective_av_chi3)) allocate(advective_av_chi3(number_of_meshes))
      if (.not. allocated(u_grad_f)) allocate(u_grad_f(number_of_meshes))
      variables_allocated = .true.
    else
      call log_event('Variables have already been allocated for ' // &
                     'advective_update_alg', LOG_LEVEL_ERROR)
    end if

  end subroutine

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  !> @brief Initialises the advection coefficients for a single mesh
  !>
  !> @details Initialises the advection coefficients and other constants that
  !>          can be pre-computed for a single mesh.
  !>          FV aspects currently only support lowest order elements.
  !>
  !> @param[in] mesh_id Identity of the mesh to create all fields on
  !>
  subroutine advective_update_alg_init( mesh_id )

    use function_space_collection_mod,     only: function_space_collection
    use sample_field_kernel_mod,           only: sample_field_kernel_type
    use reference_element_mod,             only: reference_element_type
    use mesh_mod,                          only: mesh_type

    implicit none

    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    integer(kind=i_def)             :: adv_stencil_size
    integer(kind=i_def)             :: adv_stencil_extent
    integer(kind=i_def)             :: adv_2d_stencil_size
    integer(kind=i_def)             :: ndata_h
    integer(kind=i_def)             :: ndata_v
    integer(kind=i_def)             :: nfaces_re_h
    integer(kind=i_def)             :: nfaces_re_v
    integer(kind=i_def)             :: polynomial
    type(field_type ),      pointer :: chi_sph(:) => null()
    type(field_type ),      pointer :: md_wt => null()
    type(field_type ),      pointer :: panel_id => null()

    type(quadrature_rule_gaussian_type )      :: gaussian_quadrature
    type(quadrature_rule_gauss_lobatto_type ) :: gauss_lobatto_quadrature

    type(quadrature_xyoz_type)             :: qr
    type(quadrature_edge_type)             :: qre
    type(function_space_type),     pointer :: w1_fs => null()
    type(function_space_type),     pointer :: wt_fs => null()
    type(field_type)                       :: wt_field
    class(reference_element_type), pointer :: reference_element => null()
    type(mesh_type),               pointer :: mesh => null()
    integer(kind=i_def)                    :: edge_quad_order
    integer(kind=i_def)                    :: nqp_xy, nqp_z, nqp_xyz
    real(kind=r_def)                       :: transform_radius, chi3_max
    type(function_space_type),     pointer :: multidata_fs => null()

    mesh_index = find_mesh_index(mesh_id)

    ! Set default values of 0 for the integers to be stored for this mesh_id,
    ! in case they aren't set below
    adv_stencil_extent = 0_i_def
    adv_2d_stencil_size = 0_i_def
    ndata_h = 0_i_def
    ndata_v = 0_i_def

    w1_fs => function_space_collection%get_fs( mesh_id, element_order, W1 )
    wt_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta )
    call u_grad_f(mesh_index)%initialise( vector_space = wt_fs )

    if ( operators == operators_fv ) then
      ! This setup is only needed for FV methods
      if ( element_order == 0 ) then

        ! Compute stencil extents
        adv_stencil_extent = int(real(fv_advective_order+1)/2.0,i_def)
        !======================================================================!
        ! Compute stencil for nonuniform polynomial fit
        ! Initialise PSy layer arrays (this will not be needed when
        ! multi-dimensional field arrays are supported)
        ! This needs some temporary fields for computing undf in the PSy layer
        md_wt => get_mass_matrix_diagonal(Wtheta, mesh_id)
        call wt_field%initialise( vector_space = wt_fs )
        call advective_av(mesh_index)%initialise( vector_space = w1_fs )
        call advective_av_chi3(mesh_index)%initialise( vector_space = w1_fs )

        ! Get topology information from the reference element
        mesh              => wt_field%get_mesh()
        reference_element => mesh%get_reference_element()
        ! Number of horizontal neighbours from the reference element
        nfaces_re_h = reference_element%get_number_horizontal_faces()
        ! Number of vertical neighbours from the reference element
        nfaces_re_v = reference_element%get_number_vertical_faces()
        if ( oned_reconstruction ) then
          adv_stencil_size = fv_advective_order + 1
        else
          adv_2d_stencil_size = 1
          do polynomial = 1, fv_advective_order
            if ( mod(polynomial, 2) == 0 ) then
              adv_2d_stencil_size = adv_2d_stencil_size + nfaces_re_h
            else
              adv_2d_stencil_size = adv_2d_stencil_size + nfaces_re_h*polynomial
            end if
          end do
          adv_stencil_size = adv_2d_stencil_size
        end if

        ! Create multi-data space and field for the horizontal advective coefficients
        ndata_h = nfaces_re_h*adv_stencil_size
        multidata_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta, ndata_h )
        call adv_coeffs_list(mesh_index)%initialise( vector_space = multidata_fs, ndata_first = .true. )

        chi_sph => get_coordinates(mesh_id)
        panel_id => get_panel_id(mesh_id)

        if ( spherical_coord_system == spherical_coord_system_xyz ) then
          transform_radius = 0.0_r_def
        else
          ! Compute the smallest surface height of chi(3)
          ! (this is only needed for spherically-based coordinates
          ! Transform radius is minimum height + 1
          call chi_sph(3)%field_minmax(transform_radius, chi3_max)
          transform_radius = 1.0_r_def + abs(transform_radius)
        end if

        nqp_xyz = fv_advective_order+1
        if ( oned_reconstruction ) then
          edge_quad_order = 1
        else
          edge_quad_order = fv_advective_order+1
        end if

        ! Compute advective coefficients
        call log_event( "Gungho: computing horizontal advective coeffs", LOG_LEVEL_INFO )
        ! Quadrature rule which has enough points to exactly integrate
        ! fv_advective_order polynomials and is odd order horizontaly and has
        ! 2 vertical points such that it gives points on the vertical faces
        nqp_xy = fv_advective_order+3
        nqp_z  = 2
        qr = quadrature_xyoz_type(nqp_xy, nqp_xy, nqp_z, &
                                  gauss_lobatto_quadrature)
        ! Edge based quadrature rule on horizontal edges
        nqp_xyz = edge_quad_order
        qre = quadrature_edge_type(nqp_xyz, .true., .false.,           &
                                   reference_element, gaussian_quadrature)

        if ( oned_reconstruction ) then
          call invoke( poly1d_advective_coeffs_kernel_type(                    &
                                               adv_coeffs_list(mesh_index),    &
                                               md_wt,                          &
                                               adv_stencil_extent,             &
                                               chi_sph,                        &
                                               adv_stencil_extent,             &
                                               panel_id,                       &
                                               adv_stencil_extent,             &
                                               ndata_h, fv_advective_order,    &
                                               nfaces_re_h, transform_radius,  &
                                               qr, qre ) )
        else
          call invoke( poly2d_advective_coeffs_kernel_type(                    &
                                               adv_coeffs_list(mesh_index),    &
                                               md_wt,                          &
                                               adv_stencil_extent,             &
                                               chi_sph,                        &
                                               adv_stencil_extent,             &
                                               panel_id,                       &
                                               adv_stencil_extent,             &
                                               ndata_h, fv_advective_order,    &
                                               nfaces_re_h,                    &
                                               adv_2d_stencil_size,            &
                                               transform_radius, qr, qre ) )
        end if
        ! Vertical advective coeffs
        call log_event( "Gungho: computing vertical advective coeffs", LOG_LEVEL_INFO )
        ! Create multi-data space and field for the vertical advective coefficients
        ndata_v = nfaces_re_v*(fv_advective_order+1)
        multidata_fs => function_space_collection%get_fs( mesh_id, element_order, Wtheta, ndata_v )
        call vert_adv_coeffs_list(mesh_index)%initialise( vector_space = multidata_fs, ndata_first = .true. )

        call invoke( poly1d_vert_adv_coeffs_kernel_type(                       &
                                            vert_adv_coeffs_list(mesh_index),  &
                                            chi_sph, ndata_v,                  &
                                            fv_advective_order) )
        if ( consistent_metric ) then
          call wt_metrics(mesh_index)%initialise(vector_space = wt_fs )
          call chi3_at_wt_list(mesh_index)%initialise(vector_space = wt_fs )
          call invoke( setval_c(wt_field,   0.0_r_def),                        &
                       setval_c(chi3_at_wt_list(mesh_index), 0.0_r_def),       &
                       multiplicity_kernel_type(wt_field),                     &
                       sample_field_kernel_type(chi3_at_wt_list(mesh_index),   &
                                                wt_field, chi_sph(3)) )
        end if
        !======================================================================!
      else
        call log_event( "Gungho: subgrid mappings needed for FV MoL with high &
                         order elements", LOG_LEVEL_ERROR )
      end if
    end if

    adv_stencil_extent_list(mesh_index) = adv_stencil_extent
    adv_2d_stencil_size_list(mesh_index) = adv_2d_stencil_size
    ndata_h_list(mesh_index) = ndata_h
    ndata_v_list(mesh_index) = ndata_v

    nullify( chi_sph, panel_id, md_wt, wt_fs, w1_fs, reference_element, mesh )

  end subroutine advective_update_alg_init

  !=============================================================================
  !> @details Finalises allocatable arrays and calls finalising routines for
  !>          kernels that this algorithm initialised.
  subroutine advective_update_alg_final()

    implicit none

    if (allocated(advective_av_chi3)) deallocate(advective_av_chi3)
    if (allocated(chi3_at_wt_list)) deallocate(chi3_at_wt_list)
    if (allocated(u_grad_f)) deallocate(u_grad_f)
    if (allocated(advective_av)) deallocate(advective_av)
    if (allocated(wt_metrics)) deallocate(wt_metrics)
    if (allocated(vert_adv_coeffs_list)) deallocate(vert_adv_coeffs_list)
    if (allocated(adv_coeffs_list)) deallocate(adv_coeffs_list)
    if (allocated(ndata_v_list)) deallocate(ndata_v_list)
    if (allocated(ndata_h_list)) deallocate(ndata_h_list)
    if (allocated(adv_2d_stencil_size_list)) deallocate(adv_2d_stencil_size_list)
    if (allocated(adv_stencil_extent_list)) deallocate(adv_stencil_extent_list)

  end subroutine advective_update_alg_final

  !=============================================================================
  !> @brief Compute advective update wind.grad(adv_field) using desired spatial
  !>        reconstruction. Options for this are either FE or FV reconstructions.
  !>        A monotonicity filter is also applied if enforce_monotonicity=.true.
  !> @param[in,out] rhs Advective increment
  !> @param[in]    adv_field Field to advect
  !> @param[in]    wind Advecting wind field
  !> @param[in]    dt timestep
  !> @param[in]    logspace Carry out interpolation in log space
  subroutine advective_update_alg(rhs, adv_field, wind, dt, logspace)

    use consistent_wind_kernel_mod,  only: consistent_wind_kernel_type
    use reference_element_mod,       only: reference_element_type
    use mesh_mod,                    only: mesh_type
    use monotonic_update_kernel_mod, only: monotonic_update_kernel_type

    implicit none

    type(field_type),  intent(in)             :: adv_field, wind
    type(field_type),  intent(inout)          :: rhs
    real(kind=r_def),  intent(in)             :: dt
    logical(kind=l_def), optional, intent(in) :: logspace

    type(quadrature_xyoz_type)             :: qr
    type(quadrature_face_type)             :: qrf
    type(quadrature_rule_gaussian_type)    :: quadrature_rule
    type(field_type), pointer              :: chi_sph(:) => null()
    type(field_type), pointer              :: mt_lumped_inv => null()
    class(reference_element_type), pointer :: reference_element => null()
    type(mesh_type),               pointer :: mesh => null()
    integer(kind=i_def)                    :: mono_stencil, mesh_id, mesh_index
    type(field_type)                       :: consistent_wind
    type(field_type),              pointer :: adv_coeffs => null()
    type(field_type),              pointer :: vert_adv_coeffs => null()
    type(field_type),              pointer :: chi3_at_wt => null()
    type(field_type),              pointer :: w1_multiplicity => null()

    integer(kind=i_def) :: logspace_loc
    integer(kind=i_def), pointer :: adv_stencil_extent => null()
    integer(kind=i_def), pointer :: adv_2d_stencil_size => null()
    integer(kind=i_def), pointer :: ndata_h => null()
    integer(kind=i_def), pointer :: ndata_v => null()

    logspace_loc = 0
    if ( present(logspace) ) then
      if ( logspace ) logspace_loc = 1
    end if

    mesh_id = wind%get_mesh_id()
    mesh_index = find_mesh_index(mesh_id)
    adv_stencil_extent => adv_stencil_extent_list(mesh_index)
    adv_2d_stencil_size => adv_2d_stencil_size_list(mesh_index)
    ndata_h => ndata_h_list(mesh_index)
    ndata_v => ndata_v_list(mesh_index)
    adv_coeffs => adv_coeffs_list(mesh_index)
    vert_adv_coeffs => vert_adv_coeffs_list(mesh_index)

    w1_multiplicity => get_multiplicity(W1, mesh_id)
    mt_lumped_inv => get_inverse_lumped_mass_matrix(Wtheta, mesh_id)
    chi3_at_wt => chi3_at_wt_list(mesh_index)

    select case(operators)

    case default
      call log_event( "Gungho: Unrecognised option for operator.", LOG_LEVEL_ERROR )

    case(operators_fv)
      ! Set default value (for advective_av and advective_av_chi3)
      ! to be average of the field at W1 points and then update where the wind
      ! is non-zero using an upwind reconstruction
      call invoke( setval_c(advective_av(mesh_index), 0.0_r_def),            &
                   sample_field_kernel_type(advective_av(mesh_index),          &
                                            w1_multiplicity,                   &
                                            adv_field) )
      if ( consistent_metric ) then
        call invoke( setval_c(advective_av_chi3(mesh_index), 0.0_r_def),       &
                     sample_field_kernel_type(advective_av_chi3(mesh_index),   &
                                              w1_multiplicity,                 &
                                              chi3_at_wt) )
      end if
      if ( oned_reconstruction ) then
        ! Use 1d reconstruction
        call invoke( poly1d_adv_recon_kernel_type(                             &
                                              advective_av(mesh_index),        &
                                              wind, adv_field,                 &
                                              adv_stencil_extent,              &
                                              adv_coeffs,                      &
                                              ndata_h,                         &
                                              fv_advective_order ) )
        ! Horizontal advection of metric terms
        if ( consistent_metric ) &
          call invoke( poly1d_adv_recon_kernel_type(                           &
                                              advective_av_chi3(mesh_index),   &
                                              wind, chi3_at_wt,                &
                                              adv_stencil_extent,              &
                                              adv_coeffs,                      &
                                              ndata_h,                         &
                                              fv_advective_order ) )
      else
        ! Use 2d reconstruction
        call invoke( poly2d_adv_recon_kernel_type(                             &
                                              advective_av(mesh_index),        &
                                              wind, adv_field,                 &
                                              adv_stencil_extent,              &
                                              adv_coeffs,                      &
                                              ndata_h,                         &
                                              adv_2d_stencil_size ) )

        ! Horizontal advection of metric terms
        if ( consistent_metric ) &
          call invoke( poly2d_adv_recon_kernel_type(                           &
                                                advective_av_chi3(mesh_index), &
                                                wind, chi3_at_wt,              &
                                                adv_stencil_extent,            &
                                                adv_coeffs,                    &
                                                ndata_h,                       &
                                                adv_2d_stencil_size ) )
      end if
      ! Compute horizontal advective update
      call invoke( poly_advective_kernel_type(u_grad_f(mesh_index), wind,      &
                                              advective_av(mesh_index)) )
      call wind%copy_field(consistent_wind)
      if ( consistent_metric ) then
        chi_sph => get_coordinates(mesh_id)
        call invoke( poly_advective_kernel_type(                               &
                                              wt_metrics(mesh_index),          &
                                              wind,                            &
                                              advective_av_chi3(mesh_index) ), &
        ! Modify advecting wind
                     consistent_wind_kernel_type(consistent_wind, wind,        &
                                                 wt_metrics(mesh_index), chi_sph) )
      end if
      ! Compute vertical component of advective update
      call invoke( poly1d_vert_adv_kernel_type( u_grad_f(mesh_index),          &
                                                consistent_wind,               &
                                                adv_field,                     &
                                                vert_adv_coeffs,               &
                                                ndata_v,                       &
                                                fv_advective_order,            &
                                                logspace_loc ) )

    case(operators_fem)
      ! Use project u.grad(adv_field) into the adv_field function space
      qr = quadrature_xyoz_type(nqp_exact, quadrature_rule)
      mesh              => rhs%get_mesh()
      reference_element => mesh%get_reference_element()
      ! Quadrature rule on lateral faces only
      qrf = quadrature_face_type(nqp_exact, .true., .false., &
                                 reference_element, quadrature_rule)
      call invoke( setval_c(u_grad_f(mesh_index), 0.0_r_def),                  &
                   rtheta_kernel_type(u_grad_f(mesh_index),                    &
                                      adv_field, wind, qr),                    &
                   rtheta_bd_kernel_type(u_grad_f(mesh_index), adv_field,      &
                                         1, wind, 1, qrf) )
    end select

    ! Apply monotonicity (as if this is a full timestep)
    if ( enforce_monotonicity ) then
      if ( operators == operators_fem ) then
        mono_stencil = 1
      else
        mono_stencil = fv_advective_order/2
      end if
      call invoke( monotonic_update_kernel_type(u_grad_f(mesh_index),          &
                                                adv_field,                     &
                                                mono_stencil, mt_lumped_inv,   &
                                                dt, fv_advective_order) )
    end if

    call invoke( X_times_Y(rhs, u_grad_f(mesh_index), mt_lumped_inv) )

    nullify( chi_sph, mt_lumped_inv, reference_element, mesh,    &
             adv_coeffs, vert_adv_coeffs, chi3_at_wt,            &
             w1_multiplicity, ndata_h, ndata_v,                  &
             adv_stencil_extent, adv_2d_stencil_size )

  end subroutine advective_update_alg

  !> @brief Returns a pointer to the advective stencil extent
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The advective stencil extent
  function get_adv_stencil_extent(mesh_id) result(adv_stencil_extent)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    integer(kind=i_def),    pointer :: adv_stencil_extent

    mesh_index = find_mesh_index(mesh_id)
    adv_stencil_extent => adv_stencil_extent_list(mesh_index)

  end function get_adv_stencil_extent

  !> @brief Returns a pointer to the advective 2D stencil size
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The advective 2D stencil size
  function get_adv_2d_stencil_size(mesh_id) result(adv_2d_stencil_size)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    integer(kind=i_def),    pointer :: adv_2d_stencil_size

    mesh_index = find_mesh_index(mesh_id)
    adv_2d_stencil_size => adv_2d_stencil_size_list(mesh_index)

  end function get_adv_2d_stencil_size

  !> @brief Returns a pointer to the number of horizontal data points
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The ndata_h integer
  function get_adv_ndata_h(mesh_id) result(ndata_h)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    integer(kind=i_def),    pointer :: ndata_h

    mesh_index = find_mesh_index(mesh_id)
    ndata_h => ndata_h_list(mesh_index)

  end function get_adv_ndata_h

  !> @brief Returns a pointer to the number of vertical data points
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The ndata_v integer
  function get_adv_ndata_v(mesh_id) result(ndata_v)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    integer(kind=i_def),    pointer :: ndata_v

    mesh_index = find_mesh_index(mesh_id)
    ndata_v => ndata_v_list(mesh_index)

  end function get_adv_ndata_v

  !> @brief Returns a pointer to the advective coefficients
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The advective coefficients field
  function get_adv_coeffs(mesh_id) result(adv_coeffs)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    type(field_type),       pointer :: adv_coeffs

    mesh_index = find_mesh_index(mesh_id)
    adv_coeffs => adv_coeffs_list(mesh_index)

  end function get_adv_coeffs

  !> @brief Returns a pointer to the vertical advective coefficients
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The vertical advective coefficient field
  function get_vert_adv_coeffs(mesh_id) result(vert_adv_coeffs)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    type(field_type),       pointer :: vert_adv_coeffs

    mesh_index = find_mesh_index(mesh_id)
    vert_adv_coeffs => vert_adv_coeffs_list(mesh_index)

  end function get_vert_adv_coeffs

  !> @brief Returns a pointer to the chi3 at Wtheta
  !> @param[in] mesh_id the ID of the mesh to get the object for
  !> @return The chi3_at_wt field
  function get_chi3_at_wt(mesh_id) result(chi3_at_wt)
    implicit none
    integer(kind=i_def), intent(in) :: mesh_id
    integer(kind=i_def)             :: mesh_index
    type(field_type),       pointer :: chi3_at_wt

    mesh_index = find_mesh_index(mesh_id)
    chi3_at_wt => chi3_at_wt_list(mesh_index)

  end function get_chi3_at_wt

end module advective_update_alg_mod
