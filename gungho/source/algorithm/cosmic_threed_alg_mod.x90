!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!-------------------------------------------------------------------------------
!> @brief Algorithm for 3D COSMIC transport scheme.
!> @details The algorithm performs a horizontal update of density using Cosmic
!>          and then does a single Cosmic update in the vertical. The algorithm
!>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
!>          transport of rho. This algorithm outputs increment which is added
!>          to rho_n using rho_n - dt*increment.
module cosmic_threed_alg_mod

  use cosmic_divergence_alg_mod,        only: cosmic_divergence_alg
  use cusph_cosmic_transport_alg_mod,   only: cusph_cosmic_transport_step
  use cusph_fv_density_update_alg_mod,  only: cusph_fv_density_update_alg
  use field_mod,                        only: field_type
  use finite_element_config_mod,        only: element_order
  use flux_direction_mod,               only: x_direction, &
                                              y_direction, &
                                              z_direction
  use fs_continuity_mod,                only: W2, W3
  use function_space_mod,               only: function_space_type
  use function_space_collection_mod,    only: function_space_collection
  use io_config_mod,                    only: subroutine_timers
  use geometric_constants_mod,          only: get_coordinates, &
                                              get_cell_orientation
  use timer_mod,                        only: timer
  use timestepping_config_mod,          only: dt
  use vert_conservative_cosmic_alg_mod, only: vert_conservative_cosmic_alg

  implicit none

  private

  public :: cosmic_threed_transport_step

contains

  !> @brief Algorithm for 3D COSMIC transport scheme.
  !> @details The algorithm performs a horizontal update of density using Cosmic
  !>          and then does a single Cosmic update in the vertical. The algorithm
  !>          may be defined as rho_np1 = Z(rho_xy) with rho_xy the horizontal
  !>          transport of rho.
  !> @param[in,out] increment         Density increment
  !> @param[in]     rho               Density field
  !> @param[in]     dep_pts_x         Departure points in x direction
  !> @param[in]     dep_pts_y         Departure points in y direction
  !> @param[in]     dep_pts_z         Departure points in z direction
  !> @param[in]     detj_at_w2        Det(J) at W2 dofs
  !> @param[in]     cell_orientation  Orientation of halo cells
  subroutine cosmic_threed_transport_step( increment,     &
                                           rho,           &
                                           dep_pts_x,     &
                                           dep_pts_y,     &
                                           dep_pts_z,     &
                                           detj_at_w2,    &
                                           cell_orientation )

    implicit none

    type(field_type), intent(inout) :: increment
    type(field_type), intent(in)    :: rho
    type(field_type), intent(in)    :: dep_pts_x
    type(field_type), intent(in)    :: dep_pts_y
    type(field_type), intent(in)    :: dep_pts_z
    type(field_type), intent(in)    :: detj_at_w2
    type(field_type), intent(in)    :: cell_orientation


    type(field_type) :: rho_xy
    type(field_type) :: increment_xy
    type(field_type) :: increment_z
    type(field_type) :: mass_flux_z

    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()

    w2_fs => dep_pts_x%get_function_space()
    w3_fs => rho%get_function_space()

    call rho_xy%initialise( vector_space = w3_fs )
    call increment_xy%initialise( vector_space = w3_fs )
    call increment_z%initialise( vector_space = w3_fs )
    call mass_flux_z%initialise( vector_space = w2_fs )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    ! Calculate increment in horizontal (x-y) direction.
    call cusph_cosmic_transport_step( increment_xy, rho, dep_pts_x, dep_pts_y, &
                                      detj_at_w2, cell_orientation )

    call invoke( X_minus_bY( rho_xy, rho, dt, increment_xy ) )

    ! Update density in the vertical given the horizontally transported density.
    call vert_conservative_cosmic_alg( mass_flux_z, rho_xy, dep_pts_z, dt )

    call cosmic_divergence_alg( increment_z, mass_flux_z, detj_at_w2,       &
                                cell_orientation, z_direction )

    call invoke( X_plus_Y( increment, increment_xy, increment_z ) )

    if ( subroutine_timers ) call timer('3D Cosmic update')

    nullify( w2_fs, w3_fs )

  end subroutine cosmic_threed_transport_step

end module cosmic_threed_alg_mod
