!-------------------------------------------------------------------------------
! (C) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!>@brief Contains code for Smagorinsky diffusion calculation
module smagorinsky_alg_mod

  use constants_mod,            only: i_def
  use field_mod,                only: field_type
  use field_collection_mod,     only: field_collection_type
  use mr_indices_mod,           only: nummr, imr_v, imr_cl

  use formulation_config_mod,    only: use_moisture
  use section_choice_config_mod, only: boundary_layer,     &
                                       boundary_layer_um
  use mixing_config_mod,         only: mix_factor
  use timestepping_config_mod,   only: dt

  use tracer_smagorinsky_diff_kernel_mod, only: tracer_smagorinsky_diff_kernel_type
  use momentum_smagorinsky_kernel_mod,    only: momentum_smagorinsky_kernel_type

  use log_mod,                   only: log_event,         &
                                       LOG_LEVEL_INFO

  use io_config_mod,             only: subroutine_timers, &
                                       write_conservation_diag
  use timer_mod,                 only: timer
  use moisture_conservation_alg_mod, &
                                 only: moisture_conservation_alg
  use geometric_constants_mod,   only: get_height, get_coordinates_xyz
  use physical_op_constants_mod, only: get_delta_at_wtheta
  use fs_continuity_mod,         only: W3, Wtheta

  implicit none

  private

  public :: smagorinsky_alg

contains

  !> @brief Diffuse prognostic fields using Smagorinsky coefficients
  !> @details Either uses Smagorinsky diffusion coefficients calculated
  !>          in the UM boundary-layer scheme or those calculate locally
  !>          to diffuse quantities in the horizontal
  !> @param[in,out] mr             mixing ratios
  !> @param[in,out] theta          Potential temperature
  !> @param[in,out] u              3D wind field
  !> @param[in,out] visc_m         Smag diffusion coef for momentum
  !> @param[in,out] visc_h         Smag diffusion coef for heat
  !> @param[in]     derived_fields Group of derived fields
  !> @param[in]     rho            Density
  !> @param[in]     step           Model timestep
  subroutine smagorinsky_alg(mr, theta, u, visc_m,                   &
                             visc_h, derived_fields, rho, step )

    implicit none

    ! Prognostic fields
    type( field_type ), intent(inout) :: u, theta
    type( field_type ), intent(inout) :: visc_m, visc_h
    type( field_type ), intent(inout) :: mr ( nummr )

    type( field_collection_type ), intent(in) :: derived_fields

    integer( i_def ),   intent(in) :: step
    type( field_type ), intent(in) :: rho

    ! Increments on state fields
    type( field_type ) :: du, dtheta

    type( field_type ), pointer :: shear => null()
    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: chi_xyz(:) => null()
    type( field_type ), pointer :: delta => null()
    type( field_type ) :: dmr_v, dmr_cl

    ! Stencil depth for the Smagorinsky diffusion kernel
    integer(kind=i_def), parameter :: smag_stencil_depth = 1

    if ( subroutine_timers ) call timer("smagorinsky_alg")

    call log_event( 'Applying Smagorinsky mixing', LOG_LEVEL_INFO )

    call du%initialise( u%get_function_space() )
    call dtheta%initialise( theta%get_function_space() )

    height_wth => get_height(Wtheta)
    height_w3 => get_height(W3)
    chi_xyz => get_coordinates_xyz()

    if ( boundary_layer == boundary_layer_um ) then

      ! Use stability-dependent diffusion coefficient from UM BL scheme
      ! This will be the blended BL-Smag coefficient if mixing_option='blending'
      ! Vertical mixing is done by the BL scheme
      call log_event( 'Using stability-dependent Smagorinsky coefficient', LOG_LEVEL_INFO )

      ! Potential temperature:
      call invoke( tracer_smagorinsky_diff_kernel_type( dtheta,               &
                                                        theta,                &
                                                        smag_stencil_depth,   &
                                                        visc_h,               &
                                                        chi_xyz ) )
      if ( use_moisture ) then
        ! Water vapour and cloud liquid:
        call dtheta%copy_field_properties(dmr_v)
        call dtheta%copy_field_properties(dmr_cl)
        call invoke( tracer_smagorinsky_diff_kernel_type( dmr_v,              &
                                                          mr(imr_v),          &
                                                          smag_stencil_depth, &
                                                          visc_h,             &
                                                          chi_xyz ),          &
                     tracer_smagorinsky_diff_kernel_type( dmr_cl,             &
                                                          mr(imr_cl),         &
                                                          smag_stencil_depth, &
                                                          visc_h,             &
                                                          chi_xyz ) )
      end if

      ! Momentum:
      call invoke( momentum_smagorinsky_kernel_type( du,                   &
                                                     u,                    &
                                                     smag_stencil_depth,   &
                                                     height_w3,            &
                                                     smag_stencil_depth,   &
                                                     height_wth,           &
                                                     smag_stencil_depth,   &
                                                     visc_m,               &
                                                     chi_xyz ) )

    else ! Without UM BL scheme

      call log_event( 'Using pure Smagorinsky coefficient', LOG_LEVEL_INFO )
      ! Calculate visc_h and visc_m as (mix_factor * delta)**2 * shear
      shear => derived_fields%get_field('shear')
      delta => get_delta_at_wtheta()

      ! Potential temperature:
      call invoke( a_times_X( visc_h, mix_factor, delta ),                    &
                   inc_X_powint_n( visc_h, 2 ),                               &
                   inc_X_times_Y( visc_h, shear ),                            &
                   tracer_smagorinsky_diff_kernel_type( dtheta,               &
                                                        theta,                &
                                                        smag_stencil_depth,   &
                                                        visc_h,               &
                                                        chi_xyz ) )
      if (use_moisture) then
        ! Water vapour and cloud liquid:
        call dtheta%copy_field_properties(dmr_v)
        call dtheta%copy_field_properties(dmr_cl)
        call invoke( tracer_smagorinsky_diff_kernel_type( dmr_v,              &
                                                          mr(imr_v),          &
                                                          smag_stencil_depth, &
                                                          visc_h,             &
                                                          chi_xyz ),          &
                     tracer_smagorinsky_diff_kernel_type( dmr_cl,             &
                                                          mr(imr_cl),         &
                                                          smag_stencil_depth, &
                                                          visc_h,             &
                                                          chi_xyz ) )
      end if

      ! Momentum:
      call invoke( a_times_X( visc_m, mix_factor, delta ),                    &
                   inc_X_powint_n( visc_m, 2 ),                               &
                   inc_X_times_Y( visc_m, shear ),                            &
                   momentum_smagorinsky_kernel_type( du,                      &
                                                     u,                       &
                                                     smag_stencil_depth,      &
                                                     height_w3,               &
                                                     smag_stencil_depth,      &
                                                     height_wth,              &
                                                     smag_stencil_depth,      &
                                                     visc_m,                  &
                                                     chi_xyz ) )

    end if ! With or without BL scheme

    if (use_moisture) then
      call invoke( inc_X_plus_bY( mr(imr_v), dt, dmr_v ),    &
                   inc_X_plus_bY( mr(imr_cl), dt, dmr_cl ) )
    end if

    ! Apply increments from Smagorinsky mixing
    call invoke( inc_X_plus_bY( theta, dt, dtheta ),      &
                 inc_X_plus_bY( u,     dt, du ) )

    if (write_conservation_diag .and. use_moisture) &
      call moisture_conservation_alg( step, rho, mr, 'After mixing' )

    if ( subroutine_timers ) call timer("smagorinsky_alg")

  end subroutine smagorinsky_alg

end module smagorinsky_alg_mod
