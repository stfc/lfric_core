!-----------------------------------------------------------------------------
! (C) Crown copyright 2018 Met Office. All rights reserved.
! For further details please refer to the file COPYRIGHT.txt
! which you should have received as part of this distribution.
!-----------------------------------------------------------------------------
!
!-------------------------------------------------------------------------------
!> @brief Calculates the mass flux in the z direction.
!> @details The algorithm outputs the vertical mass flux at timestep n+1 (np1)
!>          given the wind fields at timestep n and n+1 and the density field at
!>          timestep n. A positive mass flux in the z direction refers to an
!>          upward flow (i.e. positive z direction with outward normal).
module vertical_fluxes_alg_mod

  use constants_mod,                     only: r_def
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use calc_departure_wind_kernel_mod,    only: calc_departure_wind_kernel_type
  use vertical_trapezoidal_kernel_mod,   only: vertical_trapezoidal_kernel_type
  use vert_conservative_cosmic_alg_mod,  only: vert_conservative_cosmic_alg
  use timestepping_config_mod,           only: dt

  implicit none

  private

  public :: vertical_fluxes_alg

contains

  !> @brief Calculates the mass flux in the z direction.
  !> @details The algorithm outputs the vertical mass flux at timestep n+1 (np1)
  !>          given the wind fields at timestep n and n+1 and the density field at
  !>          timestep n. A positive mass flux in the z direction refers to an
  !>          upward flow (i.e. positive z direction with outward normal).
  !> @param[out] mass_flux_z       Mass flux in z direction
  !> @param[in]  rho               Density at time level n
  !> @param[in]  u_n               Wind field at time level n
  !> @param[in]  u_np1             Wind field at time level n+1
  !> @param[in]  chi               Coordinate field
  subroutine vertical_fluxes_alg(  mass_flux_z,        &
                                   rho,                &
                                   u_n,                &
                                   u_np1,              &
                                   chi )

    implicit none

    type(field_type), intent(inout)    :: mass_flux_z
    type(field_type), intent(in)       :: rho
    type(field_type), intent(in)       :: u_n
    type(field_type), intent(in)       :: u_np1
    type(field_type), intent(in)       :: chi(3)

    type(function_space_type), pointer :: w2_fs => null()

    type(field_type) :: departure_wind_n
    type(field_type) :: departure_wind_np1
    type(field_type) :: dep_pts_z

    w2_fs => mass_flux_z%get_function_space()

    departure_wind_n   = field_type( vector_space = w2_fs )
    departure_wind_np1 = field_type( vector_space = w2_fs )
    dep_pts_z          = field_type( vector_space = w2_fs )

    ! Calculate departure points in the z direction.
    call invoke( setval_c( mass_flux_z, -987.0_r_def ),                             &
                 setval_c( dep_pts_z, -987.0_r_def ),                               &
                 calc_departure_wind_kernel_type( departure_wind_n, u_n, chi ),     &
                 calc_departure_wind_kernel_type( departure_wind_np1, u_np1, chi ), &
                 vertical_trapezoidal_kernel_type( dep_pts_z, departure_wind_n,     &
                                                             departure_wind_np1 )   )

    ! Calculate mass flux in the z direction.
    call vert_conservative_cosmic_alg( mass_flux_z, rho, dep_pts_z, dt )

    nullify( w2_fs )

  end subroutine vertical_fluxes_alg

end module vertical_fluxes_alg_mod
