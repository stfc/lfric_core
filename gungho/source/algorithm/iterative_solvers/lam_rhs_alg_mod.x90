!-----------------------------------------------------------------------------
! (C) Crown copyright 2021 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> @brief   RHS term operations for limited area models.

module lam_rhs_alg_mod

  use constants_mod,               only: r_def
  use matrix_vector_kernel_mod,    only: matrix_vector_kernel_type
  use dg_matrix_vector_kernel_mod, only: dg_matrix_vector_kernel_type
  use enforce_bc_kernel_mod,       only: enforce_bc_kernel_type
  use io_config_mod,               only: subroutine_timers
  use timer_mod,                   only: timer
  use derived_config_mod,          only: bundle_size
  use si_operators_alg_mod,        only: get_ptheta2,       &
                                         get_rho_at_u
  use fem_constants_mod,           only: get_div,           &
                                         get_mass_matrix,   &
                                         w2_id, w3inv_id,   &
                                         wt_id,             &
                                         dl_id,             &
                                         w2_si_id,          &
                                         get_normalisation
  use limited_area_constants_mod,  only: get_mask
  use fs_continuity_mod,           only: W2, W3, Wtheta
  use field_mod,                   only: field_type
  use field_collection_mod,        only: field_collection_type
  use operator_mod,                only: operator_type
  use field_indices_mod,           only: igh_u, igh_t, igh_d, igh_p

  implicit none

  private
  public :: calc_rhs_lbc
  public :: apply_mask_rhs

contains

  !> @brief   Apply LAM masks to the RHS.
  !> @details Set the RHS to zero outside the limited area model interior.
  !> @param[in,out] rhs The full RHS, to be used in the linear mixed solver
  subroutine apply_mask_rhs(rhs)

    implicit none

    type(field_type), intent(inout) :: rhs(bundle_size)
    type(field_type), pointer       :: w2_mask     => null(), &
                                       w3_mask     => null(), &
                                       wtheta_mask => null()
    ! LAM Masks
    w2_mask     => get_mask(W2)
    w3_mask     => get_mask(W3)
    wtheta_mask => get_mask(Wtheta)

    call invoke( inc_X_times_Y( rhs(igh_u),     &
                                w2_mask),       &
                 inc_X_times_Y( rhs(igh_d),     &
                                w3_mask),       &
                 inc_X_times_Y( rhs(igh_p),     &
                                w3_mask),       &
                 inc_X_times_Y( rhs(igh_t),     &
                                wtheta_mask)    )

    nullify( w2_mask, w3_mask, wtheta_mask )

  end subroutine apply_mask_rhs

  !> @brief   Compute the RHS LBC term.
  !> @details Apply the LHS mixed operator to the lateral boundary condition, to
  !>          be used as an additional RHS term, rhs = A(x_b). This code is
  !>          similar to the operator constructor in mixed_operator_alg_mod.x90,
  !>          but noting that x_b is only non-zero for the wind field.
  !> @param[in,out] rhs        RHS term for the mixed solve, to include LBCs
  !> @param[in]     lbc_fields Field collection containing LBC data
  subroutine calc_rhs_lbc( rhs, lbc_fields )

    implicit none

    type(field_type),            intent(inout) :: rhs(bundle_size)
    type(field_collection_type), intent(in)    :: lbc_fields

    type(field_type),    pointer :: boundary_u => null()
    type(operator_type), pointer :: mm_vel     => null(), &
                                    mm_w3_inv  => null(), &
                                    div        => null(), &
                                    ptheta2    => null()
    type(field_type),    pointer :: rho_at_u      => null(), &
                                    mt_lumped_inv => null(), &
                                    m2_diag       => null()
    type(field_type)             :: f_star, &
                                    u_tmp,  &
                                    r_tmp
    real(r_def),       parameter :: minus_one = -1.0_r_def
    type(field_type),    pointer :: w2_mask     => null(), &
                                    w3_mask     => null(), &
                                    wtheta_mask => null()

    if ( subroutine_timers ) call timer('rhs_lbc')

    ! Get the LBC data
    boundary_u => lbc_fields%get_field('boundary_u_diff')

    ! LAM Masks
    w2_mask     => get_mask(W2)
    w3_mask     => get_mask(W3)
    wtheta_mask => get_mask(Wtheta)

    ! Obtain operators from runtime constants
    mm_w3_inv     => get_mass_matrix(w3inv_id)
    div           => get_div()
    mt_lumped_inv => get_normalisation(wt_id)
    m2_diag       => get_normalisation(w2_id)
    mm_vel        => get_mass_matrix(w2_si_id) ! = m2 (or dl) + tau_u*dt*coriolis

    ! Obtain operators from SI operators
    ptheta2  => get_ptheta2()
    rho_at_u => get_rho_at_u()
    call f_star%initialise( vector_space = rhs(igh_u)%get_function_space() )
    call u_tmp%initialise( vector_space = rhs(igh_u)%get_function_space() )
    call r_tmp%initialise( vector_space = rhs(igh_d)%get_function_space() )

    call invoke( name = "Compute_rhs_lbc",                         &
         ! Initialise field that are incremented
                      setval_c( u_tmp, 0.0_r_def ),                &
                      setval_c( rhs(igh_t), 0.0_r_def ),           &
                      setval_c( rhs(igh_u), 0.0_r_def ),           &
         ! Initialise rhs pressure
                      setval_c( rhs(igh_p), 0.0_r_def ),           &
         ! L_u
                      matrix_vector_kernel_type(   u_tmp,          &
                                                   boundary_u,     &
                                                   mm_vel ),       &
                      inc_aX_plus_Y(               minus_one,      &
                                                   rhs(igh_u),     &
                                                   u_tmp),         &
                      enforce_bc_kernel_type(      rhs(igh_u) ),   &
         ! L_theta
                      matrix_vector_kernel_type(   rhs(igh_t),     &
                                                   boundary_u,     &
                                                   ptheta2 ),      &
         ! L_rho
                      X_times_Y(                   f_star,         &
                                                   rho_at_u,       &
                                                   boundary_u ),   &
                      dg_matrix_vector_kernel_type(r_tmp,          &
                                                   f_star,         &
                                                   div ),          &
                      dg_matrix_vector_kernel_type(rhs(igh_d),     &
                                                   r_tmp,          &
                                                   mm_w3_inv ),    &
         ! Normalise lu and lt
                      inc_X_times_Y(               rhs(igh_u),     &
                                                   m2_diag),       &
                      inc_X_times_Y(               rhs(igh_t),     &
                                                   mt_lumped_inv), &
         ! Lateral boundaries
                      inc_X_times_Y(               rhs(igh_u),     &
                                                   w2_mask),       &
                      inc_X_times_Y(               rhs(igh_d),     &
                                                   w3_mask),       &
                      inc_X_times_Y(               rhs(igh_t),     &
                                                   wtheta_mask)    &
                      )

    nullify ( boundary_u, w2_mask, w3_mask, wtheta_mask )
    nullify ( mm_w3_inv, div, mt_lumped_inv, m2_diag, mm_vel )
    nullify ( ptheta2, rho_at_u )

    if ( subroutine_timers ) call timer('rhs_lbc')

  end subroutine calc_rhs_lbc

end module lam_rhs_alg_mod
