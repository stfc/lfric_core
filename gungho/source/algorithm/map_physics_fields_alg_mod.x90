!-------------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------

!>@brief Map fields to spaces required by the FD physics routines
module map_physics_fields_alg_mod

  use constants_mod,            only: r_def, i_def
  use field_mod,                only: field_type
  use field_collection_mod,     only: field_collection_type
  use physics_mappings_alg_mod, only: map_physics_winds, map_physics_scalars
  use moist_dyn_mod,            only: num_moist_factors, total_mass
  use extract_w_kernel_mod,     only: extract_w_kernel_type
  use smagorinsky_shear_kernel_mod, only: smagorinsky_shear_kernel_type
  use mixing_config_mod,            only: smagorinsky
  use physics_config_mod,        only: blayer_placement,               &
                                       blayer_placement_slow,          &
                                       convection_placement,           &
                                       convection_placement_slow
  use geometric_constants_mod,  only: get_da_at_w2, get_height, &
                                      get_coordinates, get_panel_id
  use fs_continuity_mod,        only: W3, Wtheta

  use log_mod,                   only: log_event,         &
                                       LOG_LEVEL_INFO
  use print_field_stats_alg_mod, only: print_field_stats_alg

  use io_config_mod,             only: subroutine_timers
  use timer_mod,                 only: timer

  implicit none

  private

  public :: map_physics_fields_alg

contains

  !> @details An algorithm for mapping native FE fields to the lowest order
  !>          equivalents for use in the FD physics codes.
  !> @param[in]    u 3D wind field
  !> @param[in]    exner Pressure
  !> @param[in]    rho Density
  !> @param[in]    theta Potential temperature
  !> @param[in]    moist_dyn  Moist dynamics factors
  !> @param[inout] derived_fields Group of derived fields
  subroutine map_physics_fields_alg(u, exner, rho, theta, moist_dyn, derived_fields)

    implicit none
    ! Prognostic fields
    type( field_type ), intent(in)               :: u, rho, theta, exner
    type( field_type ), intent(in)               :: moist_dyn(num_moist_factors)
    type( field_collection_type ), intent(inout) :: derived_fields

    type( field_type ), pointer :: theta_in_w3 => null()
    type( field_type ), pointer :: theta_star => null()
    type( field_type ), pointer :: rho_in_wth => null()
    type( field_type ), pointer :: exner_in_wth => null()
    type( field_type ), pointer :: wetrho_in_wth => null()
    type( field_type ), pointer :: wetrho_in_w3 => null()
    type( field_type ), pointer :: u1_in_w3 => null()
    type( field_type ), pointer :: u2_in_w3 => null()
    type( field_type ), pointer :: u3_in_w3 => null()
    type( field_type ), pointer :: u1_in_w3_star => null()
    type( field_type ), pointer :: u2_in_w3_star => null()
    type( field_type ), pointer :: u_physics => null()
    type( field_type ), pointer :: u_physics_star => null()
    type( field_type ), pointer :: w_physics => null()
    type( field_type ), pointer :: shear => null()
    type( field_type ), pointer :: dA => null()
    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: chi(:) => null()
    type( field_type ), pointer :: panel_id => null()
    integer(kind=i_def) :: mesh_id
    if ( subroutine_timers ) call timer("map_physics_fields")

    mesh_id = u%get_mesh_id()

    theta_in_w3 => derived_fields%get_field('theta_in_w3')
    call map_physics_scalars(theta_in_w3, theta)

    rho_in_wth => derived_fields%get_field('rho_in_wth')
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    call map_physics_scalars(rho_in_wth,   rho)
    call map_physics_scalars(exner_in_wth, exner)

    ! create wetrho which is needed by the physics
    wetrho_in_wth => derived_fields%get_field('wetrho_in_wth')
    call invoke(X_times_Y(wetrho_in_wth, rho_in_wth, moist_dyn(total_mass)))
    wetrho_in_w3 => derived_fields%get_field('wetrho_in_w3')
    call map_physics_scalars(wetrho_in_w3, wetrho_in_wth)

    ! Now the winds (NB if sample_physics_winds=.false. then the winds are
    ! not re-oriented and so they are not necessarily perpendicular or
    ! aligned lat-lon)
    u1_in_w3 => derived_fields%get_field('u1_in_w3')
    u2_in_w3 => derived_fields%get_field('u2_in_w3')
    u3_in_w3 => derived_fields%get_field('u3_in_w3')
    call map_physics_winds(u1_in_w3, u2_in_w3, u3_in_w3, u)

    u_physics => derived_fields%get_field('u_physics')
    w_physics => derived_fields%get_field('w_physics')
    ! map dynamics winds into physics space
    dA => get_da_at_w2(mesh_id)
    call invoke(         X_divideby_Y(u_physics, u, dA), &
                extract_w_kernel_type(w_physics, u_physics) )
    call u_physics%log_minmax(LOG_LEVEL_INFO, 'u_physics')
    call print_field_stats_alg(u1_in_w3, LOG_LEVEL_INFO, &
                               printmin=-150.0_r_def, printmax=150.0_r_def)
    call print_field_stats_alg(u2_in_w3, LOG_LEVEL_INFO, &
                               printmin=-100.0_r_def, printmax=100.0_r_def)
    call print_field_stats_alg(w_physics, LOG_LEVEL_INFO, &
                               printmin=-10.0_r_def, printmax=10.0_r_def)

    if ( smagorinsky ) then

      call log_event( 'Calculate 3D shear', LOG_LEVEL_INFO )
      ! Calculate 3D shear
      shear => derived_fields%get_field('shear')
      height_wth => get_height(Wtheta, mesh_id)
      height_w3 => get_height(W3, mesh_id)
      chi => get_coordinates(mesh_id)
      panel_id => get_panel_id(mesh_id)
      call invoke(smagorinsky_shear_kernel_type( shear,         &
                                                 u_physics,  1, &
                                                 height_wth, 1, &
                                                 height_w3,  1, &
                                                 chi,           &
                                                 panel_id     ) )
    end if

    if (blayer_placement == blayer_placement_slow .or.                        &
         convection_placement == convection_placement_slow) then
      ! Set up predictors with start of timestep variables
      theta_star => derived_fields%get_field('theta_star')
      u_physics_star => derived_fields%get_field('u_physics_star')
      u1_in_w3_star => derived_fields%get_field('u1_in_w3_star')
      u2_in_w3_star => derived_fields%get_field('u2_in_w3_star')
      call invoke(setval_X(theta_star, theta),                                &
                  setval_X(u_physics_star, u_physics),                        &
                  setval_X(u1_in_w3_star, u1_in_w3),                          &
                  setval_X(u2_in_w3_star, u2_in_w3) )
    end if

    if ( subroutine_timers ) call timer("map_physics_fields")

  end subroutine map_physics_fields_alg

end module map_physics_fields_alg_mod
