!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!-----------------------------------------------------------------------------
!> @brief A Runge-Kutta time-discretisation of the density transport equation
!         and routines to do splitting approach.
!> @details An algorithm for timstepping the 3D transport equation using a
!>          multistage explicit Runge-Kutta algorithm and also to perform
!>          a splitting strategy.
!> @todo There are number of workarounds in this algorithm for aspects which
!!       aren't yet supported by PSyclone/LFRic:
!!       1. Issue #868 Multidata fields are not natively supported in PSyclone.
!!                     Currently we treat these as normal fields but need to
!!                     additionally pass in ndata_h or ndata_v to the kernels
!!                     as extra integer arguments.
!!       2. Issue #1246 Region stencils require both the global maximum size of
!!                      the stencil and local column size of the stencil.
!!                      PSyclone currently only passes in the local size and so
!!                      until this is resolved we also pass in the global maximum
!!                      size as an integer argument.
!!       3. Issue #1248 There is no PSyclone support for passing logical types
!!                      to kernels and so as an itermediate solution we create
!!                      an integer argument that = 1 (.true.) or = 0 (.false.).
!-----------------------------------------------------------------------------
module rk_transport_rho_mod

  use constants_mod,                     only: r_def, i_def, l_def, tiny_eps
  use log_mod,                           only: log_event,         &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_ERROR
  use fs_continuity_mod,                 only: W2, W3
  use finite_element_config_mod,         only: element_order
  use runge_kutta_init_mod,              only: num_rk_stage, ak
  use fem_constants_mod,                 only: get_inverse_w3_mass_matrix,     &
                                               get_detj_at_w2,                 &
                                               get_qr

  use transport_config_mod,              only: fv_flux_order,                  &
                                               oned_reconstruction,            &
                                               cfl_mol_3d_stab

  ! PsyKAl PSYClone kernels
  use rrho_kernel_mod,                   only: rrho_kernel_type
  use rtheta_kernel_mod,                 only: rtheta_kernel_type
  use poly_advective_kernel_mod,         only: poly_advective_kernel_type
  use poly1d_flux_kernel_mod,            only: poly1d_flux_kernel_type
  use poly1d_vert_flux_kernel_mod,       only: poly1d_vert_flux_kernel_type
  use poly2d_flux_kernel_mod,            only: poly2d_flux_kernel_type

  ! Derived Types
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use quadrature_xyoz_mod,               only: quadrature_xyoz_type
  use operator_mod,                      only: operator_type

  ! Log
  use io_config_mod,                     only: subroutine_timers
  use timer_mod,                         only: timer
  use matrix_vector_kernel_mod,          only: matrix_vector_kernel_type
  use runtime_tools_mod,                 only: find_mesh_index

  implicit none

  private

  logical(kind=l_def) :: variables_allocated = .false.

  ! 'State' items that need to be created once but used every step
  type(field_type), allocatable :: rho_n(:)
  type(field_type), allocatable :: r_rho(:)
  type(field_type), allocatable :: rho_inc(:)
  type(field_type), allocatable :: mass_flux(:)
  type(field_type), allocatable :: rr_prediction(:,:)
  type(field_type), allocatable :: wind2(:)

  public :: rk_transport_rho_set_num_meshes
  public :: rk_transport_rho_init
  public :: rk_transport_rho_step
  public :: rk_transport_rho_multistep
  public :: rk_transport_rho_final

contains

  !=============================================================================
  !> @brief Allocates the lists of fields used in this module
  !> @param[in] number_of_meshes Number of meshes to have coefficients on.
  !=============================================================================
  subroutine rk_transport_rho_set_num_meshes( number_of_meshes )

    implicit none

    integer(kind=i_def), intent(in) :: number_of_meshes

    if (.not. variables_allocated) then
      if (.not. allocated(rho_n)) allocate(rho_n(number_of_meshes))
      if (.not. allocated(r_rho)) allocate(r_rho(number_of_meshes))
      if (.not. allocated(rho_inc)) allocate(rho_inc(number_of_meshes))
      if (.not. allocated(mass_flux)) allocate(mass_flux(number_of_meshes))
      if (.not. allocated(rr_prediction)) allocate(rr_prediction(num_rk_stage, number_of_meshes))
      if (.not. allocated(wind2)) allocate(wind2(number_of_meshes))
      variables_allocated = .true.
    else
      call log_event('Variables have already been allocated for ' // &
                     'rk_transport_rho', LOG_LEVEL_ERROR)
    end if

  end subroutine

  !=============================================================================
  !> @brief Initialisation routine to set up fields for the Runge-Kutta
  !>        (or Method of Line) transport scheme on a particular mesh.
  !> @param[in]    mesh_id Mesh id of mesh of the model
  !=============================================================================
   subroutine rk_transport_rho_init( mesh_id )

    use flux_alg_mod,          only: flux_alg_init

    implicit none

    integer(kind=i_def), intent(in) :: mesh_id

    integer(kind=i_def) :: mesh_index, stage

    type(function_space_type), pointer :: u_fs     => null()
    type(function_space_type), pointer :: rho_fs   => null()

    mesh_index = find_mesh_index(mesh_id)

    ! Get function spaces for this mesh
    rho_fs => function_space_collection%get_fs( mesh_id, element_order, W3 )
    u_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )

    ! Initialise the fields
    call rho_n(mesh_index)%initialise( vector_space = rho_fs )
    call r_rho(mesh_index)%initialise( vector_space = rho_fs )
    call rho_inc(mesh_index)%initialise( vector_space = rho_fs )
    call mass_flux(mesh_index)%initialise( vector_space = u_fs )
    call wind2(mesh_index)%initialise( vector_space = u_fs )

    do stage = 1, num_rk_stage
      call rr_prediction(stage, mesh_index)%initialise( vector_space = rho_fs )
    end do

    ! Set up all the flux coefficients from flux_alg
    call flux_alg_init(mesh_id)

    nullify( u_fs, rho_fs )

  end subroutine rk_transport_rho_init

  !=============================================================================
  !> @brief A Runge-Kutta time-discretisation of the density transport equation.
  !> @param[in]     u 3D wind field
  !> @param[in,out] rho Density-like tracer
  !> @param[in]     dts Local time-step, which could be different from model timestep
  !=============================================================================
  subroutine rk_transport_rho_step( u, rho, dts )

    use flux_alg_mod,    only: get_flux_coeffs,          &
                               get_vert_flux_coeffs,     &
                               get_flux_stencil_extent,  &
                               get_flux_2d_stencil_size, &
                               get_flux_ndata_h,         &
                               get_flux_ndata_v
    implicit none

    ! Prognostic fields
    type(field_type),  intent(in)    :: u
    type(field_type),  intent(inout) :: rho
    real(kind=r_def),  intent(in)    :: dts

    ! Field pointers
    type(field_type), pointer :: flux_coeffs => null()
    type(field_type), pointer :: vert_flux_coeffs => null()
    ! The quadrature object
    type(quadrature_xyoz_type), pointer :: qr => null()

    ! Other integers
    integer(kind=i_def) :: stage, st, mesh_id, mesh_index
    integer(kind=i_def) :: logspace
    integer(kind=i_def) :: stencil_extent
    integer(kind=i_def), pointer :: flux_stencil_extent => null()
    integer(kind=i_def), pointer :: flux_2d_stencil_size => null()
    integer(kind=i_def), pointer :: ndata_h => null()
    integer(kind=i_def), pointer :: ndata_v => null()

    ! Temps for retrieving objects back from runtime constants
    type(operator_type), pointer :: mm_w3_inv => null()

    if ( subroutine_timers ) call timer('rk_transport_rho_alg')

    mesh_id = u%get_mesh_id()
    mesh_index = find_mesh_index(mesh_id)

    flux_stencil_extent => get_flux_stencil_extent(mesh_id)
    flux_2d_stencil_size => get_flux_2d_stencil_size(mesh_id)
    ndata_h => get_flux_ndata_h(mesh_id)
    ndata_v => get_flux_ndata_v(mesh_id)
    flux_coeffs => get_flux_coeffs(mesh_id)
    vert_flux_coeffs => get_vert_flux_coeffs(mesh_id)
    qr => get_qr()

    ! Create operators needed for timestepping
    mm_w3_inv => get_inverse_w3_mass_matrix(mesh_id)

    call invoke( setval_X(rho_n(mesh_index), rho) )

    ! Runge-Kutta algorithm
    do stage = 1,num_rk_stage
      ! Compute advection terms
      ! Set default mass flux ( F = rho * u ) to be 0 and then update the mass flux
      ! in cases where the wind is nonzero using an upwind reconstruction
      call invoke( setval_c(mass_flux(mesh_index), 0.0_r_def) )
      if ( oned_reconstruction ) then
         call invoke( poly1d_flux_kernel_type(mass_flux(mesh_index),   &
                                              u, rho,                  &
                                              flux_stencil_extent,     &
                                              flux_coeffs,             &
                                              fv_flux_order, ndata_h)  )
      else
        ! Use 2d flux code
        stencil_extent = fv_flux_order/2_i_def
        call invoke( poly2d_flux_kernel_type( mass_flux(mesh_index),   &
                                              u, rho,                  &
                                              stencil_extent,          &
                                              flux_coeffs,             &
                                              ndata_h,                 &
                                              flux_2d_stencil_size) )

      end if

      ! Compute vertical components of mass flux
      logspace = 0_i_def
      call invoke( poly1d_vert_flux_kernel_type( mass_flux(mesh_index),  &
                                                 u, rho,                 &
                                                 vert_flux_coeffs,       &
                                                 ndata_v, fv_flux_order, &
                                                 logspace) )

      ! Compute rrho = div(F)
      call invoke( rrho_kernel_type(rr_prediction(stage,mesh_index), &
                                    mass_flux(mesh_index), qr),      &
                   setval_c(r_rho(mesh_index),   0.0_r_def)          )

      do st = 1, stage
        call invoke( inc_X_plus_bY(r_rho(mesh_index), ak(stage,st), &
                                   rr_prediction(st,mesh_index)) )
      end do

      ! Invert mass matrices and add increments

      call invoke( name = "Invert mass matrices and add increments",        &
                   setval_c(rho_inc(mesh_index), 0.0_r_def),                &
                   matrix_vector_kernel_type(rho_inc(mesh_index),           &
                                             r_rho(mesh_index), mm_w3_inv), &
                   aX_plus_Y(rho, dts, rho_inc(mesh_index), rho_n(mesh_index))                   )

    end do

    nullify( mm_w3_inv, flux_stencil_extent, flux_2d_stencil_size, &
             ndata_h, ndata_v, flux_coeffs, vert_flux_coeffs, qr )
    if ( subroutine_timers ) call timer('rk_transport_rho_alg')

  end subroutine rk_transport_rho_step

  !=============================================================================
  !> @brief A routine to transport a density using multiple substeps (RK-method)
  !>        depending on the maximum Courant number.
  !> @param[in]     u 3D wind field
  !> @param[in,out] rho Density like tracer
  !> @param[in]     dt The model timestep length
  !=============================================================================
  subroutine rk_transport_rho_multistep(wind, density, dt)

   implicit none

   type(field_type), intent(in)       :: wind
   type(field_type), intent(inout)    :: density
   real(kind=r_def), intent(in)       :: dt

   type(function_space_type), pointer :: w2_fs => null()
   type(field_type),          pointer :: detj_at_w2 => null()

   real(kind=r_def)    :: dt_substep
   real(kind=r_def)    :: cfl_min, cfl_max
   integer(kind=i_def) :: sub_step_count, mol_substeps
   integer(kind=i_def) :: mesh_id, mesh_index

   if ( subroutine_timers ) call timer('rk_transport_rho_multistep')

   mesh_id = wind%get_mesh_id()
   mesh_index = find_mesh_index(mesh_id)
   w2_fs =>  wind%get_function_space()
   detj_at_w2 => get_detj_at_w2(mesh_id)

   call invoke( X_divideby_Y(wind2(mesh_index), wind, detj_at_w2), &
                inc_a_times_X(dt, wind2(mesh_index) )            )
   call wind2(mesh_index)%field_minmax(cfl_min, cfl_max)
   cfl_max = max(abs(cfl_max),abs(cfl_min))
   mol_substeps = int(cfl_max/(cfl_mol_3d_stab+tiny_eps),i_def) + 1_i_def
   dt_substep = dt/real(mol_substeps, r_def)

   do sub_step_count = 1, mol_substeps
      call rk_transport_rho_step( wind, density, dt_substep)
   end do

   nullify( w2_fs, detj_at_w2 )

   if ( subroutine_timers ) call timer('rk_transport_rho_multistep')

  end subroutine rk_transport_rho_multistep

  !===========================================================================
  !> @brief Deallocate arrays used in transport.
  !===========================================================================
  subroutine rk_transport_rho_final()

    implicit none

    if (allocated(wind2)) deallocate(wind2)
    if (allocated(rr_prediction)) deallocate(rr_prediction)
    if (allocated(mass_flux)) deallocate(mass_flux)
    if (allocated(rho_inc)) deallocate(rho_inc)
    if (allocated(r_rho)) deallocate(r_rho)
    if (allocated(rho_n)) deallocate(rho_n)

  end subroutine rk_transport_rho_final

end module rk_transport_rho_mod
