!-------------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown,
! Met Office and NERC 2014.
! However, it has been created with the help of the GungHo Consortium,
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-------------------------------------------------------------------------------

!>@brief A Runge-Kutta time-discretisation of the transport equation
!>@details An algorithm for timstepping the 3D transport equation using a
!>         multistage explicit Runge-Kutta algorithm.
module rk_transport_mod

  use constants_mod,                     only: r_def, i_def
  use log_mod,                           only: log_event,         &
                                               log_scratch_space, &
                                               LOG_LEVEL_INFO,    &
                                               LOG_LEVEL_TRACE

  ! Configuration and restart/checkpoint options
  use finite_element_config_mod,         only: element_order
  use restart_control_mod,               only: restart_type
  use solver_config_mod,                 only: method,         &
                                               preconditioner, &
                                               solver_preconditioner_diagonal
  use timestepping_config_mod,           only: dt
  use runge_kutta_init_mod,              only: num_rk_stage, ak
  use runtime_constants_mod,             only: get_mass_matrix, get_qr

  ! PsyKAl-lite kernels
  use psykal_lite_mod,                   only: invoke_axpy,                   &
                                               invoke_copy_field_data,        &
                                               invoke_set_field_scalar

  ! PsyKAl PSYClone kernels
  use rrho_kernel_mod,                   only: rrho_kernel_type
  use flux_rhs_kernel_mod,               only: flux_rhs_kernel_type
  use enforce_bc_kernel_mod,             only: enforce_bc_kernel_type

  ! Derived Types
  use field_mod,                         only: field_type
  use function_space_mod,                only: function_space_type
  use function_space_collection_mod,     only: function_space_collection
  use quadrature_mod,                    only: quadrature_type, GAUSSIAN
  use operator_mod,                      only: operator_type

  ! Algorithms
  use solver_mod,                        only: solver_algorithm

  ! Handles
  use fs_continuity_mod,                 only: W0, W1

  implicit none

  private

  ! 'State' items that need to be created once but used every step
  type( field_type ) :: rho_n, r_u, r_rho, rho_inc, mass_flux
  type( field_type ), allocatable ::       rr_prediction(:)

  public :: rk_transport_init
  public :: rk_transport_step


contains

  !> @brief Init routine for rk transport timestepping algorithm
  !> @details Rho and u fields are initialised before
  !>          this algorithm is called. State items are created,
  !> @param[in]    mesh_id Mesh id of mesh on which the model runs
  !> @param[in]    u The 3D wind field
  !> @param[inout] rho The density
  subroutine rk_transport_init( mesh_id, u, rho)

    implicit none

    ! Mesh id
    integer(i_def),  intent(in) :: mesh_id
    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho

    type(function_space_type), pointer :: u_fs     => null()
    type(function_space_type), pointer :: rho_fs   => null()

    ! Timestepping variables
    integer :: stage

    allocate ( rr_prediction(num_rk_stage) )

    !=== Section 1: Create field objects =====================================!
    ! Local fields
    u_fs     => function_space_collection%get_fs(mesh_id, element_order, &
                                                 u%which_function_space())
    rho_fs   => function_space_collection%get_fs(mesh_id, element_order, &
                                                 rho%which_function_space())


    rho_n        = field_type( vector_space = rho_fs )
    r_rho        = field_type( vector_space = rho_fs )
    rho_inc      = field_type( vector_space = rho_fs )
    r_u          = field_type( vector_space = u_fs )
    mass_flux    = field_type( vector_space = u_fs )

    do stage = 1,num_rk_stage
      rr_prediction(stage) = field_type( vector_space = rho_fs )
    end do


    call invoke_set_field_scalar(0.0_r_def, mass_flux)


  end subroutine rk_transport_init

  !> @brief A Runge-Kutta time-discretisation of the transport equation
  !> @details An algorithm for timstepping the transport equation using the
  !>          Runge-Kutta algorithm. rho and u fields are initialised before
  !>          this algorithm is called.
  !> @param[in]    mesh_id Mesh id of mesh on which the model runs
  !> @param[inout] chi  The finite element form of the coordinates
  !> @param[in]    u The 3D wind field
  !> @param[inout] rho The density

  subroutine rk_transport_step( mesh_id, chi, u, rho)

    implicit none

    ! Mesh id
    integer(i_def),  intent(in) :: mesh_id
    ! Coordinate fields
    type( field_type ), intent( inout ) :: chi(3)
    ! Prognostic fields
    type( field_type ), intent( inout ) :: u, rho

    ! the quadrature object
    type( quadrature_type )          :: qr

    integer(i_def) :: stage, st


    ! temps for retrieving objects back from runtime constants
    type(operator_type)       :: mm_w2

    qr = get_qr()

    ! Create operators needed for timestepping
    mm_w2 = get_mass_matrix(2)

    call invoke_set_field_scalar(0.0_r_def, rho_n)
    call invoke_copy_field_data(rho,rho_n)

    ! Runge-Kutta algorithm
    do stage = 1,num_rk_stage
      ! Compute advection terms
      ! mass flux ( F = rho * u )
      call invoke_set_field_scalar(0.0_r_def, r_u)
      call invoke(flux_rhs_kernel_type( r_u, u, rho, chi, qr ) )
      call invoke(enforce_bc_kernel_type( r_u ) )

      call invoke_set_field_scalar(0.0_r_def, mass_flux)
      call solver_algorithm( mass_flux, r_u, mesh_id, chi, method, mm=mm_w2 )

      ! Compute rrho = div(F)
      call invoke( rrho_kernel_type  ( rr_prediction(stage), mass_flux, qr ) )

      call invoke_set_field_scalar(0.0_r_def, r_rho)

      do st = 1, stage
        call invoke_axpy(ak(stage,st), rr_prediction(st),r_rho, r_rho)
      end do

      ! Invert mass matrices
      call solver_algorithm( rho_inc, r_rho, mesh_id, chi, method, qr=qr )

      ! Add increments
      call invoke_axpy(dt, rho_inc, rho_n, rho)
    end do


  end subroutine rk_transport_step

end module rk_transport_mod
