!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the mesh collection object
!>
module mesh_collection_mod_test

    use constants_mod,        only : i_def, r_def, str_def
    use global_mesh_mod,      only : global_mesh_type
    use partition_mod,        only : partition_type
    use global_mesh_collection_mod,                                         &
                              only : global_mesh_collection_type            &
                                   , global_mesh_collection
    use mesh_collection_mod,  only : mesh_collection_type,                  &
                                     mesh_collection
    use mesh_mod,             only : mesh_type, PLANE_BI_PERIODIC
    use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public ::mesh_collection_test_type
    private
    type(global_mesh_type), pointer :: global_mesh => null()
    type(partition_type)   :: partition
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type mesh_collection_test_type

  integer(i_def), parameter :: nlayers    = 5_i_def
  real(r_def),    parameter :: domain_top = 10000.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use reference_element_mod, only : reference_cube

    implicit none

    class(mesh_collection_test_type), intent(inout) :: this

    integer(i_def) :: global_test_mesh_id

    call reference_cube()

    ! Create top level collections
    global_mesh_collection = global_mesh_collection_type()


    global_test_mesh_id = global_mesh_collection%add_unit_test_global_mesh()
    this%global_mesh => global_mesh_collection%get_global_mesh( global_test_mesh_id )
    this%partition   = partition_type()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(mesh_collection_test_type), intent(inout) :: this

    call deallocate_reference()
    call global_mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use extrusion_config_mod, only : extrusion_method_uniform

    implicit none

    class(mesh_collection_test_type), intent(inout) :: this

    integer(i_def) :: mesh_id, mesh_test_id
    type(mesh_type), pointer :: mesh, mesh_test
    integer(i_def) :: ncells_2d_with_ghost, num_layers


    ! Create a mesh collection
    mesh_collection = mesh_collection_type()

    ! Add a couple of meshes to the collection
    mesh_id = mesh_collection%add_new_mesh( this%global_mesh, &
                                            this%partition,   &
                                            nlayers,          &
                                            domain_top,       &
                                            extrusion_method_uniform )

    mesh_test_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    ! Two consecutively created meshes should have consecutive ids
    @assertTrue ( mesh_test_id /= mesh_id )

    ! Extract the second mesh using its mesh id
    mesh => mesh_collection%get_mesh( mesh_test_id )

    ! Do some tests to check we have the mesh we expect (9 cells, 3 vert levels)
    ncells_2d_with_ghost = mesh%get_ncells_2d_with_ghost()
    @assertEqual ( 9, ncells_2d_with_ghost )

    num_layers = mesh%get_nlayers()
    @assertEqual ( 3, num_layers )

    ! Extract the first mesh in the list using its mesh id
    mesh => mesh_collection%get_mesh( mesh_id )

    ! Do some tests to check we have the mesh we expect (9 cells, 5 vert levels)
    ncells_2d_with_ghost = mesh%get_ncells_2d_with_ghost()
    @assertEqual ( 9, ncells_2d_with_ghost )

    num_layers = mesh%get_nlayers()
    @assertEqual ( 5, num_layers )
 
    call mesh_collection%clear()

  end subroutine test_all

end module mesh_collection_mod_test
