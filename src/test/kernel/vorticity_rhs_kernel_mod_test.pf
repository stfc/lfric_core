!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the vorticity rhs computation
!>
module vorticity_rhs_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W0, W1, W2
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: vorticity_rhs_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: chi(3)
    type(field_type)                   :: rhs, u
    real(r_def), allocatable           :: basis_u(:,:,:,:),       &
                                          diff_basis_xi(:,:,:,:), &
                                          diff_basis_chi(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type vorticity_rhs_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(vorticity_rhs_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: rhs_p, u_p


    integer(i_def) :: ndf_xi, dim_diff_xi,  &
                      ndf_u, dim_u,          &
                      ndf_chi, dim_diff_chi, &
                      nqp_h, nqp_v

    integer(i_def) :: mesh_id

    integer :: i

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs(mesh_id, element_order, W0)
    this%w1_fs => function_space_collection%get_fs(mesh_id, element_order, W1)
    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)

    this%u = field_type( vector_space = this%w2_fs )
    u_p    = this%u%get_proxy()
    ndf_u  = u_p%vspace%get_ndf( )
    dim_u  = u_p%vspace%get_dim_space( )

    this%rhs    = field_type( vector_space = this%w1_fs )
    rhs_p       = this%rhs%get_proxy()
    ndf_xi      = rhs_p%vspace%get_ndf( )
    dim_diff_xi = rhs_p%vspace%get_dim_space_diff( )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do
    ndf_chi      = chi_p(1)%vspace%get_ndf( )
    dim_diff_chi = chi_p(1)%vspace%get_dim_space_diff( )

    allocate( this%basis_u(dim_u, ndf_u, nqp_h, nqp_v), &
              this%diff_basis_xi(dim_diff_xi, ndf_xi, nqp_h, nqp_v), &
              this%diff_basis_chi(dim_diff_chi, ndf_chi, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(vorticity_rhs_test_type), intent(inout) :: this

    deallocate( this%basis_u, this%diff_basis_xi, this%diff_basis_chi )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use vorticity_rhs_kernel_mod,   only : vorticity_rhs_code

    implicit none

    class(vorticity_rhs_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    type(field_proxy_type) :: chi_p(3)
    type(field_proxy_type) :: rhs_p, u_p


    real(r_def) :: answer

    integer :: i, j, k, cell

    integer          :: ndf_xi, undf_xi,   &
                        ndf_u, undf_u,     &
                        ndf_chi, undf_chi, &
                        nqp_h, nqp_v

    integer, pointer :: map_xi(:) => null(), &
                        map_u(:) => null(),  &
                        map_chi(:) => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*4.0_r_def
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*3.0_r_def
          chi_p(3)%data(map_chi(1) + k) = real(k)  *2.0_r_def
        end do
        cell = cell + 1
      end do
    end do

    ! Test vorticity rhs
    rhs_p = this%rhs%get_proxy()
    u_p   = this%u%get_proxy()

    ndf_u  = u_p%vspace%get_ndf( )
    undf_u = u_p%vspace%get_undf( )

    ndf_xi  = rhs_p%vspace%get_ndf( )
    undf_xi = rhs_p%vspace%get_undf( )

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf( )

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    call u_p%vspace%compute_basis_function( &
         this%basis_u, ndf_u, nqp_h, nqp_v, xp, zp)
    call rhs_p%vspace%compute_diff_basis_function( &
         this%diff_basis_xi, ndf_xi, nqp_h, nqp_v, xp, zp)
    call chi_p(1)%vspace%compute_diff_basis_function( &
         this%diff_basis_chi, ndf_chi, nqp_h, nqp_v, xp, zp)

    cell = 5
    map_u   => u_p%vspace%get_cell_dofmap( cell )
    map_xi  => rhs_p%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )

    ! Create the data
    rhs_p%data(:) = 0.0_r_def
    u_p%data(:) = 0.0_r_def
    u_p%data(map_u(5) + 1 ) = 0.0_r_def
    u_p%data(map_u(6) + 1 ) = 0.0_r_def

    call vorticity_rhs_code( rhs_p%vspace%get_nlayers(), &
                             rhs_p%data,                 &
                             u_p%data,                   &
                             chi_p(1)%data,              &
                             chi_p(2)%data,              &
                             chi_p(3)%data,              &
                             ndf_xi,                     &
                             undf_xi,                    &
                             map_xi,                     &
                             this%diff_basis_xi,         &
                             ndf_u,                      &
                             undf_u,                     &
                             map_u,                      &
                             this%basis_u,               &
                             ndf_chi,                    &
                             undf_chi,                   &
                             map_chi,                    &
                             this%diff_basis_chi,        &
                             nqp_h,                      &
                             nqp_v,                      &
                             wh,                         &
                             wv )

   ! This is only testing the vertical component of vorticity
    answer = 0.0_r_def
    do k = 0,1
      @assertEqual(answer, rhs_p%data(map_xi(9)  + (k-1) ), tol)
      @assertEqual(answer, rhs_p%data(map_xi(10) + (k-1) ), tol)
      @assertEqual(answer, rhs_p%data(map_xi(11) + (k-1) ), tol)
      @assertEqual(answer, rhs_p%data(map_xi(12) + (k-1) ), tol)
    end do

  end subroutine test_all

end module vorticity_rhs_kernel_mod_test
