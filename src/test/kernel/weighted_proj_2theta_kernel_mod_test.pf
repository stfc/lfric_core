!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the theta weighted divergence operator computation
!>
module weighted_proj_2theta_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2, W3
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_mod,                only : quadrature_type, GAUSSIAN
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: weighted_proj_2theta_test_type
    private
    integer(i_def)                     :: mesh_id
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: theta, rho
    type(operator_type)                :: proj
    real(r_def), allocatable           :: diff_basis_w2(:,:,:,:), &
                                          basis_w3(:,:,:,:), &
                                          diff_basis_w0(:,:,:,:), &
                                          basis_w2(:,:,:,:), &
                                          basis_w0(:,:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type weighted_proj_2theta_test_type

  integer(i_def), parameter :: element_order = 0

  real(r_def), parameter :: gravity = 9.80665_r_def
  real(r_def), parameter :: radius  = 6371229.0_r_def
  real(r_def), parameter :: omega   = 7.292116E-5_r_def
  real(r_def), parameter :: p_zero  = 100000.0_r_def
  real(r_def), parameter :: rd      = 287.05_r_def
  real(r_def), parameter :: cp      = 1005.0_r_def
  real(r_def), parameter :: scaling = 1.0_r_def

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config, &
                                          feign_planet_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(weighted_proj_2theta_test_type), intent(inout) :: this

    type(operator_proxy_type) :: proj_p
    type(field_proxy_type)    :: theta_p, rho_p

    integer :: nlayers, dim, diff_dim, dim_w3, diff_dim_w2, dim_w2
    integer :: ndf_w0, ndf_w2, ndf_w3
    integer :: nqp_h, nqp_v

    integer :: i

    call feign_finite_element_config( finite_element_shape_quadrilateral, &
                                      0, 1, .true., .false. )
    call feign_planet_config( gravity, radius, omega, rd, cp, p_zero, scaling )
    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level function space collection

    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_collection = mesh_collection_type()
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, element_order, W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, element_order, W2 )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id, element_order, W3 )

    this%proj = operator_type(this%w2_fs,this%w0_fs)

    this%theta = field_type( vector_space = this%w0_fs )
    theta_p    = this%theta%get_proxy()

    this%rho = field_type( vector_space = this%w3_fs )
    rho_p    = this%rho%get_proxy()

    diff_dim = theta_p%vspace%get_dim_space_diff( )
    dim      = theta_p%vspace%get_dim_space( )
    ndf_w0   = theta_p%vspace%get_ndf()
    allocate(this%diff_basis_w0(diff_dim,ndf_w0,nqp_h,nqp_v) )
    allocate(this%basis_w0(dim,ndf_w0,nqp_h,nqp_v) )

    proj_p = this%proj%get_proxy()   
 
    ndf_w2  = proj_p%fs_to%get_ndf()
    dim_w2 = proj_p%fs_to%get_dim_space( )
    diff_dim_w2 = proj_p%fs_to%get_dim_space_diff( )
    allocate(this%diff_basis_w2(diff_dim_w2 ,ndf_w2, nqp_h, nqp_v) )
    allocate(this%basis_w2(dim_w2 ,ndf_w2, nqp_h, nqp_v) )

    ndf_w3 = rho_p%vspace%get_ndf()
    dim_w3 = rho_p%vspace%get_dim_space( )
    allocate(this%basis_w3(dim_w3, ndf_w3, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(weighted_proj_2theta_test_type), intent(inout) :: this

    deallocate( this%basis_w3, this%diff_basis_w2, this%diff_basis_w0, &
                this%basis_w2, this%basis_w0)

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use weighted_proj_2theta_kernel_mod, only : weighted_proj_2theta_code
    use calc_exner_pointwise_mod,        only : calc_exner_pointwise

    implicit none

    class(weighted_proj_2theta_test_type), intent(inout) :: this

    type(operator_proxy_type) :: proj_p
    type(field_proxy_type )   :: theta_p, rho_p

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def

    integer :: i, j, k, cell
    integer(i_def) :: err

    integer :: nlayers
    integer :: ndf_w0, undf_w0, ndf_w2, ndf_w3, undf_w3
    integer :: nqp_h, nqp_v

    integer, pointer :: map_w0(:), map_w3(:)  => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def), dimension(6,8) :: answer
    real(kind=r_def)                 :: exner

    ! Test the operator kernel
    proj_p = this%proj%get_proxy()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    theta_p = this%theta%get_proxy()
    ndf_w0  = theta_p%vspace%get_ndf()
    undf_w0 = theta_p%vspace%get_undf()
    call theta_p%vspace%compute_basis_function(                    &
         this%basis_w0,ndf_w0, nqp_h, nqp_v, xp, zp )
    call theta_p%vspace%compute_diff_basis_function(                    &
         this%diff_basis_w0,ndf_w0, nqp_h, nqp_v, xp, zp )

    rho_p = this%rho%get_proxy()

    nlayers = proj_p%fs_from%get_nlayers()
    ndf_w2  = proj_p%fs_to%get_ndf()
    call proj_p%fs_to%compute_basis_function(                     &
         this%basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp ) 
    call proj_p%fs_to%compute_diff_basis_function(                     &
         this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp ) 

    ndf_w3  = rho_p%vspace%get_ndf()
    undf_w3 = rho_p%vspace%get_undf()
    call rho_p%vspace%compute_basis_function(                            &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_w0 => theta_p%vspace%get_cell_dofmap(cell)
    map_w3 => rho_p%vspace%get_cell_dofmap(cell)
    proj_p%local_stencil(:,:,:) = 0.0_r_def
    theta_p%data(:) = 300.0_r_def
    rho_p%data(:) = 1.0_r_def
    call weighted_proj_2theta_code( cell,                              &
                                    nlayers,                           &
                                    proj_p%ncell_3d,                   &
                                    proj_p%local_stencil,              &
                                    theta_p%data,                      &
                                    rho_p%data,                        &
                                    ndf_w2,                            &
                                    this%basis_w2,                     &
                                    this%diff_basis_w2,                &
                                    ndf_w0,                            &
                                    undf_w0,                           &
                                    map_w0,                            &
                                    this%basis_w0,                     &
                                    this%diff_basis_w0,                &
                                    ndf_w3,                            &
                                    undf_w3,                           &
                                    map_w3,                            &
                                    this%basis_w3,                     &
                                    nqp_h,                             &
                                    nqp_v,                             &
                                    wh,                                &
                                    wv                                 &
                                   )

    call proj_operator(answer)
    exner = calc_exner_pointwise(rho_p%data(1), theta_p%data(1))
    @assertEqual(answer*exner, proj_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine proj_operator( proj ) 
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(6,8), intent(inout) :: proj


  proj = 0.25_r_def*real(reshape( [ &
  -1,1,0,0,-1,0, &
   0,1,1,0,-1,0, &
   0,0,1,-1,-1,0,&
  -1,0,0,-1,-1,0,&
  -1,1,0,0,0,1, &
   0,1,1,0,0,1,&
   0,0,1,-1,0,1,&
  -1,0,0,-1,0,1 &
!  -1, 0, 0,-1,-1, 0, 0,-1, &
!   1, 1, 0, 0, 1, 1, 0, 0, &
!   0, 1, 1, 0, 0, 1, 1, 0, &
!   0, 0,-1,-1, 0, 0,-1,-1, &
!  -1,-1,-1,-1, 0, 0, 0, 0, &
!   0, 0, 0, 0,-1,-1,-1,-1  &
 ], shape(proj) ),r_def) 

    return
  end subroutine proj_operator

end module weighted_proj_2theta_kernel_mod_test
