!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module multiplicity_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use pFUnit_Mod

  implicit none 

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: multiplicity_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type multiplicity_test_type

  integer(i_def), parameter :: element_order = 0
contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(multiplicity_test_type), intent(inout) :: this

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(multiplicity_test_type), intent(inout) :: this

    call function_space_collection%clear()
    call mesh_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use field_mod,               only : field_type,      &
                                        field_proxy_type
    use fs_continuity_mod,       only : W1, W2
    use function_space_mod,      only : function_space_type
    use mesh_mod,                only : PLANE_BI_PERIODIC
    use multiplicity_kernel_mod, only : multiplicity_code

    implicit none

    class(multiplicity_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-12_r_def

    type(field_type) :: chi(3)
    type(field_proxy_type )   :: chi_p(3)

    type(function_space_type), pointer :: w1_fs => null()
    type(function_space_type), pointer :: w2_fs => null()

    real(r_def) :: answer

    integer(i_def) :: df, cell
    integer(i_def) :: err

    ! Variables for testing multiplicity
    type( field_type ) :: m

    integer(i_def)          :: ndf, undf

    integer(i_def), pointer :: map(:) => null()

    integer(i_def)          :: mesh_id

    type( field_proxy_type ) :: m_p

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    w1_fs => function_space_collection%get_fs( mesh_id, element_order, W1 )
    w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )

    ! Test multiplicity computation for W1
    m = field_type( vector_space = w1_fs )
    m_p    = m%get_proxy()

    ndf  = m_p%vspace%get_ndf( )
    undf = m_p%vspace%get_undf( )

    m_p%data(:) = 0.0_r_def
    do cell = 1,m_p%vspace%get_ncell()
      map => m_p%vspace%get_cell_dofmap( cell )

      call multiplicity_code(m_p%vspace%get_nlayers(), &
                            m_p%data, & 
                            ndf, & 
                            undf, &
                            map &
                            )
    end do 
    map => m_p%vspace%get_cell_dofmap( 1 )
    answer = 2.0_r_def
    do df = 1,4
      @assertEqual(answer, m_p%data(map(df) ), tol)
    end do
    answer = 4.0_r_def
    do df = 5,12
      @assertEqual(answer, m_p%data(map(df) ), tol)
    end do

    ! Test multiplicity computation for W2
    m = field_type( vector_space = w2_fs )
    m_p    = m%get_proxy()

    ndf  = m_p%vspace%get_ndf( )
    undf = m_p%vspace%get_undf( )

    m_p%data(:) = 0.0_r_def
    do cell = 1,m_p%vspace%get_ncell()
      map => m_p%vspace%get_cell_dofmap( cell )

      call multiplicity_code(m_p%vspace%get_nlayers(), &
                            m_p%data, & 
                            ndf, & 
                            undf, &
                            map &
                            )
    end do 
    map => m_p%vspace%get_cell_dofmap( 1 )
    answer = 2.0_r_def
    do df = 1,4
      @assertEqual(answer, m_p%data(map(df) ), tol)
    end do
    answer = 1.0_r_def
    @assertEqual(answer, m_p%data(map(5) ), tol)
    answer = 2.0_r_def
    @assertEqual(answer, m_p%data(map(6) ), tol)

  end subroutine test_all

end module multiplicity_kernel_mod_test
