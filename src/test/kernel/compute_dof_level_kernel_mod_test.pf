!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module compute_dof_level_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type, field_proxy_type
  use fs_continuity_mod,             only : W2
  use mesh_mod,                      only : PLANE_BI_PERIODIC
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: compute_dof_level_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_dof_level_test_type

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(compute_dof_level_test_type), intent(inout) :: this

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(compute_dof_level_test_type), intent(inout) :: this

    call mesh_collection%clear()
    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use compute_dof_level_kernel_mod, only: compute_dof_level_code

    implicit none

    class(compute_dof_level_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def

    real(r_def) :: answer

    integer(i_def) :: k
    integer(i_def) :: element_order

    ! Variables for testing kernel
    type(field_type) :: level
    type(field_proxy_type) :: l_p
    integer(i_def) :: cell, ncell, ndf, undf
    real(kind=r_def), pointer :: nodes(:,:) => null()
    integer(i_def), pointer :: map(:) => null()
    integer(i_def) :: mesh_id

    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    ! Set element order
    element_order = 0

    ! Test flux computation
    level = field_type( vector_space =                                      &
                function_space_collection%get_fs( mesh_id, element_order, W2 ) )

    ! Insert Psykal_lite unpacking code here
    l_p = level%get_proxy()
    undf = l_p%vspace%get_undf()
    ndf  = l_p%vspace%get_ndf()
    nodes => l_p%vspace%get_nodes( )
 
    ncell = l_p%vspace%get_ncell()
    ! Create the data
    l_p%data(:) = 0.0_r_def


    ! Call the kernel
    do cell = 1,ncell
      map => l_p%vspace%get_cell_dofmap(cell)
      call compute_dof_level_code(l_p%vspace%get_nlayers(),                 &
                                  l_p%data,                                 &
                                  ndf,                                      &
                                  undf,                                     &
                                  map,                                      &
                                  nodes                                     &
                                 )
    end do

    !Test the answer
    do k = 0,l_p%vspace%get_nlayers()-1
      answer = 0.5_r_def + real(k,r_def) 
      @assertEqual(answer, l_p%data(map(1) + k ), tol)
      answer = real(k,r_def)
      @assertEqual(answer, l_p%data(map(5) + k ), tol)
      answer = 1.0_r_def + real(k,r_def)
      @assertEqual(answer, l_p%data(map(6) + k ), tol)
    end do

  end subroutine test_all

end module compute_dof_level_kernel_mod_test
