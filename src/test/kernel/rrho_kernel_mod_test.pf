!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the rrho computation
!>
module rrho_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W2, W3
  use function_space_mod,            only : function_space_type
  use pFUnit_Mod
  use quadrature_mod,                only : quadrature_type, GAUSSIAN
  use reference_element_mod,         only : reference_cube, reference_element

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: rrho_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: r_rho, u
    real(r_def), allocatable           :: basis_w3(:,:,:,:), &
                                          diff_basis_w2(:,:,:,:)
    real(r_def), allocatable           :: answer(:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type rrho_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(rrho_test_type), intent(inout) :: this

    type(field_proxy_type)    :: r_rho_proxy, u_proxy


    integer(i_def) :: nqp_v, nqp_h
    integer(i_def) :: ndf_w2, ndf_w3
    integer(i_def) :: diff_dim_w2, dim_w3
    integer(i_def) :: undf_w3
    integer(i_def) :: mesh_id

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w2_fs => function_space_collection%get_fs(mesh_id, element_order, W2)
    this%w3_fs => function_space_collection%get_fs(mesh_id, element_order, W3)

    this%r_rho = field_type( vector_space = this%w3_fs )
    r_rho_proxy = this%r_rho%get_proxy()
    ndf_w3  = r_rho_proxy%vspace%get_ndf( )
    dim_w3  = r_rho_proxy%vspace%get_dim_space( )
    undf_w3 = r_rho_proxy%vspace%get_undf()
    allocate( this%basis_w3(dim_w3,ndf_w3,nqp_h,nqp_v) )
    allocate( this%answer(undf_w3) )

    this%u     = field_type( vector_space = this%w2_fs )
    u_proxy     = this%u%get_proxy()
    ndf_w2      = u_proxy%vspace%get_ndf( )
    diff_dim_w2 = u_proxy%vspace%get_dim_space_diff( )
    allocate( this%diff_basis_w2(diff_dim_w2,ndf_w2,nqp_h,nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(rrho_test_type), intent(inout) :: this

    deallocate( this%basis_w3, this%diff_basis_w2 )
    deallocate( this%answer )

    call deallocate_reference()
    call function_space_collection%clear()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use rrho_kernel_mod, only : rrho_code

    implicit none

    class(rrho_test_type), intent(inout) :: this

    real(r_def), parameter :: tol = 1.0e-6_r_def


    integer :: cell

    integer                 :: nlayers, nqp_v, nqp_h
    integer                 :: ndf_w2, ndf_w3
    integer                 :: undf_w2, undf_w3
    integer, pointer  :: map_w3(:), map_w2(:) => null()

    type(field_proxy_type) :: r_rho_proxy, u_proxy

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:) => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()


    ! Test kernel
    r_rho_proxy = this%r_rho%get_proxy()
    u_proxy     = this%u%get_proxy()

    nlayers = r_rho_proxy%vspace%get_nlayers()
    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_w3  = r_rho_proxy%vspace%get_ndf( )
    undf_w3 = r_rho_proxy%vspace%get_undf()

    ndf_w2      = u_proxy%vspace%get_ndf( )
    undf_w2     = u_proxy%vspace%get_undf()

    call r_rho_proxy%vspace%compute_basis_function(this%basis_w3, ndf_w3,     &
                                                   nqp_h, nqp_v, xp, zp)

    call u_proxy%vspace%compute_diff_basis_function(                     &
         this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp)

    ! Create the data
    u_proxy%data(:) = 2.0_r_def
    r_rho_proxy%data(:) = 0.0_r_def

    do cell = 1, r_rho_proxy%vspace%get_ncell()

       map_w3 => r_rho_proxy%vspace%get_cell_dofmap( cell )
       map_w2 => u_proxy%vspace%get_cell_dofmap( cell )

       call rrho_code( nlayers,                          &
                       r_rho_proxy%data,                 &
                       u_proxy%data,                     &
                       ndf_w3,                           &
                       undf_w3,                          &
                       map_w3,                           &
                       this%basis_w3,                         &
                       ndf_w2,                           &
                       undf_w2,                          &
                       map_w2,                           &
                       this%diff_basis_w2,                    &
                       nqp_h,                            &
                       nqp_v,                            &
                       wh,                               &
                       wv                                &
                       )
    end do 

    ! Simple test, check when u = const, div(u) = 0    
    this%answer(:) = 0.0_r_def
    @assertEqual(this%answer, r_rho_proxy%data(:), tol)

  end subroutine test_all

end module rrho_kernel_mod_test
