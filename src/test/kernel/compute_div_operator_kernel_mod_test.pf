!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

!> Test the divergence operator computation
!>
module compute_div_operator_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2, W3
    use function_space_mod,            only : function_space_type
    use operator_mod,                  only : operator_type, operator_proxy_type
    use pFUnit_Mod
    use quadrature_mod,                only : quadrature_type, GAUSSIAN

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: compute_div_operator_test_type
    private
    type(quadrature_type)              :: qr
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(function_space_type), pointer :: w3_fs => null()
    type(field_type)                   :: chi(3)
    type(operator_type)                :: div
    real(r_def), allocatable           :: diff_basis_w2(:,:,:,:), &
                                          basis_w3(:,:,:,:), &
                                          diff_basis_chi(:,:,:,:)
    integer(i_def)                     :: mesh_id
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type compute_div_operator_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use feign_config_mod,          only : feign_finite_element_config
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(compute_div_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: div_p
    type(field_proxy_type)    :: chi_p(3)

    integer :: nlayers, dim, diff_dim, dim_w3, diff_dim_w2
    integer :: ndf_chi, ndf_w2, ndf_w3
    integer :: nqp_h, nqp_v

    integer :: i

    call feign_finite_element_config( finite_element_shape_quadrilateral, &
                                      0, 1, .true., .false. )

    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    this%qr  = quadrature_type(3, GAUSSIAN)

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W2 )
    this%w3_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W3 )

    this%div = operator_type(this%w3_fs,this%w2_fs)

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    allocate(this%diff_basis_chi(diff_dim,ndf_chi,nqp_h,nqp_v) )

    div_p = this%div%get_proxy()   
 
    ndf_w2  = div_p%fs_from%get_ndf()
    diff_dim_w2 = div_p%fs_from%get_dim_space_diff( )
    allocate(this%diff_basis_w2(diff_dim_w2 ,ndf_w2, nqp_h, nqp_v) )

    ndf_w3 = div_p%fs_to%get_ndf()
    dim_w3 = div_p%fs_to%get_dim_space( )
    allocate(this%basis_w3(dim_w3, ndf_w3, nqp_h, nqp_v) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(compute_div_operator_test_type), intent(inout) :: this

    deallocate( this%basis_w3, this%diff_basis_w2, this%diff_basis_chi)

    call mesh_collection%clear()
    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use compute_div_operator_kernel_mod, only : compute_div_operator_code
    use mesh_mod,                        only : mesh_type

    implicit none

    class(compute_div_operator_test_type), intent(inout) :: this

    type(operator_proxy_type) :: div_p
    type(field_proxy_type )   :: chi_p(3) 

    type(mesh_type),pointer   :: mesh => null()

    real(kind=r_def), parameter :: tol = 1.0e-6_r_def
    real(kind=r_def), parameter :: dx = 6000.0_r_def, &
                                   dy = 1000.0_r_def

    integer :: i, j, k, cell
    integer(i_def) :: err

    real(r_def) :: dz, dz_test(3)
     
    integer :: nlayers
    integer :: ndf_chi, undf_chi, ndf_w2, ndf_w3
    integer :: nqp_h, nqp_v

    integer, pointer                 :: map_chi(:)  => null()

    real(kind=r_def), pointer :: xp(:,:) => null()
    real(kind=r_def), pointer :: zp(:)   => null()
    real(kind=r_def), pointer :: wh(:), wv(:) => null()
    real(kind=r_def), dimension(1,6) :: answer

    mesh => mesh_collection%get_mesh( this%mesh_id )
    call mesh%get_dz(dz_test)
    dz = dz_test(1)

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    ! Test the operator kernel
    div_p = this%div%get_proxy()

    nqp_h=this%qr%get_nqp_h()
    nqp_v=this%qr%get_nqp_v()
    zp=>this%qr%get_xqp_v()
    xp=>this%qr%get_xqp_h()
    wh=>this%qr%get_wqp_h()
    wv=>this%qr%get_wqp_v()

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()
    call chi_p(1)%vspace%compute_diff_basis_function(                    &
         this%diff_basis_chi,ndf_chi, nqp_h, nqp_v, xp, zp )

    nlayers = div_p%fs_from%get_nlayers()
    ndf_w2  = div_p%fs_from%get_ndf()
    call div_p%fs_from%compute_diff_basis_function(                     &
         this%diff_basis_w2, ndf_w2, nqp_h, nqp_v, xp, zp ) 

    ndf_w3 = div_p%fs_to%get_ndf()
    call div_p%fs_to%compute_basis_function(                            &
         this%basis_w3, ndf_w3, nqp_h, nqp_v, xp, zp ) 

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)
    div_p%local_stencil(:,:,:) = 0.0_r_def
    call compute_div_operator_code( cell,                              &
                                    nlayers,                           &
                                    div_p%ncell_3d,                    &
                                    div_p%local_stencil,               &
                                    chi_p(1)%data,                     &
                                    chi_p(2)%data,                     &
                                    chi_p(3)%data,                     &
                                    ndf_w3,                            &
                                    this%basis_w3,                     &
                                    ndf_w2,                            &
                                    this%diff_basis_w2,                &
                                    ndf_chi,                           &
                                    undf_chi,                          &
                                    map_chi,                           &
                                    this%diff_basis_chi,               &
                                    nqp_h,                             &
                                    nqp_v,                             &
                                    wh,                                &
                                    wv                                 &
                                    )

    call div_operator(answer)
    @assertEqual(answer, div_p%local_stencil(:, :, 1 ), tol)

  end subroutine test_all

  subroutine div_operator( div ) 
    use constants_mod, only: r_def
    implicit none

    real(kind=r_def), dimension(1,6), intent(inout) :: div
    real(kind=r_def), dimension(1,1) :: m3
    real(kind=r_def), dimension(1,6) :: d32

    m3 = reshape( [ &
  1.0000000000000000E+00_r_def &
    ], shape(m3) )

  d32 = real(reshape( [ &
  -1, 1, 1, -1, -1, 1 &
 ], shape(d32) ),r_def) 

  div = matmul(m3,d32)

    return
  end subroutine div_operator

end module compute_div_operator_kernel_mod_test
