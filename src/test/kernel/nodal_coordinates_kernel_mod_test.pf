!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module nodal_coordinates_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W2
  use pFUnit_Mod

  implicit none 

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: nodal_coordinates_test_type
    private
    type(field_type)         :: x(3)
    type(field_type)         :: chi(3)
    real(r_def), allocatable :: basis_chi(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type nodal_coordinates_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(nodal_coordinates_test_type), intent(inout) :: this

    type(field_proxy_type)    :: x_p(3), chi_p(3)

    integer(i_def) :: ndf_chi, ndf_x
    integer(i_def) :: dim_chi
    integer(i_def) :: mesh_id

    integer(i_def) :: i


    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    mesh_id = mesh_collection%get_id_for_new_unit_test_mesh( PLANE_BI_PERIODIC )

    do i = 1,3
      this%x(i)   = field_type( vector_space = &
             function_space_collection%get_fs( mesh_id, element_order, W2 ) )
      this%chi(i) = field_type( vector_space = &
             function_space_collection%get_fs( mesh_id, element_order, W0 ) )
      x_p(i)      = this%x(i)%get_proxy()
      chi_p(i)    = this%chi(i)%get_proxy()
    end do

    ndf_x    = x_p(1)%vspace%get_ndf( )
    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    dim_chi  = chi_p(1)%vspace%get_dim_space( )

    allocate( this%basis_chi(dim_chi, ndf_chi, ndf_x) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(nodal_coordinates_test_type), intent(inout) :: this

    deallocate( this%basis_chi )

    call function_space_collection%clear()
    call deallocate_reference()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use nodal_coordinates_kernel_mod, only: nodal_coordinates_code

    implicit none

    class(nodal_coordinates_test_type), intent(inout) :: this

    real(r_def), parameter :: dx = 6000.0_r_def, &
                              dy = 1000.0_r_def, &
                              dz = 2000.0_r_def
    real(r_def), parameter :: tol = 1.0e-6_r_def

    real(r_def) :: answer

    integer :: i, j, k, cell

    type(field_proxy_type) :: x_p(3), chi_p(3)

    integer                 :: nlayers
    integer                 :: ndf_chi, ndf_x
    integer                 :: undf_chi, undf_x
    integer                 :: element_order
    integer, pointer        :: map_chi(:), map_x(:) => null()

    real(kind=r_def), pointer :: nodes(:,:) => null()


    ! Insert Psykal_lite unpacking code here
    do i = 1,3
      x_p(i)   = this%x(i)%get_proxy()
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    nlayers = x_p(1)%vspace%get_nlayers()

    ndf_x  = x_p(1)%vspace%get_ndf( )
    undf_x = x_p(1)%vspace%get_undf()

    ndf_chi  = chi_p(1)%vspace%get_ndf( )
    undf_chi = chi_p(1)%vspace%get_undf()

    nodes => x_p(1)%vspace%get_nodes( )
    call chi_p(1)%vspace%compute_nodal_basis_function(this%basis_chi, ndf_chi, ndf_x, nodes)

    ! Create the data
    x_p(1)%data(:) = 0.0_r_def
    x_p(2)%data(:) = 0.0_r_def
    x_p(3)%data(:) = 0.0_r_def

    ! Call the kernel
    cell = 3
    map_x   => x_p(1)%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
    call nodal_coordinates_code(nlayers, &
                                x_p(1)%data, &
                                x_p(2)%data, &
                                x_p(3)%data, &
                                chi_p(1)%data, &
                                chi_p(2)%data, &
                                chi_p(3)%data, &
                                ndf_x, undf_x, map_x, &
                                ndf_chi, undf_chi, map_chi, &
                                this%basis_chi &
                               )

    !Test the answer
    answer = chi_p(1)%data(map_chi(1))
    @assertEqual(answer, x_p(1)%data(map_x(1)), tol)
    answer = 0.5_r_def*(chi_p(2)%data(map_chi(1))+chi_p(2)%data(map_chi(4)))
    @assertEqual(answer, x_p(2)%data(map_x(1)), tol)
    answer = chi_p(3)%data(map_chi(5))
    @assertEqual(answer, x_p(3)%data(map_x(6)), tol)

  end subroutine test_all

end module nodal_coordinates_kernel_mod_test
