!-----------------------------------------------------------------------------
! (c) The copyright relating to this work is owned jointly by the Crown, 
! Met Office and NERC 2014. 
! However, it has been created with the help of the GungHo Consortium, 
! whose members are identified at https://puma.nerc.ac.uk/trac/GungHo/wiki
!-----------------------------------------------------------------------------

module calc_departure_wind_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W0, W2
  use function_space_mod,            only : function_space_type
  use evaluator_xyz_mod,             only : evaluator_xyz_type
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(TestCase), public :: calc_departure_wind_test_type
    private
    type(field_type)         :: chi(3)
    type(field_type)         :: piola_field
    type(field_type)         :: departure_field
    real(r_def), allocatable :: diff_basis_chi(:,:,:)
    real(r_def), allocatable :: nodal_basis_u(:,:,:)
    type(evaluator_xyz_type) :: evaluator
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type calc_departure_wind_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_shape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC
    use reference_element_mod,     only : reference_cube, reference_element

    implicit none

    class(calc_departure_wind_test_type), intent(inout) :: this

    type(function_space_type) :: fs
    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: u_piola

    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()

    integer(i_def) :: ndf_chi, ndf
    integer(i_def) :: dim_u, diff_dim_chi

    integer(i_def) :: mesh_id

    integer :: i


    reference_element = finite_element_shape_quadrilateral
    call reference_cube()

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )
    this%piola_field =  field_type( vector_space = w2_fs )
    this%departure_field =  field_type( vector_space = w2_fs )
    u_piola = this%piola_field%get_proxy()
    ndf  = u_piola%vspace%get_ndf()

    dim_u  = u_piola%vspace%get_dim_space( )
    allocate( this%nodal_basis_u(dim_u, ndf, ndf) )

    w0_fs => function_space_collection%get_fs( mesh_id, element_order, W0 )
    do i = 1,3
      this%chi(i) = field_type( vector_space = w0_fs )
      chi_p(i)    = this%chi(i)%get_proxy()
    end do
    ndf_chi  = chi_p(1)%vspace%get_ndf()
    diff_dim_chi = chi_p(1)%vspace%get_dim_space_diff()
    allocate( this%diff_basis_chi(diff_dim_chi, ndf_chi, ndf) )

    this%evaluator = evaluator_xyz_type( w2_fs%get_ndf( ), w2_fs%get_nodes() )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use reference_element_mod, only : deallocate_reference

    implicit none

    class(calc_departure_wind_test_type), intent(inout) :: this

    deallocate( this%nodal_basis_u )
    deallocate( this%diff_basis_chi )

    call function_space_collection%clear()
    call deallocate_reference()
    call mesh_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @test
  subroutine test_all( this )

    use calc_departure_wind_kernel_mod, only: calc_departure_wind_code
    use evaluate_function_mod,          only: BASIS, DIFF_BASIS
    implicit none

    class(calc_departure_wind_test_type), intent(inout) :: this

    type(field_proxy_type)    :: u_piola, u_departure_wind, chi_p(3)

    real(kind=r_def)          :: answer, physical_wind_value
    real(r_def), parameter    :: tol = 1.0e-12_r_def
    real(r_def), parameter    :: dx = 6.0_r_def, &
                                 dy = 1.0_r_def, &
                                 dz = 2.0_r_def

    integer                   :: i, j, k, cell, df
    integer                   :: nlayers
    integer                   :: ndf_chi, ndf_u
    integer                   :: undf_chi, undf_u
    integer                   :: diff_dim_chi, dim_u
    integer                   :: element_order
    integer, pointer          :: map_chi(:), map(:) => null()
    type(evaluator_xyz_type)  :: evaluator

    ! Insert Psykal_lite unpacking code here
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    evaluator = this%evaluator

    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz
        end do
        cell = cell + 1
      end do
    end do

    u_piola = this%piola_field%get_proxy()
    u_departure_wind = this%departure_field%get_proxy()

    nlayers = u_piola%vspace%get_nlayers()

    ndf_u  = u_piola%vspace%get_ndf()
    undf_u = u_piola%vspace%get_undf()
    dim_u  = u_piola%vspace%get_dim_space( )

    ndf_chi  = chi_p(1)%vspace%get_ndf()
    undf_chi = chi_p(1)%vspace%get_undf()
    diff_dim_chi = chi_p(1)%vspace%get_dim_space_diff( )

    call evaluator%compute_evaluate( DIFF_BASIS, chi_p(1)%vspace, diff_dim_chi, ndf_chi, this%diff_basis_chi)
    call evaluator%compute_evaluate( BASIS, u_piola%vspace, dim_u, ndf_u, this%nodal_basis_u)

    cell = 1
    map     => u_piola%vspace%get_cell_dofmap( cell )
    map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )

    physical_wind_value = 3.0_r_def

    do df =  1,ndf_u
      u_piola%data(map(df)) = physical_wind_value*dot_product(this%nodal_basis_u(:,df,df),abs(this%nodal_basis_u(:,df,df)))
    end do
    u_departure_wind%data(:) = 0.0_r_def

    call calc_departure_wind_code(nlayers,                                  &
                                  u_departure_wind%data,                    &
                                  u_piola%data,                             &
                                  chi_p(1)%data,                            &
                                  chi_p(2)%data,                            &
                                  chi_p(3)%data,                            &
                                  ndf_u, undf_u, map, this%nodal_basis_u,       &
                                  ndf_chi, undf_chi, map_chi,               &
                                  this%diff_basis_chi                       &
                                  )

    answer = physical_wind_value/(dx*dy*dz)
    do df = 1,ndf_u
      @assertEqual(answer, u_departure_wind%data(map(df)), tol)
    end do

  end subroutine test_all

end module calc_departure_wind_kernel_mod_test
