!------------------------------------------------------------------------------
! (c) Crown copyright 2018 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!------------------------------------------------------------------------------
! @brief Control code for the radiative transfer scheme

module radiation_alg_mod

use field_mod,               only: field_type
use field_collection_mod,    only: field_collection_type
use fs_continuity_mod,       only: W3, Wtheta
use mr_indices_mod,          only: nummr, imr_v, imr_cl, imr_ci
use constants_mod,           only: r_def
use timestepping_config_mod, only: dt
use io_config_mod,           only: write_diag, use_xios_io
use geometric_constants_mod, only: get_latitude, get_longitude, get_height
use sw_kernel_mod,           only: sw_kernel_type
use lw_kernel_mod,           only: lw_kernel_type
use io_config_mod,           only: subroutine_timers
use timer_mod,               only: timer
use log_mod,                 only: log_event, LOG_LEVEL_INFO

implicit none
private

!------------------------------------------------------------------------------
! Contained functions/subroutines
!------------------------------------------------------------------------------
public :: radiation_alg

contains

! @param[in,out] dtheta_rad             Potential temperature increment
! @param[in]     theta                  Potential temperature field
! @param[in]     exner                  Exner pressure field in density space
! @param[in]     mr                     Mixing ratios
! @param[in]     derived_fields         Derived fields
! @param[in,out] radiation_fields       Fields for radiation scheme
! @param[in]     cloud_fields           Fields for cloud scheme
! @param[in]     convection_fields      Fields for convection scheme
! @param[in]     aerosol_fields         Fields for aerosol scheme
! @param[in]     surface_fields         Fields for surface scheme
! @param[in]     timestep               Model timestep number
! @param[in]     tile_sw_direct_albedo  Tile SW direct albedos
! @param[in]     tile_sw_diffuse_albedo Tile SW diffuse albedos
! @param[in]     tile_lw_albedo         Tile LW albedos (1 - emissivity)
! @param[in]     aer_mix_ratio          MODE aerosol mixing ratios
! @param[in]     aer_sw_absorption      MODE aerosol SW absorption
! @param[in]     aer_lw_absorption      MODE aerosol LW absorption
! @param[in]     aer_sw_scattering      MODE aerosol SW scattering
! @param[in]     aer_lw_scattering      MODE aerosol LW scattering
! @param[in]     aer_sw_asymmetry       MODE aerosol SW asymmetry
! @param[in]     aer_lw_asymmetry       MODE aerosol LW asymmetry

subroutine radiation_alg(dtheta_rad, theta, exner, mr,                    &
                         derived_fields, radiation_fields,                &
                         cloud_fields, convection_fields,                 &
                         aerosol_fields, surface_fields, timestep,        &
                         tile_sw_direct_albedo, tile_sw_diffuse_albedo,   &
                         tile_lw_albedo, aer_mix_ratio,                   &
                         aer_sw_absorption, aer_lw_absorption,            &
                         aer_sw_scattering, aer_lw_scattering,            &
                         aer_sw_asymmetry,  aer_lw_asymmetry)

  implicit none

  type( field_type ), intent( inout ) :: dtheta_rad
  type( field_type ), intent( in )    :: theta, exner, mr(nummr)
  type( field_type ), intent( in )    :: tile_sw_direct_albedo
  type( field_type ), intent( in )    :: tile_sw_diffuse_albedo
  type( field_type ), intent( in )    :: tile_lw_albedo
  type( field_type ), intent( in )    :: aer_mix_ratio
  type( field_type ), intent( in )    :: aer_sw_absorption, aer_lw_absorption
  type( field_type ), intent( in )    :: aer_sw_scattering, aer_lw_scattering
  type( field_type ), intent( in )    :: aer_sw_asymmetry,  aer_lw_asymmetry
  type( field_collection_type ), intent(in) :: derived_fields
  type( field_collection_type ), intent(inout) :: radiation_fields
  type( field_collection_type ), intent(in) :: cloud_fields
  type( field_collection_type ), intent(in) :: convection_fields
  type( field_collection_type ), intent(in) :: aerosol_fields
  type( field_collection_type ), intent(in) :: surface_fields
  integer, intent(in) :: timestep

  real(r_def) :: dt_again

  ! Unpacked fields from collections
  type( field_type ), pointer :: tile_fraction       => null()
  type( field_type ), pointer :: tile_temperature    => null()

  type( field_type ), pointer :: exner_in_wth        => null()
  type( field_type ), pointer :: rho_in_wth          => null()
  type( field_type ), pointer :: theta_in_w3         => null()

  type( field_type ), pointer :: area_fraction       => null()
  type( field_type ), pointer :: liquid_fraction     => null()
  type( field_type ), pointer :: ice_fraction        => null()
  type( field_type ), pointer :: sigma_qcw           => null()
  type( field_type ), pointer :: cca                 => null()
  type( field_type ), pointer :: ccw                 => null()

  type( field_type ), pointer :: cloud_drop_no_conc  => null()
  type( field_type ), pointer :: sulphuric           => null()

  type( field_type ), pointer :: cos_zenith_angle    => null()
  type( field_type ), pointer :: lit_fraction        => null()
  type( field_type ), pointer :: lw_down_surf        => null()
  type( field_type ), pointer :: sw_down_surf        => null()
  type( field_type ), pointer :: sw_direct_surf      => null()
  type( field_type ), pointer :: sw_down_blue_surf   => null()
  type( field_type ), pointer :: sw_direct_blue_surf => null()
  type( field_type ), pointer :: lw_up_tile          => null()
  type( field_type ), pointer :: sw_up_tile          => null()
  type( field_type ), pointer :: sw_up_blue_tile     => null()
  type( field_type ), pointer :: sw_heating_rate     => null()
  type( field_type ), pointer :: lw_heating_rate     => null()

  type( field_type ), pointer :: lw_down_surf_rts        => null()
  type( field_type ), pointer :: sw_down_surf_rts        => null()
  type( field_type ), pointer :: sw_direct_surf_rts      => null()
  type( field_type ), pointer :: sw_direct_toa_rts       => null()
  type( field_type ), pointer :: sw_down_blue_surf_rts   => null()
  type( field_type ), pointer :: sw_direct_blue_surf_rts => null()
  type( field_type ), pointer :: lw_up_tile_rts          => null()
  type( field_type ), pointer :: sw_up_tile_rts          => null()
  type( field_type ), pointer :: sw_up_blue_tile_rts     => null()
  type( field_type ), pointer :: sw_heating_rate_rts     => null()
  type( field_type ), pointer :: lw_heating_rate_rts     => null()
  type( field_type ), pointer :: cos_zenith_angle_rts    => null()
  type( field_type ), pointer :: lit_fraction_rts        => null()
  type( field_type ), pointer :: stellar_irradiance_rts  => null()

  type( field_type ), pointer :: ozone               => null()

  type( field_type ), pointer :: latitude            => null()
  type( field_type ), pointer :: longitude           => null()
  type( field_type ), pointer :: height_w3           => null()
  type( field_type ), pointer :: height_wth          => null()

  ! Local diagnostic fields
  type( field_type ) :: cloud_cover_rts, cloud_fraction_rts, &
                        cloud_droplet_re_rts

  call log_event( 'slow_physics: Running Radiation', LOG_LEVEL_INFO )
  call theta%copy_field_properties(dtheta_rad)

  tile_fraction    => surface_fields%get_field('tile_fraction')
  tile_temperature => surface_fields%get_field('tile_temperature')

  lw_up_tile       => radiation_fields%get_field('lw_up_tile')
  sw_up_tile       => radiation_fields%get_field('sw_up_tile')
  sw_up_blue_tile  => radiation_fields%get_field('sw_up_blue_tile')
  cos_zenith_angle => radiation_fields%get_field('cos_zenith_angle')
  lit_fraction     => radiation_fields%get_field('lit_fraction')
  lw_down_surf        => radiation_fields%get_field('lw_down_surf')
  sw_down_surf        => radiation_fields%get_field('sw_down_surf')
  sw_direct_surf      => radiation_fields%get_field('sw_direct_surf')
  sw_down_blue_surf   => radiation_fields%get_field('sw_down_blue_surf')
  sw_direct_blue_surf => radiation_fields%get_field('sw_direct_blue_surf')
  sw_heating_rate     => radiation_fields%get_field('sw_heating_rate')
  lw_heating_rate     => radiation_fields%get_field('lw_heating_rate')

  lw_down_surf_rts        => radiation_fields%get_field('lw_down_surf_rts')
  sw_down_surf_rts        => radiation_fields%get_field('sw_down_surf_rts')
  sw_direct_surf_rts      => radiation_fields%get_field('sw_direct_surf_rts')
  sw_direct_toa_rts       => radiation_fields%get_field('sw_direct_toa_rts')
  sw_down_blue_surf_rts   => radiation_fields%get_field('sw_down_blue_surf_rts')
  sw_direct_blue_surf_rts => radiation_fields%get_field('sw_direct_blue_surf_rts')
  lw_up_tile_rts          => radiation_fields%get_field('lw_up_tile_rts')
  sw_up_tile_rts          => radiation_fields%get_field('sw_up_tile_rts')
  sw_up_blue_tile_rts     => radiation_fields%get_field('sw_up_blue_tile_rts')
  sw_heating_rate_rts     => radiation_fields%get_field('sw_heating_rate_rts')
  lw_heating_rate_rts     => radiation_fields%get_field('lw_heating_rate_rts')
  cos_zenith_angle_rts    => radiation_fields%get_field('cos_zenith_angle_rts')
  lit_fraction_rts        => radiation_fields%get_field('lit_fraction_rts')
  stellar_irradiance_rts  => radiation_fields%get_field('stellar_irradiance_rts')

  ozone               => radiation_fields%get_field('ozone')

  exner_in_wth        => derived_fields%get_field('exner_in_wth')
  rho_in_wth          => derived_fields%get_field('rho_in_wth')
  theta_in_w3         => derived_fields%get_field('theta_in_w3')

  area_fraction       => cloud_fields%get_field('area_fraction')
  liquid_fraction     => cloud_fields%get_field('liquid_fraction')
  ice_fraction        => cloud_fields%get_field('ice_fraction')
  sigma_qcw           => cloud_fields%get_field('sigma_qcw')

  cca                 => convection_fields%get_field('cca')
  ccw                 => convection_fields%get_field('ccw')

  cloud_drop_no_conc  => aerosol_fields%get_field('cloud_drop_no_conc')
  sulphuric           => aerosol_fields%get_field('sulphuric')

  latitude  => get_latitude()
  longitude => get_longitude()
  height_wth => get_height(Wtheta)
  height_w3 => get_height(W3)

  ! Local 2D diagnostic fields
  call lw_down_surf%copy_field_properties(cloud_cover_rts)

  ! Local 3D diagnostic fields
  call lw_heating_rate%copy_field_properties(cloud_fraction_rts)
  call lw_heating_rate%copy_field_properties(cloud_droplet_re_rts)

  if ( subroutine_timers ) call timer("sw_radiation")
  call invoke( sw_kernel_type(                                              &
                 sw_heating_rate, sw_down_surf, sw_direct_surf,             &
                 sw_down_blue_surf, sw_direct_blue_surf,                    &
                 sw_up_tile, sw_up_blue_tile,                               &
                 sw_heating_rate_rts, sw_down_surf_rts, sw_direct_surf_rts, &
                 sw_direct_toa_rts,                                         &
                 sw_down_blue_surf_rts, sw_direct_blue_surf_rts,            &
                 sw_up_tile_rts, sw_up_blue_tile_rts,                       &
                 theta, exner, exner_in_wth, rho_in_wth,                    &
                 height_w3, height_wth,                                     &
                 cos_zenith_angle, lit_fraction,                            &
                 cos_zenith_angle_rts, lit_fraction_rts,                    &
                 stellar_irradiance_rts,                                    &
                 ozone,                                                     &
                 mr(imr_v), mr(imr_cl), mr(imr_ci),                         &
                 area_fraction, liquid_fraction, ice_fraction, sigma_qcw,   &
                 cca, ccw, cloud_drop_no_conc,                              &
                 tile_fraction,                                             &
                 tile_sw_direct_albedo, tile_sw_diffuse_albedo,             &
                 sulphuric, aer_mix_ratio,                                  &
                 aer_sw_absorption, aer_sw_scattering, aer_sw_asymmetry,    & 
                 latitude, longitude, timestep ) )
  if ( subroutine_timers ) call timer("sw_radiation")
  if ( subroutine_timers ) call timer("lw_radiation")
  call invoke( lw_kernel_type(                                              &
                 lw_heating_rate, lw_down_surf, lw_up_tile,                 &
                 lw_heating_rate_rts, lw_down_surf_rts, lw_up_tile_rts,     &
                 cloud_cover_rts, cloud_fraction_rts, cloud_droplet_re_rts, &
                 theta, theta_in_w3, exner, exner_in_wth, rho_in_wth,       &
                 height_w3, height_wth,                                     &
                 ozone,                                                     &
                 mr(imr_v), mr(imr_cl), mr(imr_ci),                         &
                 area_fraction, liquid_fraction, ice_fraction, sigma_qcw,   &
                 cca, ccw, cloud_drop_no_conc,                              &
                 tile_fraction, tile_temperature,                           &
                 tile_lw_albedo,                                            &
                 sulphuric, aer_mix_ratio,                                  &
                 aer_lw_absorption, aer_lw_scattering, aer_lw_asymmetry,    & 
                 latitude, longitude, timestep ) )
  if ( subroutine_timers ) call timer("lw_radiation")
  ! first calculate the total temperature increment
  dt_again=dt
  call invoke(aX_plus_bY(dtheta_rad, dt,       sw_heating_rate,  &
                                     dt_again, lw_heating_rate), &
  ! then convert it to a theta increment
              inc_X_divideby_Y(dtheta_rad, exner_in_wth) )

  ! Output diagnostics
  if (write_diag .and. use_xios_io) then
    call lw_down_surf%write_field('lw_down_surf')
    call sw_down_surf%write_field('sw_down_surf')
    call sw_direct_surf%write_field('sw_direct_surf')
    call sw_down_blue_surf%write_field('sw_down_blue_surf')
    call sw_direct_blue_surf%write_field('sw_direct_blue_surf')
    call sw_heating_rate%write_field('sw_heating_rate')
    call lw_heating_rate%write_field('lw_heating_rate')

    call lw_down_surf_rts%write_field('lw_down_surf_rts')
    call sw_down_surf_rts%write_field('sw_down_surf_rts')
    call sw_direct_surf_rts%write_field('sw_direct_surf_rts')
    call sw_direct_toa_rts%write_field('sw_direct_toa_rts')
    call sw_down_blue_surf_rts%write_field('sw_down_blue_surf_rts')
    call sw_direct_blue_surf_rts%write_field('sw_direct_blue_surf_rts')
    call sw_heating_rate_rts%write_field('sw_heating_rate_rts')
    call lw_heating_rate_rts%write_field('lw_heating_rate_rts')

    call cos_zenith_angle%write_field('cos_zenith_angle')
    call lit_fraction%write_field('lit_fraction')

    call cos_zenith_angle_rts%write_field('cos_zenith_angle_rts')
    call lit_fraction_rts%write_field('lit_fraction_rts')
    call stellar_irradiance_rts%write_field('stellar_irradiance_rts')

    call cloud_cover_rts%write_field('cloud_cover_rts')
    call cloud_fraction_rts%write_field('cloud_fraction_rts')
    call cloud_droplet_re_rts%write_field('cloud_droplet_re_rts')

    call sw_up_tile%write_field('sw_up_tile')

  end if

end subroutine radiation_alg

end module radiation_alg_mod
